
// Функция - Получает com-соединение с ДБФ статусов, чтобы при последующих операциях со статусами сразу обращаться к соединению, а не получать заново
//
// Параметры:
//  ПараметрыСоединенияВходящие	 - Структура
//		Файл - Строка (обязательный).
//		Ключ - Строка (необязательный)	 - если не указан, то Файл
//		Тип - Строка (обязательный) - для получения специфичной строки соединения DBF - Статусы/Сопоставление
//  ДопПараметры	 - Структура	 - для расширения
// 
// Возвращаемое значение:
//   - COM объект, соединение с DBF
//
Функция ПолучитьСоединениеDBF(ПараметрыСоединенияВходящие=Неопределено, ДопПараметры=Неопределено) Экспорт
	Перем КлючDBF, СоединениеСДБФ;
	МодульОбъектаСервер = МодульОбъектаСервер();
	Попытка
		Если Не ПараметрыСоединения.Свойство("СоединениеСДБФ", СоединениеСДБФ) Тогда
			СоединениеСДБФ = МодульОбъектаСервер.РаботаСДБФ_ПолучитьСоединениеСДБФ(ПараметрыСоединения.РаботаСДБФ, ПараметрыСоединения.ОписаниеСоединенияДБФ.Ключ);
			Если СоединениеСДБФ = Неопределено Тогда
				СоединениеСДБФ = МодульОбъектаСервер.РаботаСДБФ_СоздатьСоединениеСДБФ(ПараметрыСоединения.РаботаСДБФ, ПараметрыСоединения.ОписаниеСоединенияДБФ.Параметры);
			КонецЕсли;
			ПараметрыСоединения.Вставить("СоединениеСДБФ", СоединениеСДБФ);
		КонецЕсли;
		БД = МодульОбъектаСервер.РаботаСДБФ_ПолучитьСоединениеБД(ПараметрыСоединения.РаботаСДБФ, СоединениеСДБФ);
		ПроверитьФайлСтатусовДБФ(БД);
	Исключение
		ИнфоОбОшибке = ИнформацияОбОшибке();
		МодульОбъектаСервер.ВызватьСбисИсключениеСервер(ИнфоОбОшибке, "Статусы_ДБФ.ПолучитьСоединениеDBF")
	КонецПопытки;
	Возврат БД;
КонецФункции

&НаСервере
Процедура ПроверитьФайлСтатусовДБФ(СоединениеБД)
	СоединениеСДБФ			= МодульОбъектаСервер().РаботаСДБФ_ПолучитьСоединениеСДБФ(ПараметрыСоединения.РаботаСДБФ, ПараметрыСоединения.ОписаниеСоединенияДБФ.Ключ);
	ОписаниеФайла			= СоединениеСДБФ.Файлы.Получить(ПараметрыСоединения.ОписаниеСоединенияДБФ.Параметры.ИмяФайла);
	
	ИмяБазыСтатусов			= ПараметрыСоединения.ОписаниеСоединенияДБФ.Параметры.ИмяФайла;
	ИмяФайлаСтатусов		= ИмяБазыСтатусов + "." + ПараметрыСоединения.ОписаниеСоединенияДБФ.Параметры.Расширение;
	Если ОписаниеФайла.Есть = Неопределено Тогда
		ФайлСтатусов = Новый Файл(СоединениеСДБФ.Путь + ИмяФайлаСтатусов);
		ОписаниеФайла.Есть = ФайлСтатусов.Существует();
	КонецЕсли;	
	Если ОписаниеФайла.Есть Тогда	
		Если Не ОписаниеФайла.Проверен Тогда
			RecordSet = Новый ComОбъект("ADODB.Recordset");
		    Recordset.CursorLocation = 3;
			Recordset.Open("Select top 1 * from " + ИмяБазыСтатусов, СоединениеБД, 2, 3);
			
			Если		Recordset.Fields.Count = 6 Тогда
				//Всё ок
			ИначеЕсли	Recordset.Fields.Count = 4 Тогда
				ИмяБазыБэкапа	= ИмяБазыСтатусов + "OLD";
				ИмяФайлаБэкапа	= ИмяБазыБэкапа + "." + ПараметрыСоединения.ОписаниеСоединенияДБФ.Параметры.Расширение;
				Попытка
					ПереместитьФайл(СоединениеСДБФ.Путь + ИмяФайлаСтатусов, СоединениеСДБФ.Путь + ИмяФайлаБэкапа);
				Исключение
					ИнфоОбОшибке = ИнформацияОбОшибке();
					МодульОбъектаСервер().ВызватьСбисИсключениеСервер(ИнфоОбОшибке, "Статусы_ДБФ.ПроверитьФайлСтатусовДБФ.ПереместитьФайл", , "Не удалось обновить файл сопоставления");
				КонецПопытки;
				
				ОписаниеФайла.Есть = НовыйФайлСтатусовДБФ(СоединениеСДБФ, СоединениеБД).Существует();
				Если Не ОписаниеФайла.Есть Тогда
					ПереместитьФайл(СоединениеСДБФ.Путь + ИмяФайлаБэкапа, СоединениеСДБФ.Путь + ИмяФайлаСтатусов);
					МодульОбъектаСервер().ВызватьСбисИсключениеСервер(, "Статусы_ДБФ.ПроверитьФайлСтатусовДБФ", 772,, "Не удалось создать файл статусов " + СоединениеСДБФ.Путь + ПараметрыСоединения.ОписаниеСоединенияДБФ.Параметры.ИмяФайла);
				КонецЕсли;
				
				Попытка
					СоединениеБД.Execute("INSERT INTO " + ИмяБазыСтатусов + "(IDPACK, IDINPACK, IDDOC, STATE) SELECT IDPACK, IDINPACK, IDDOC, STATE FROM " + ИмяБазыБэкапа);
				Исключение
					ПереместитьФайл(СоединениеСДБФ.Путь + ИмяФайлаБэкапа, СоединениеСДБФ.Путь + ИмяФайлаСтатусов);
					ИнфоОбОшибке = ИнформацияОбОшибке();
					МодульОбъектаСервер().ВызватьСбисИсключениеСервер(ИнфоОбОшибке, "Статусы_ДБФ.ПроверитьФайлСтатусовДБФ");
				КонецПопытки;
			КонецЕсли;           
			ОписаниеФайла.Проверен	= Истина;
			Recordset.Close();
		КонецЕсли;
	Иначе
		ОписаниеФайла.Есть = НовыйФайлСтатусовДБФ(СоединениеСДБФ, СоединениеБД).Существует();
		Если Не ОписаниеФайла.Есть Тогда
			МодульОбъектаСервер().ВызватьСбисИсключениеСервер(, "Статусы_ДБФ.ПроверитьФайлСтатусовДБФ", 772,, "Не удалось создать файл статусов " + СоединениеСДБФ.Путь + ПараметрыСоединения.ОписаниеСоединенияДБФ.Параметры.ИмяФайла);
		КонецЕсли;
		ОписаниеФайла.Проверен = Истина;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция НовыйФайлСтатусовДБФ(СоединениеСДБФ, СоединениеБД)
	
	ИмяФайла = ПараметрыСоединения.ОписаниеСоединенияДБФ.Параметры.ИмяФайла + "." + ПараметрыСоединения.ОписаниеСоединенияДБФ.Параметры.Расширение;
	Попытка
		СоединениеБД.Execute("Create Table " + ИмяФайла + "(IDPACK char(120), IDINPACK char(120), IDDOC char(240), STATE char(240), IDACC char(40))");
	Исключение
		ИнфоОбОшибке = ИнформацияОбОшибке();
		МодульОбъектаСервер().ВызватьСбисИсключениеСервер(ИнфоОбОшибке, "СоединениеБД.Execute");
	КонецПопытки;
	Возврат Новый Файл(СоединениеСДБФ.Путь + ИмяФайла);
	
КонецФункции

&НаСервереБезКонтекста
Процедура  ЗавершитьСоединениеСDBFНаСервере(ПараметрыСоединения) Экспорт
	ПараметрыСоединения.Подключено = Ложь;
	ПараметрыСоединения.Удалить("Хранилище");
	ПараметрыСоединения.ВызывалосьСоздание = Ложь;
КонецПроцедуры
	
