
&НаКлиенте
Процедура WriteAttachment(АсинхроннаяСБИСКоманда, ДопПараметры) Экспорт
	
	ОшибкаВызова	= Ложь;
	
	События = Новый Структура;
	События.Вставить("Error",		Новый Структура("Функция",	"WriteDocumentEx2_Error"));
	МодульОбъектаКлиент().АсинхроннаяСбисКоманда_Установить(АсинхроннаяСБИСКоманда, "События", События);
	
	КомандаПлагина	= НовыйКомандаСБИСПлагин(ДопПараметры.Кэш, АсинхроннаяСБИСКоманда, Новый Структура("Метод", "WriteAttachment"));
	КомандаПлагина.Параметры = Новый Структура("Document", КомандаПлагина.Параметры.ДокументЗаписать);
	РезультатВызова	= СБИС_ОтправитьКомандуБезОбработки(ДопПараметры.Кэш, КомандаПлагина, Новый Структура, ОшибкаВызова);
	
	АсинхроннаяСБИСКоманда.ВремяВызова = КомандаПлагина.ВремяВызова;
	
	Если ОшибкаВызова Тогда
		МодульОбъектаКлиент().ВызватьСбисИсключение(РезультатВызова, ДопПараметры.Кэш.СБИС.ПараметрыИнтеграции.ИнтеграцияИмя + ".СБИСПлагин_ОтправитьКоманду_Асинхронно");
	КонецЕсли;
	
КонецПроцедуры

//Обработка успешной отправки пакета
&НаКлиенте
Процедура WriteAttachment_Message(РезультатВызова, ПараметрыОбработки) Экспорт
	
	МодульОбъектаКлиент().РезультатОтправки_Асинх_Ответ(РезультатВызова, ПараметрыОбработки);
	
	Если НЕ ПараметрыОбработки.Команда.АргументВызова.СоставПакета.Свойство("НеЗапускатьВДокументооборот")
		ИЛИ НЕ ПараметрыОбработки.Команда.АргументВызова.СоставПакета.НеЗапускатьВДокументооборот = Истина Тогда
		События = Новый Структура;
		События.Вставить("Message",		Новый Структура("Функция",			"WriteDocumentEx2_Message"));
		События.Вставить("AfterCall",	Новый Структура("Функция, Модуль",	"РезультатОтправки_Асинх_ДобавитьПоток", МодульОбъектаКлиент()));
		МодульОбъектаКлиент().АсинхроннаяСбисКоманда_Установить(ПараметрыОбработки.Команда, "События", События);
		
		ПараметрыОтправки	= Новый Структура("Кэш, РезультатОтправки", ПараметрыОбработки.Кэш, ПараметрыОбработки.РезультатОтправки);
		ОбработчикОтправки	= МодульОбъектаКлиент().НовыйСбисОписаниеОповещения("ExecuteActionEx", ПараметрыОбработки.Кэш.ТекущийСеанс.Модули.Интеграция, ПараметрыОтправки);
		Сбис3Команда		= МодульОбъектаКлиент().РезультатОтправки_НовыйАсинхроннаяСбисКоманда(ПараметрыОбработки.РезультатОтправки, ПараметрыОбработки.Команда.АргументВызова, ОбработчикОтправки);
		
		МодульОбъектаКлиент().АсинхроннаяСбисКоманда_Установить(Сбис3Команда, "События", События);

		МодульОбъектаКлиент().АсинхроннаяСбисКоманда_Выполнить(Сбис3Команда);
	
	КонецЕсли;
	
КонецПроцедуры

