
&НаКлиенте
Процедура WriteDocumentEx(АсинхроннаяСБИСКоманда, ДопПараметры) Экспорт
	
	ОшибкаВызова	= Ложь;
	
	КомандаПлагина	= НовыйКомандаСБИСПлагин(ДопПараметры.Кэш, АсинхроннаяСБИСКоманда, Новый Структура("Метод", "WriteDocumentEx"));
	КомандаПлагина.Параметры = Новый Структура("Document", КомандаПлагина.Параметры.ДокументОтправить);
	РезультатВызова	= СБИС_ОтправитьКомандуБезОбработки(ДопПараметры.Кэш, КомандаПлагина, Новый Структура, ОшибкаВызова);
	
	АсинхроннаяСБИСКоманда.ВремяВызова = КомандаПлагина.ВремяВызова;
	
	Если ОшибкаВызова Тогда
		МодульОбъектаКлиент().ВызватьСбисИсключение(РезультатВызова, ДопПараметры.Кэш.СБИС.ПараметрыИнтеграции.ИнтеграцияИмя + ".СБИСПлагин_ОтправитьКоманду_Асинхронно");
	КонецЕсли;
	
КонецПроцедуры

//Обработка успешной отправки пакета
&НаКлиенте
Процедура WriteDocumentEx2_Message(РезультатВызова, ПараметрыОбработки) Экспорт
	
	WriteDocumentEx2_ОбработатьВременныеФайлы(РезультатВызова, ПараметрыОбработки);
	МодульОбъектаКлиент().РезультатОтправки_Асинх_Ответ(РезультатВызова, ПараметрыОбработки);
	
КонецПроцедуры

//Обработка ошибки отправки
&НаКлиенте
Процедура WriteDocumentEx2_Error(РезультатВызова, ПараметрыОбработки) Экспорт
	
	WriteDocumentEx2_ОбработатьВременныеФайлы(РезультатВызова, ПараметрыОбработки);
	МодульОбъектаКлиент().РезультатОтправки_Асинх_Ошибка(РезультатВызова, ПараметрыОбработки)
	
КонецПроцедуры

//Вспомогательная функция, чисто под способ обмена.
&НаКлиенте
Процедура WriteDocumentEx2_ОбработатьВременныеФайлы(РезультатВызова, ПараметрыОбработки)
	
	Попытка
		ОтправкаДокументаСБИС	= ПараметрыОбработки.Команда.АргументВызова;
		ВложенияОбработаны		= МодульОбъектаКлиент().ОтправкаДокументаСБИС_Получить(ОтправкаДокументаСБИС, "ДокументОтправить").Вложение;
		Для Каждого ВложениеПакета Из ВложенияОбработаны Цикл
			КлючВременногоФайла = ВложениеПакета;
			Если	Не КлючВременногоФайла.Свойство("Файл",			КлючВременногоФайла)
				Или Не КлючВременногоФайла.Свойство("ПутьКФайлу",	КлючВременногоФайла) Тогда
				Продолжить;
			КонецЕсли;
			СбисВременныйФайл = ПараметрыОбработки.Кэш.ТекущийСеанс.ВременныеФайлы.Файлы.Получить(КлючВременногоФайла);
			Если СбисВременныйФайл = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			СбисВременныйФайл.Освободить = Истина;
		КонецЦикла;
	Исключение
		Возврат;
	КонецПопытки;
	ПараметрыОбработки.Кэш.ОбщиеФункции.СбисОчиститьВременныеФайлы();
	
КонецПроцедуры

