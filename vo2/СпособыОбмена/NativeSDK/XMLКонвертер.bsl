
//Разбор сообщения от плагина

&НаКлиенте
Функция XmlSerializer_Decode(Кэш, СтрокаXML, Отказ)
		
	Node = Новый ЧтениеXML();
	Node.УстановитьСтроку(СтрокаXML);
		
	//Если Не Node.Прочитать() Тогда
	//	Возврат Неопределено;
	//КонецЕсли;
	Попытка
		Результат = XmlSerializer_DecodeNode(Node);
	Исключение
		Отказ = Истина;
		Сообщить(ОписаниеОшибки());
		Результат = Кэш.ОбщиеФункции.СбисИсключение(,  Кэш.СБИС.ПараметрыИнтеграции.ИнтеграцияИмя + ".XmlSerializer_Decode", 773,,ИнформацияОбОшибке().Описание,Новый Структура("message", СтрокаXML));
	КонецПопытки;
	Node.Закрыть();
	Node = Неопределено;
	Возврат Результат;
КонецФункции                                      

&НаКлиенте
Функция XmlSerializer_DecodeString(str)
	
	Результат = str;
	
	Если	Сред(str,3,1) = "."
		И	Сред(str,6,1) = "." Тогда
		//Переопределим значение как дату, если это действительно она
		Если		СтрДлина(str) = 10 Тогда
			Попытка
				Результат = Дата(Сред(str,7,4), Сред(str,4,2), Лев(str, 2));
			Исключение
				//В случае исключения, ничего не делаем. Это не дата, значение не меняется
			КонецПопытки;
		ИначеЕсли	СтрДлина(str) = 19 Тогда 
			Попытка
				Результат = Дата(Сред(str,7,4), Сред(str,4,2), Лев(str, 2), Сред(str,12, 2), Сред(str,15, 2), Сред(str,18, 2))
			Исключение
				//В случае исключения, ничего не делаем. Это не дата, значение не меняется
			КонецПопытки;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция XmlSerializer_DecodeNode(Node, Parent=Неопределено)
	Результат = Неопределено;	
	Пока Node.Прочитать() Цикл 
		
		Если Node.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
			
		    ИмяУзла = Node.Имя;
		    ТипУзла = Node.ПолучитьАтрибут("xsi:type");
			
			Если ТипУзла = "Structure" Тогда
				
				Результат = Новый Структура; 
				XmlSerializer_DecodeNode(Node, Результат);
				
			ИначеЕсли ТипУзла = "Array" Тогда
				
				Результат = Новый Массив; 
				XmlSerializer_DecodeNode(Node, Результат);
				
			ИначеЕсли ТипУзла = "xs:string" Тогда
				
				Результат = XmlSerializer_DecodeNode(Node, Результат);
				Результат = XmlSerializer_DecodeString(Результат);
				
			ИначеЕсли ТипУзла = "xs:boolean" Тогда
				
				Результат = Булево(XmlSerializer_DecodeNode(Node, Результат));
				
			ИначеЕсли ТипУзла = "xs:decimal" Тогда
				
				Результат = Число(XmlSerializer_DecodeNode(Node, Результат));
				
			ИначеЕсли ТипУзла = "xs:Null" Тогда
				
				Результат = Неопределено;
				
			ИначеЕсли ИмяУзла = "Property" Тогда //ТипУзла = "Property"
				
				ИмяСвойства = Node.ПолучитьАтрибут("name");
				ЗначениеСвойства = XmlSerializer_DecodeNode(Node, Результат);
				Попытка
					Parent.Вставить(ИмяСвойства, ЗначениеСвойства);
				Исключение
					//Возможно, когда-нибудь, в следующей жизни будет нормальный овтет, который  мы сможем разобрать всегда.
					//Пропускаем некорректные ключи
					//ВызватьИсключение("Неверный формат ответа по пути " + Parent.Путь + ": некорректное значение " + Node_key + " в ключе ""name"".");	
				КонецПопытки;
			Иначе
				
				ВызватьИсключение("Неизвестный тип ответа в узле " + ТипУзла);
				
			КонецЕсли; 
			
			Если ТипЗнч(Parent) = Тип("Массив") Тогда
				Parent.Добавить(Результат);
			КонецЕсли;
		    
		ИначеЕсли Node.ТипУзла = ТипУзлаXML.Текст Тогда    

			Результат = Node.Значение;
			
		ИначеЕсли Node.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда

			Возврат Результат;
		    
		КонецЕсли;    

	КонецЦикла;
	
	Возврат Результат;
КонецФункции

