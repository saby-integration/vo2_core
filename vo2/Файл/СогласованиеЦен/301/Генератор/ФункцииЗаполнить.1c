/////////////////////////////////////////

&НаКлиенте
Процедура ЗаполнитьГенераторДаннымиВложения(Кэш, Генератор, Вложение, ДопПараметры)

	Файл_Шаблон = Кэш.ГлавноеОкно.СбисПолучитьФормуОбработки(Кэш, "Файл_Шаблон", Новый Структура, Ложь);
	ЗаполнитьОбщиеДанные(Кэш, Генератор, Вложение);
	
	Файл_Шаблон.ЗаполнитьПоИмениСтороны(Кэш, Генератор, Вложение, "Получатель",			ДопПараметры);
	Файл_Шаблон.ЗаполнитьПоИмениСтороны(Кэш, Генератор, Вложение, "Отправитель",		ДопПараметры);
	//Файл_Шаблон.ЗаполнитьПодписант(Кэш, Генератор, Вложение, ДопПараметры);
	ЗаполнитьТабличныеДанные(Кэш, Генератор, Вложение, ДопПараметры);
	
КонецПроцедуры

&НаКлиенте
Функция ИсточникДанных_ПолучитьДанныеСтороныПоИмени(Кэш, Вложение, ИмяСтороны) Экспорт
	
	Попытка
		Возврат Вложение[ИмяСтороны];	
	Исключение
		Возврат Неопределено;
	КонецПопытки;
	
КонецФункции

&НаКлиенте
Процедура ЗаполнитьОбщиеДанные(Кэш, Генератор, Вложение)
	
	Файл_Шаблон = Кэш.ГлавноеОкно.СбисПолучитьФормуОбработки(Кэш, "Файл_Шаблон", Новый Структура, Ложь);
	ТекФайл = Вложение.СтруктураДокумента.Файл;
	КоммерческаяИнфа = ТекФайл.КоммерческаяИнформация;
	
	Генератор.Вставить("Время",	Формат(ТекущаяДата(), "ДФ=ЧЧ.мм.сс"));
	
	Файл_Шаблон.УстановитьЗначениеПоКлючу(Генератор, "Дата",	ТекФайл);
	Файл_Шаблон.УстановитьЗначениеПоКлючу(Генератор, "ИдФайл",	ТекФайл);
	Файл_Шаблон.УстановитьЗначениеПоКлючу(Генератор, "Номер",	ТекФайл);
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьГенераторПоСтруктуре(Кэш, Генератор, ИмяРеквизита, СтруктураДанных)
	
	Для Каждого ТекРеквизит ИЗ СтруктураДанных Цикл
		
		Ключ = ИмяРеквизита + ТекРеквизит.Ключ;
		
		ЗначениеВГенераторе = Генератор.Получить(Ключ);
		Если ЗначениеВГенераторе = Неопределено Тогда
			Продолжить;	
		КонецЕсли;
		//Если требуется положить по новому пути, то смотрим на значение, указанное в генераторе 
		Если ЗначениеЗаполнено(ЗначениеВГенераторе) Тогда
			Генератор.Вставить(ЗначениеВГенераторе, ТекРеквизит.Значение);
			Генератор.Удалить(Ключ);
		Иначе
			Генератор.Вставить(Ключ, ТекРеквизит.Значение);
		КонецЕсли;
			//Генератор.Вставить(Ключ, ТекРеквизит.Значение);
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТабличныеДанные(Кэш, Генератор, Вложение, ДопПараметры)
	
	Файл_Шаблон = Кэш.ГлавноеОкно.СбисПолучитьФормуОбработки(Кэш, "Файл_Шаблон", Новый Структура, Ложь);

	ТабличнаяЧастьДокумента = Вложение.СтруктураДокумента.Файл.КоммерческаяИнформация.ПакетПредложений.Предложения.Предложение;
	Если ТабличнаяЧастьДокумента = Неопределено Тогда
		Возврат;	
	КонецЕсли;

	ТабличныеДанные = Новый Массив;
	ПорНомерСтроки = 0;	
	Для Каждого ТекСтрока ИЗ ТабличнаяЧастьДокумента Цикл
		ПорНомерСтроки = ПорНомерСтроки + 1;

		ТекДанные = Новый Соответствие();
		ТекДанные.Вставить("НомНомер", ПорНомерСтроки);
		
		Файл_Шаблон.УстановитьЗначениеПоКлючу(ТекДанные, "Артикул",			ТекСтрока, "Артикул");
		Файл_Шаблон.УстановитьЗначениеПоКлючу(ТекДанные, "ЕдИзм",			ТекСтрока, "БазоваяЕдиница.ЗначениеУзла");
		Файл_Шаблон.УстановитьЗначениеПоКлючу(ТекДанные, "ОКЕИ",			ТекСтрока, "БазоваяЕдиница.Атрибуты.Код");
		Файл_Шаблон.УстановитьЗначениеПоКлючу(ТекДанные, "Идентификатор",	ТекСтрока, "Ид");
		Файл_Шаблон.УстановитьЗначениеПоКлючу(ТекДанные, "Количество",		ТекСтрока);
		Файл_Шаблон.УстановитьЗначениеПоКлючу(ТекДанные, "Наименование", 	ТекСтрока);
		Файл_Шаблон.УстановитьЗначениеПоКлючу(ТекДанные, "НДСЗначение", 	ТекСтрока, "СтавкиНалогов.СтавкаНалога.Ставка");
		Файл_Шаблон.УстановитьЗначениеПоКлючу(ТекДанные, "НДСТип", 			ТекСтрока, "СтавкиНалогов.СтавкаНалога.Наименование");
		Файл_Шаблон.УстановитьЗначениеПоКлючу(ТекДанные, "Примечание", 		ТекСтрока, "Комментарий");
		Файл_Шаблон.УстановитьЗначениеПоКлючу(ТекДанные, "GTIN", 			ТекСтрока, "GTIN");
		
		ЗаполнитьТабличныеДанныеЦена(Кэш, ТекСтрока, ТекДанные); 
		
		ТабличныеДанные.Добавить(ТекДанные);
	КонецЦикла;
	
	Генератор.Вставить("ТабличныеДанные", ТабличныеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТабличныеДанныеЦена(Кэш, Источник, Приемник, Суффикс = "")

	Файл_Шаблон = Кэш.ГлавноеОкно.СбисПолучитьФормуОбработки(Кэш, "Файл_Шаблон", Новый Структура, Ложь);
	ДанныеЦен = Источник.Цены.Цена;
	Если ТипЗнч(ДанныеЦен) = Тип("Массив") Тогда
		Если ДанныеЦен.Количество() Тогда
			ДанныеЦен = ДанныеЦен[0];
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли;
		
	Файл_Шаблон.УстановитьЗначениеПоКлючу(Приемник, "Цена" + Суффикс, ДанныеЦен, "ЦенаЗаЕдиницу");
	
КонецПроцедуры

