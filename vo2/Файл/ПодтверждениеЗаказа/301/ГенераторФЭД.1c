
&НаКлиенте
Перем Файл_Шаблон_Модуль;

&НаКлиенте
Функция ИсточникДанных_ПолучитьДанныеСтороныПоИмени(Кэш, Вложение, ИмяСтороны) Экспорт
	
	Попытка
		КлючСтороны = ИмяСтороны;
		Если		ИмяСтороны = "Получатель" Тогда
			КлючСтороны = "Поставщик";
		ИначеЕсли	ИмяСтороны = "Отправитель" Тогда
			КлючСтороны = "Покупатель";
		КонецЕсли;
		Возврат Вложение.СтруктураДокумента.Файл.Документ[КлючСтороны];	
	Исключение
		Возврат Неопределено;
	КонецПопытки;
	
КонецФункции

&НаКлиенте
Функция СформироватьДокументДляГенератора(ПараметрыФормированияВходящие, Кэш) Экспорт

	Док		= ПараметрыФормированияВходящие.Документ;
	Вложение= ПараметрыФормированияВходящие.Вложение;
	
	Возврат Док;
КонецФункции

//Генератор ФЭД для формирования вложений {
&НаКлиенте
Функция СформироватьРасхождениеДляГенератора(ПараметрыФормированияВходящие, Кэш) Экспорт
	
	Док		= ПараметрыФормированияВходящие.Документ;
	Вложение= ПараметрыФормированияВходящие.Вложение;

	Файл_Шаблон_Модуль = Кэш.ГлавноеОкно.СбисПолучитьФорму("Файл_Шаблон");
	
	ТекДокумент = Док.Файл.Документ;
	ТекДокумент.Вставить("ДефНомИспрСФ",  "-");
	ТекДокумент.Вставить("ДефДатаИспрСФ", "-");
	ТекДокумент.Вставить("ПоФактХЖ", 	Вложение.Название);
	ТекДокумент.Вставить("НаимДокОпр", 	ТекДокумент.Название);
	
	Файл_Шаблон_Модуль.ЗаполнитьПоДокументПараметр(Док, Кэш);
	Файл_Шаблон_Модуль.ЗаполнитьПоДокументОснование(Док, Кэш);
	//Файл_Шаблон_Модуль.СформироватьТабличнуюЧастьТабличнаяЧастьДокументаИзменений(Кэш, ТекДокумент);
	
	Файл_Шаблон_Модуль.СформироватьОснование(Док, Кэш);
	Файл_Шаблон_Модуль.СформироватьПараметры(Док, Кэш);
	
	СформироватьОснованиеЗаказ(Кэш, ТекДокумент);
	
	ТекДокумент.Вставить("Подписант", Новый Структура("ОблПолн, Статус, ОснПолн",
		6,
		1,
		"Должностные обязанности"));
	
	Возврат Док;
	
КонецФункции

//Штатный обработчик строки подстановки
&НаКлиенте
Процедура ОбработчикДанных_ЗаполнитьСтрокуТабличнойЧастиДокумента(Аргумент, ДопПараметры) Экспорт
	Кэш					= ДопПараметры.Кэш;
	Файл_Шаблон_Модуль	= Кэш.ГлавноеОкно.СбисПолучитьФорму("Файл_Шаблон");//Установить Файл_Шаблон для дальнейшей работы
	
	Файл_Шаблон_Модуль.ЗаполнитьТабличныеДанныеЕдИзм	(Аргумент, ДопПараметры);
	Файл_Шаблон_Модуль.ЗаполнитьТабличныеДанныеУпаковка	(Аргумент, ДопПараметры);
	ЗаполнитьТабличныеДанныеНДСЗначениеНДСТип			(Аргумент, ДопПараметры);
	ЗаполнитьТабличныеДанныеАкциз						(Аргумент, ДопПараметры);
	ЗаполнитьТабличныеДанныеОбщие						(Аргумент, ДопПараметры);		
	ЗаполнитьТабличныеДанныеПредСтрТабл					(Аргумент, ДопПараметры);
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТабличныеДанныеОбщие(Аргумент, ДопПараметры) Экспорт
	ТекСтрока = Аргумент.ИсточникДанных;
	ТекДанные = Аргумент.ДанныеРезультат;
	
	Файл_Шаблон_Модуль.УстановитьЗначениеПоКлючу(ТекДанные, "GTIN", 			ТекСтрока);
	Файл_Шаблон_Модуль.УстановитьЗначениеПоКлючу(ТекДанные, "Количество", 		ТекСтрока, "Кол_во");		
	Файл_Шаблон_Модуль.УстановитьЗначениеПоКлючу(ТекДанные, "Наименование", 	ТекСтрока, "Название");
	Файл_Шаблон_Модуль.УстановитьЗначениеПоКлючу(ТекДанные, "ПорНомер", 		ТекСтрока);
	Файл_Шаблон_Модуль.УстановитьЗначениеПоКлючу(ТекДанные, "НомНомер", 		ТекСтрока);
	Файл_Шаблон_Модуль.УстановитьЗначениеПоКлючу(ТекДанные, "НомНомерСтор", 	ТекСтрока);
	Файл_Шаблон_Модуль.УстановитьЗначениеПоКлючу(ТекДанные, "Примечание", 		ТекСтрока);
	Файл_Шаблон_Модуль.УстановитьЗначениеПоКлючу(ТекДанные, "СуммаЦен", 		ТекСтрока, "Сумма");
	Файл_Шаблон_Модуль.УстановитьЗначениеПоКлючу(ТекДанные, "СуммаЦенБезНДС",	ТекСтрока, "СуммаБезНал");
	Файл_Шаблон_Модуль.УстановитьЗначениеПоКлючу(ТекДанные, "Цена", 			ТекСтрока);
	Файл_Шаблон_Модуль.УстановитьЗначениеПоКлючу(ТекДанные, "ЦенаБезНДС", 		ТекСтрока);
	Файл_Шаблон_Модуль.УстановитьЗначениеПоКлючу(ТекДанные, "Артикул",	 		ТекСтрока);
	Файл_Шаблон_Модуль.УстановитьЗначениеПоКлючу(ТекДанные, "ВремяДоставки",	ТекСтрока);
	Файл_Шаблон_Модуль.УстановитьЗначениеПоКлючу(ТекДанные, "ДатаДоставки",		ТекСтрока);
	Файл_Шаблон_Модуль.УстановитьЗначениеПоКлючу(ТекДанные, "Идентификатор",	ТекСтрока);
	
	Файл_Шаблон_Модуль.УстановитьЗначениеПоКлючу(ТекДанные, "НомНомерСтор",		ТекСтрока, "КодПоставщика");
	Файл_Шаблон_Модуль.УстановитьЗначениеПоКлючу(ТекДанные, "НомНомер",			ТекСтрока, "КодПокупателя");
	
	Файл_Шаблон_Модуль.УстановитьЗначениеПоКлючу(ТекДанные, "Характеристика", 	ТекСтрока);
	Файл_Шаблон_Модуль.УстановитьЗначениеПоКлючу(ТекДанные, "Параметр",			ТекСтрока);
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТабличныеДанныеПредСтрТабл(Аргумент, ДопПараметры) Экспорт	
	Перем иПредСтрТабл;
	Источник = Аргумент.ИсточникДанных;
	
	Если Не Источник.Свойство("ПредСтрТабл", иПредСтрТабл) Тогда
		Возврат;
	КонецЕсли;
	АргументЗаполнить = Новый Структура("ИсточникДанных, ДанныеРезультат, Суффикс", иПредСтрТабл, Новый Соответствие, "");
	ОбработчикДанных_ЗаполнитьСтрокуТабличнойЧастиДокумента(АргументЗаполнить, ДопПараметры);
	
	Аргумент.ДанныеРезультат.Вставить("ПредСтрТабл", Новый Массив);
	Аргумент.ДанныеРезультат["ПредСтрТабл"].Добавить(АргументЗаполнить.ДанныеРезультат);
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТабличныеДанныеНДСЗначениеНДСТип(Аргумент, ДопПараметры)
	Если Не Аргумент.ИсточникДанных.Свойство("НДС") Тогда
		Возврат;
	КонецЕсли;
	Файл_Шаблон_Модуль.ЗаполнитьТабличныеДанныеНДСЗначениеНДСТип(Аргумент, Новый Структура("СимволПроцент", ""));
	Файл_Шаблон_Модуль.УстановитьЗначениеПоКлючу(Аргумент.ДанныеРезультат, "СумНДС", Аргумент.ИсточникДанных.НДС, "Сумма");
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТабличныеДанныеАкциз(Аргумент, ДопПараметры) Экспорт
	Источник = Аргумент.ИсточникДанных;
	Приемник = Аргумент.ДанныеРезультат;
	Если Не Источник.Свойство("Акциз") Тогда
		Возврат
	КонецЕсли;
	Если Источник.Акциз.Сумма = "без акциза" Тогда
		Файл_Шаблон_Модуль.УстановитьЗначениеПоКлючу(Приемник, "Акциз.Название",	Источник.Акциз, "Сумма");
	Иначе
		Файл_Шаблон_Модуль.УстановитьЗначениеПоКлючу(Приемник, "Акциз.Название",	Источник.Акциз,	"Название");
		Файл_Шаблон_Модуль.УстановитьЗначениеПоКлючу(Приемник, "Акциз.Ставка",		Источник.Акциз,	"Ставка");
		Файл_Шаблон_Модуль.УстановитьЗначениеПоКлючу(Приемник, "Акциз.Сумма",		Источник.Акциз,	"Сумма");
		Файл_Шаблон_Модуль.УстановитьЗначениеПоКлючу(Приемник, "Акциз.ТипСтавки",	Источник.Акциз,	"ТипСтавки");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьОснованиеЗаказ(Кэш, ТекДокумент)
	
	Для Каждого ТекСтрока ИЗ ТекДокумент.Основание Цикл
		
		Если ТекСтрока.Свойство("Тип") Тогда
			Ключ = "Тип";	
		Иначе
			Ключ = "Название";	
		КонецЕсли;
		
		Если ТекСтрока[Ключ] = "Заказ" Тогда
			Если НЕ ЗначениеЗаполнено(ТекСтрока.Название) Тогда
				Продолжить;	
			КонецЕсли;
			
			Если НЕ ТекСтрока.Свойство("Дата") Тогда
				Продолжить;
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(ТекСтрока.Дата) И ТекСтрока.Название <> "-" Тогда
				Продолжить;	
			КонецЕсли;
			
			ТекДокумент.Вставить("ДатаОсн", ТекСтрока.Дата);
			ТекДокумент.Вставить("НомОсн", ТекСтрока.Номер);
			ТекДокумент.Вставить("НаимОсн", ТекСтрока.Название);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

