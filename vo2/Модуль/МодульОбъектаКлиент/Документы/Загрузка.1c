
// Функция получает значение по указанному пути	
//
// Параметры:
//  ПараметрыДанные	 - 	 - 
// 
// Возвращаемое значение:
//   - 
//
&НаКлиенте
Функция ПолучитьДанныеПоПути(ПараметрыДанные, ДопПараметры = Неопределено) Экспорт
	
	ПутьКДаннымФайла	= СтрЗаменить(СтрЗаменить(СтрЗаменить(ПараметрыДанные.Путь, ".", Символы.ПС), "[", Символы.ПС), "]", "");
	ЗначениеРеквизита	= ПараметрыДанные.Данные;
	
	Для НомерСтроки = 1 По СтрЧислоСтрок(ПутьКДаннымФайла) Цикл
		Узел = СтрПолучитьСтроку(ПутьКДаннымФайла, НомерСтроки);
		Если ТипЗнч(ЗначениеРеквизита) = Тип("Структура") Тогда
			ЗначениеРеквизита.Свойство(Узел, ЗначениеРеквизита);
		ИначеЕсли ТипЗнч(ЗначениеРеквизита) = Тип("Соответствие") Тогда
			ЗначениеРеквизита = ЗначениеРеквизита.Получить(Узел);
		ИначеЕсли ТипЗнч(ЗначениеРеквизита) = Тип("Массив") Тогда
			ЗначениеРеквизита = ЗначениеРеквизита.Получить(Число(Узел));
		Иначе
			Возврат Неопределено;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ЗначениеРеквизита;
	
КонецФункции

&НаКлиенте
Функция ПолучитьДанныеСторонИзПакета(СоставПакета, ДопПараметры = Неопределено) Экспорт
		
	ДанныеОрганизации 	  = ПолучитьДанныеСтороны(СоставПакета.НашаОрганизация);
    ДанныеКонтрагента 	  = ПолучитьДанныеСтороны(СоставПакета.Контрагент); 
	ДанныеГрузополучателя = ПолучитьДанныеСтороны(ПолучитьГрузополучателяПакета(СоставПакета));
	
	ДанныеСторон = Новый Структура("ДанныеКонтрагента, ДанныеОрганизации, ДанныеГрузополучателя",	ДанныеКонтрагента, ДанныеОрганизации, ДанныеГрузополучателя);
	Возврат ДанныеСторон;
КонецФункции

&НаКлиенте
Функция ПолучитьДанныеСтороны(Сторона)
	
	ДанныеСтороны = Новый Структура;
	ДанныеСтороны.Вставить("Ссылка");
	
	Если ЗначениеЗаполнено(Сторона) Тогда
		Если Сторона.Свойство("СвЮЛ") Тогда
			ИНН = Сторона.СвЮЛ.ИНН;
			КПП = Сторона.СвЮЛ.КПП; 
			
			Если Сторона.СвЮЛ.Свойство("КодФилиала") Тогда  
				ДанныеСтороны.Вставить("КодФилиала", Сторона.СвЮЛ.КодФилиала); 
			КонецЕсли;  
			
			Если Сторона.СвЮЛ.Свойство("GLN") Тогда
				ДанныеСтороны.Вставить("GLN", Сторона.СвЮЛ.GLN); 
			КонецЕсли;
			ДанныеСтороны.Вставить("КПП", КПП);
		Иначе
			ИНН = Сторона.СвФЛ.ИНН;
		КонецЕсли;
		ДанныеСтороны.Вставить("ИНН", ИНН);	
	КонецЕсли;
	
	Возврат ДанныеСтороны;
КонецФункции

&НаКлиенте
Функция ПолучитьГрузополучателяПакета(СоставПакета, ДопПараметры = Неопределено) Экспорт
	
	Грузополучатель = Неопределено;
	
	Если СоставПакета.Тип = "ЗаказИсх"
		Или СоставПакета.Тип = "ДокОтгрВх" Тогда
		
		СоставПакета.Участники.Свойство("Лицо3", Грузополучатель);
		
	Иначе
		
		СоставПакета.Участники.Свойство("Лицо2", Грузополучатель);
		
	КонецЕсли;
	
	Возврат Грузополучатель;
	
КонецФункции

&НаКлиенте
Процедура СформироватьВходящийКонтекстНаВложении(Параметры, ДопПараметры = Неопределено) Экспорт
	
	ПодготовленныеПараметрыКонтекста = Параметры.ПодготовленныеПараметрыКонтекста;
	
	Если НЕ Параметры.Вложение = Неопределено Тогда		
		Если Не Параметры.Вложение.Свойство("ВходящийКонтекст") Тогда
			Параметры.Вложение.Вставить("ВходящийКонтекст", Новый Структура);
		КонецЕсли;
		
		Для Каждого Параметр Из ПодготовленныеПараметрыКонтекста Цикл
			
			Если НЕ Параметры.ЗаменятьЗаполненныеПараметры И Параметры.Вложение.ВходящийКонтекст.Свойство(Параметр.Ключ) Тогда
				Продолжить;	
			КонецЕсли;
			
			Параметры.Вложение.ВходящийКонтекст.Вставить(Параметр.Ключ, Параметр.Значение); 
			
		КонецЦикла;
	Иначе
		
		Для Каждого ВложениеПакета Из Параметры.СоставПакета.Вложение Цикл
			
			Если Не ВложениеПакета.Свойство("ВходящийКонтекст") Тогда
				ВложениеПакета.Вставить("ВходящийКонтекст", Новый Структура);
			КонецЕсли;
			
			Для Каждого Параметр Из ПодготовленныеПараметрыКонтекста Цикл
				
				Если НЕ Параметры.ЗаменятьЗаполненныеПараметры И ВложениеПакета.ВходящийКонтекст.Свойство(Параметр.Ключ) Тогда
					Продолжить;	
				КонецЕсли;
				
				ВложениеПакета.ВходящийКонтекст.Вставить(Параметр.Ключ, Параметр.Значение); 
				
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьВходящийКонтекстСогласноНастройкам(ПараметрыФормированияВходящегоКонтекста, ДопПараметры = Неопределено) Экспорт
	
	МестныйКэш = ПараметрыФормированияВходящегоКонтекста.МестныйКэш;
	
	Если НЕ ПараметрыФормированияВходящегоКонтекста.Вложение.Свойство("ВходящийКонтекст") Тогда
		ПараметрыФормированияВходящегоКонтекста.Вложение.Вставить("ВходящийКонтекст", Новый Структура);
	КонецЕсли;
	
	Если МестныйКэш.Парам.ЗаполнениеКонтрагента1С = "ГрузополучательСБИС" Тогда  
		
		Контрагент_Роль = "Грузополучатель";
				
		Если ПараметрыФормированияВходящегоКонтекста.Вложение.СтруктураИниФайла.Свойство("Контрагент_Роль")
			И ПараметрыФормированияВходящегоКонтекста.Вложение.СтруктураИниФайла.Контрагент_Роль.Свойство("Значение") 
			И ПараметрыФормированияВходящегоКонтекста.Вложение.Свойство("Контрагент1С") Тогда
			
				КонтрагентРоль = МестныйКэш.ОбщиеФункции.РассчитатьЗначение("Контрагент_Роль", ПараметрыФормированияВходящегоКонтекста.Вложение.СтруктураИниФайла, МестныйКэш);
				ИмяРеквизита = Сред(ПараметрыФормированияВходящегоКонтекста.Вложение.СтруктураИниФайла.мСторона[КонтрагентРоль].Сторона.Значение, 
											Найти(ПараметрыФормированияВходящегоКонтекста.Вложение.СтруктураИниФайла.мСторона[КонтрагентРоль].Сторона.Значение,".")+1);
											
				ПараметрыФормированияВходящегоКонтекста.Вложение.ВходящийКонтекст.Вставить(ИмяРеквизита, Контрагент_Роль);			
						
		КонецЕсли;
	Иначе
		
		Контрагент_Роль = "Покупатель";
		
		Если ПараметрыФормированияВходящегоКонтекста.Вложение.СтруктураИниФайла.Свойство("Контрагент_Роль")
			И ПараметрыФормированияВходящегоКонтекста.Вложение.СтруктураИниФайла.Контрагент_Роль.Свойство("Значение") 
			И ПараметрыФормированияВходящегоКонтекста.Вложение.Свойство("Контрагент1С") Тогда
			
				КонтрагентРоль = МестныйКэш.ОбщиеФункции.РассчитатьЗначение("Контрагент_Роль", ПараметрыФормированияВходящегоКонтекста.Вложение.СтруктураИниФайла, МестныйКэш);
				ИмяРеквизита = Сред(ПараметрыФормированияВходящегоКонтекста.Вложение.СтруктураИниФайла.мСторона[КонтрагентРоль].Сторона.Значение, 
											Найти(ПараметрыФормированияВходящегоКонтекста.Вложение.СтруктураИниФайла.мСторона[КонтрагентРоль].Сторона.Значение,".")+1);
				
				ПараметрыФормированияВходящегоКонтекста.Вложение.ВходящийКонтекст.Вставить(ИмяРеквизита, Контрагент_Роль);							
						
		КонецЕсли;
				
	КонецЕсли;
	
	Если ЗначениеЗаполнено(МестныйКэш.Парам.СкладПоУмолчанию) Тогда
		
		ПараметрыФормированияВходящегоКонтекста.Вложение.ВходящийКонтекст.Вставить("Склад", МестныйКэш.Парам.СкладПоУмолчанию);
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(МестныйКэш.Парам.РасСчетПоУмолчанию) Тогда
		
		ПараметрыФормированияВходящегоКонтекста.Вложение.ВходящийКонтекст.Вставить("РасСчет", МестныйКэш.Парам.РасСчетПоУмолчанию);	
		
	КонецЕсли;
		
КонецПроцедуры

// Дополняет данные сторон по пакету данными, сопоставленными в маппинге
//
// Параметры:
//  ДанныеСторон  - <Структура> - данные сторон:
//						- ДанныеКонтрагента 	- <Структура> - ИНН, КПП, GLN, КодФилиала 
//						- ДанныеОрганизации 	- <Структура> - ИНН, КПП, GLN, КодФилиала
//						- ДанныеГрузополучателя - <Структура> - ИНН, КПП, GLN, КодФилиала
//
&НаКлиенте
Процедура ДополнитьДанныеСторонСопоставлением(ДанныеСторон, ДопПараметры = Неопределено) Экспорт
	
	Кэш = ГлавноеОкно.Кэш;
	
	Если Кэш.Парам.СпособХраненияНастроек <> 1 Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПолучения = Новый Структура;
	
	ПараметрыПолучения.Вставить("ConnectionId", Кэш.Парам.ИдентификаторНастроек);
	ПараметрыПолучения.Вставить("IdType", 1);
	ПараметрыПолучения.Вставить("Filter", Новый Массив);
	
	Для каждого Сторона Из ДанныеСторон Цикл
		ДанныеСтороны = Новый Структура;
		
		Если	ТипЗнч(Сторона) = Тип("КлючИЗначение")
			И	Сторона.Ключ = "ДанныеОрганизации" Тогда
			
			ТипОбъекта = "НашаОрганизация";	
		Иначе
			ТипОбъекта = "Контрагент";
		КонецЕсли;
		ДанныеСтороны.Вставить("Type", ТипОбъекта);
		
		СведенияОСтороне = ?(ТипЗнч(Сторона) = Тип("КлючИЗначение"), Сторона.Значение, Сторона);
		Если ТипЗнч(СведенияОСтороне) <> Тип("Структура") Тогда
			Продолжить;
		КонецЕсли;
		
		Если СведенияОСтороне.Свойство("ИНН") Тогда
			ДанныеСтороны.Вставить("Key1_1", СведенияОСтороне.ИНН);
		Иначе
			Продолжить;
		КонецЕсли;
		
		Если СведенияОСтороне.Свойство("КПП") Тогда
			ДанныеСтороны.Вставить("Key1_2", СведенияОСтороне.КПП);
		КонецЕсли;
		
		Если СведенияОСтороне.Свойство("КодФилиала") Тогда
			ДанныеСтороны.Вставить("Key1_3", СведенияОСтороне.КодФилиала);
		КонецЕсли;
		
		Если СведенияОСтороне.Свойство("GLN") Тогда
			ДанныеСтороны.Вставить("Key2", СведенияОСтороне.GLN);
		КонецЕсли;
		
		ПараметрыПолучения.Filter.Добавить(ДанныеСтороны);
	КонецЦикла;
	
	Отказ = Ложь;
	СопоставленныеДанныеСторон = Кэш.Интеграция.СБИС_ПолучитьСопоставлениеСторон(Кэш, ПараметрыПолучения, , Отказ);
	
	Если Отказ Тогда   
		Кэш.СБИС.МодульОбъектаКлиент.ВызватьСбисИсключение(СопоставленныеДанныеСторон, Кэш.СБИС.ПараметрыИнтеграции.ИнтеграцияИмя + ".СБИС_ПолучитьСопоставлениеСторон");
	КонецЕсли;
	
	Для каждого СопоставленнаяСторона Из СопоставленныеДанныеСторон Цикл
		Сторона1С = ПолучитьУчастникаПоИд(СопоставленнаяСторона.ClientId, СопоставленнаяСторона.ClientType);
		Для каждого Сторона Из ДанныеСторон Цикл
			Если	?(Сторона.Значение.Свойство("ИНН"), 		Сторона.Значение.ИНН = СопоставленнаяСторона.SbisParam_1_1,			Ложь)
				И	?(Сторона.Значение.Свойство("КПП"), 		Сторона.Значение.КПП = СопоставленнаяСторона.SbisParam_1_2, 		НЕ ЗначениеЗаполнено(СопоставленнаяСторона.SbisParam_1_2))
				И	?(Сторона.Значение.Свойство("КодФилиала"),	Сторона.Значение.КодФилиала = СопоставленнаяСторона.SbisParam_1_3,	НЕ ЗначениеЗаполнено(СопоставленнаяСторона.SbisParam_1_3))
				И	?(Сторона.Значение.Свойство("GLN"), 		Сторона.Значение.GLN = СопоставленнаяСторона.SbisParam_2,			НЕ ЗначениеЗаполнено(СопоставленнаяСторона.SbisParam_2)) Тогда
				
				Сторона.Значение.Вставить("Ссылка", Сторона1С);
				
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;

КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьУчастникаПоИд(Идентификатор, Тип)
	Возврат Справочники[Сред(Тип, Найти(Тип, ".") + 1)].ПолучитьСсылку(Новый УникальныйИдентификатор(Идентификатор));
КонецФункции

// Сохраняет сопоставление сторона в маппинге
//
// Параметры:
//  ПараметрыФункции  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  ДопПараметры  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
&НаКлиенте
Процедура СохранитьСопоставлениеСторон(ПараметрыМетода, ДопПараметры) Экспорт
	
	Кэш = ДопПараметры.Кэш;
	ИниКонфигурации = Кэш.ини.Конфигурация;

	Если Кэш.Парам.СпособХраненияНастроек <> 1 Тогда
		Возврат;
	КонецЕсли;
	
	Фильтр = Новый Структура;
	
	Фильтр.Вставить("ConnectionId", Кэш.Парам.ИдентификаторНастроек);
	Фильтр.Вставить("IdType", 1);
	//ПараметрыСохранения.Вставить("Filter", Новый Массив);
	//ПараметрыСохранения.Вставить("Data", Новый Массив);
	
	//Для каждого Сторона Из ДанныеСторон Цикл 
	Сторона = ПараметрыМетода.ДанныеСтороны;
	//ДанныеСтороны = Новый Структура;
	
	Если ТипЗнч(Сторона.Ссылка) = Тип(СтрЗаменить(ИниКонфигурации.Организации.Значение, ".", "Ссылка."))  Тогда
		ТипОбъекта = "НашаОрганизация";	
	Иначе
		ТипОбъекта = "Контрагент";
	КонецЕсли;
	Фильтр.Вставить("Type", ТипОбъекта);
	
	Если ТипЗнч(Сторона) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Если Сторона.Свойство("ИНН") Тогда
		Фильтр.Вставить("Key1_1", Сторона.ИНН);
	Иначе
		Возврат;
	КонецЕсли;
	
	Если Сторона.Свойство("КПП") Тогда
		Фильтр.Вставить("Key1_2", Сторона.КПП);
	КонецЕсли;
	
	Если Сторона.Свойство("КодФилиала") Тогда
		Фильтр.Вставить("Key1_3", Сторона.КодФилиала);
	КонецЕсли;
	
	Если Сторона.Свойство("GLN") Тогда
		Фильтр.Вставить("Key2", Сторона.GLN);
	КонецЕсли;
	
	//ПараметрыСохранения.Filter.Добавить(ДанныеСтороны); 
	ДанныеСтороны = Новый Структура;
	ДанныеСтороны.Вставить("ClientId", Строка(Сторона.Ссылка.УникальныйИдентификатор()));
	ДанныеСтороны.Вставить("ClientName", Строка(Сторона.Ссылка));
	ДанныеСтороны.Вставить("Status", 1);
	ДанныеСтороны.Вставить("ClientType", СтрЗаменить(ИниКонфигурации.Организации.Значение, ".", "и."));
	//ПараметрыСохранения.Data.Добавить(Строка(Сторона.Ссылка.УникальныйИдентификатор()));
	//КонецЦикла;
	
	Отказ = Ложь;
	СопоставленныеДанныеСторон = Кэш.Интеграция.ОбновитьЗаписьСопоставления(Кэш, Фильтр, ДанныеСтороны, Отказ);
	
	Если Отказ Тогда   
		Кэш.СБИС.МодульОбъектаКлиент.ВызватьСбисИсключение(СопоставленныеДанныеСторон, Кэш.СБИС.ПараметрыИнтеграции.ИнтеграцияИмя + ".СБИС_ПолучитьСопоставлениеСторон");
	КонецЕсли;

КонецПроцедуры // СохранитьСопосталвниеСторон()


