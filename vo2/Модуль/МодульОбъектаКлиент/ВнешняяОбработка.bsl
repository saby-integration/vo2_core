
// Процедура выполняет поиск обработки, получение компоненты из макета, подключение ВК 
//
// Параметры:
//  ПараметрыОбработки	 -	Структура(ВнешняяОбработка*, ИмяОбработки*, ИмяМакета)
//							ВнешняяОбработка	- ссылка на справочник ДопОбработок или путь к файлу
//                          ИмяОбработки		- Строка, Имя обработки для поиска обработки, если ВнешняяОбработка не заполнено  	
//							ИмяМакета			- Строка 
//  Кэш					 - 	Кэш 
// 
//
&НаКлиенте
Процедура ВнешняяОбработка_ПодключитьКомпоненту(ПараметрыОбработки) Экспорт
	
	Если НЕ ПараметрыОбработки.Свойство("МестаПоиска") Тогда
		ПараметрыОбработки.Вставить("МестаПоиска", ВнешняяОбработка_МестаПоискаПоУмолчанию());
	КонецЕсли;
	
	МестоположениеКомпоненты = ВнешняяОбработка_ПолучитьМакет(ПараметрыОбработки, Новый Структура("ПоместитьВХранилище", Истина));
		    
	Если Не ПодключитьВнешнююКомпоненту(МестоположениеКомпоненты, ПараметрыОбработки.ИмяМакета, ТипВнешнейКомпоненты.Native)  Тогда
		
		Если ГлавноеОкно.Кэш.СовместимостьМетодов.НативныеКомпоненты.НачатьУстановкуВнешнейКомпоненты Тогда
			
			Выполнить("НачатьУстановкуВнешнейКомпоненты(,МестоположениеКомпоненты)");
			ВызватьИсключение("Начата установка компоненты, необходимо повторить действие");
			
		Иначе 
			
			УстановитьВнешнююКомпоненту(МестоположениеКомпоненты);
			
		КонецЕсли;

	КонецЕсли;
	
	УдалитьИзВременногоХранилища(МестоположениеКомпоненты);
	
КонецПроцедуры

// Функция - Получает макет из внешней обработки. Опционально помещает во ВременноеХранилище или извлекает текст из макета. 
//
// Параметры:
//  ПараметрыОбработки	 -	Структура(ВнешняяОбработка*, ИмяОбработки*)
//							ВнешняяОбработка	- ссылка на справочник ДопОбработок или путь к файлу
//                          ИмяОбработки		- Имя обработки для поиска обработки, если ВнешняяОбработка не заполнено  	
//  ДопПараметры*		 -	Структура*(Текст*, ПоместитьВХранилище*) 
//                       	Текст(Булево) - Вернуть текст из макета
//							ПоместитьВХранилище - Поместить макет во временное хранилище и вернуть ссылку	
// *Необязательные
// Возвращаемое значение:   Двоичные данные, Строка(Адрес в ВХ или текст макета), Неопределено, если не удалось найти обработку 
//   - 
//
&НаСервере
Функция ВнешняяОбработка_ПолучитьМакет(ПараметрыОбработки, ДопПараметры = Неопределено) Экспорт	
	Возврат МодульОбъектаСервер().ВнешняяОбработка_ПолучитьМакетНаСервере(ПараметрыОбработки, ДопПараметры);	
КонецФункции

// Функция - Выполняет поиск обработки. Возможные места поиска: файл, справочник, конфигурация. Возвращает путь или ссылку на ВО
//
// Параметры:
//  ПараметрыПоиска	 - 	 Структура(ИмяОбработки, МестаПоиска*) 
//	*Необязательные  
//
// Возвращаемое значение:
//   Строка - путь к файловой обработке
//   СправочникСсылка - на элемент справочника Доп.Обработки
//	 Неопределено - обработка не найдена
&НаСервере
Функция ВнешняяОбработка_НайтиПоИмени(ПараметрыПоиска) Экспорт	
	Возврат МодульОбъектаСервер().ВнешняяОбработка_НайтиПоИмениНаСервере(ПараметрыПоиска);	
КонецФункции

// Функция - Возвращает возможные места поиска ВО. Для сервера это справочники. На клиенте добавляется каталог настроек 
// 
// Возвращаемое значение:
//   - Массив структур
//
&НаКлиенте
Функция ВнешняяОбработка_МестаПоискаПоУмолчанию()
	
	МестаПоиска = Новый Массив();
	МестаПоиска.Добавить(Новый Структура("Путь, Алгоритм", "Справочники.ДополнительныеОтчетыИОбработки",	"Запрос"));
	МестаПоиска.Добавить(Новый Структура("Путь, Алгоритм", "Справочники.ВнешниеОбработки",					"Запрос"));
	МестаПоиска.Добавить(Новый Структура("Путь, Алгоритм", ГлавноеОкно.Кэш.Парам.КаталогНастроек,		"Каталог"));

	Возврат МестаПоиска;
	
КонецФункции


