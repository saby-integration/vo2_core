
//Функция заполняет параметры для совместимости вызова изменившихся методов на новых релизах платформы 
&НаКлиенте
Функция НовыйСовместимостьМетодов(ВерсияТекущегоПриложения) Экспорт
	
	СбисСооветстветствиеВерсий = СбисСоответствиеВерсийМетодов();
	ПараметрыПриведения = Новый Структура("ВспомогательныйРазделитель, ВариантПриведения, ВерсияПриложения", ".", "Дозаполнить", ВерсияТекущегоПриложения);
	СбисЗаполнитьСоответствиеВерсий(СбисСооветстветствиеВерсий, ПараметрыПриведения);
	Возврат СбисСооветстветствиеВерсий;
	
КонецФункции

&НаКлиенте
Процедура СбисЗаполнитьСоответствиеВерсий(СбисСоответствиеВерсий, СбисДополнительныеПараметры)
	
	ВерсияПриложения = СбисДополнительныеПараметры.ВерсияПриложения;
	Для Каждого КлючИЗначение Из сбисСоответствиеВерсий Цикл
		Если ТипЗнч(КлючИЗначение.Значение) = Тип("Структура") Тогда
			сбисЗаполнитьСоответствиеВерсий(КлючИЗначение.Значение, СбисДополнительныеПараметры);
			Продолжить;
		КонецЕсли;
		лВерсияАктивации = ПривестиВерсию(КлючИЗначение.Значение, СбисДополнительныеПараметры);
		СбисСоответствиеВерсий[КлючИЗначение.Ключ] = СравнитьВерсииБольшеМеньше(лВерсияАктивации, ВерсияПриложения);
	КонецЦикла;	
	
КонецПроцедуры

&НаКлиенте
Функция СбисСоответствиеВерсийМетодов(ПараметрыОпределения = Неопределено)
	Результат = Новый Структура;
	Результат.Вставить("ПреобразованиеXSL", Новый Структура("ПреобразоватьИзСтроки", "8.3.10"));
	Результат.Вставить("HTTPСоединение",	Новый Структура("ФункцияПолучить, OpenSSL", "8.3", "8.2.16"));
	
	Объекты1ССовместимость = Новый Структура("ПоказатьЗначение", "8.3.3");
	#Если ТолстыйКлиентОбычноеПриложение Тогда
		Объекты1ССовместимость.Вставить("ОписаниеОповещения",		"8.3.16");
		//Для ОФ - всегда сериализуемое исключение
		Объекты1ССовместимость.Вставить("СериализуемоеИсключение",	"8.0.0");
	#Иначе
		//Для УФ - всегда описание оповещения
		Объекты1ССовместимость.Вставить("ОписаниеОповещения",		"8.0.0");
		Объекты1ССовместимость.Вставить("СериализуемоеИсключение",	"8.3.16");
	#КонецЕсли
	Результат.Вставить("Объекты1С",			Объекты1ССовместимость);

	Результат.Вставить("Аккордеон",			Новый Структура("Аккордеон83", "8.3.15"));
	Результат.Вставить("НативныеКомпоненты",Новый Структура("НачатьУстановкуВнешнейКомпоненты, ОписаниеОповещения", "8.3.3", "8.3.3"));                    
	Возврат Результат;
КонецФункции

