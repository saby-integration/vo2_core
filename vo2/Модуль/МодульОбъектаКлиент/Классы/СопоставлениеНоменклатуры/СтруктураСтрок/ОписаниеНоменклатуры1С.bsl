
// Функция - создаёт строку сопоставления номенклатуры
//
// Параметры:
//  ИдСБИС	 - 	 - 
//  ИмяСБИС	 - 	 - 
// 
// Возвращаемое значение:
//   - 
//
&НаКлиенте
Функция НовыйОписаниеНоменклатуры1СКлиент() Экспорт
	
	Описание = Новый Структура;
	Описание.Вставить("_класс", "ОписаниеНоменклатуры1С"); 
	Описание.Вставить("Характеристики", Новый Массив);
	Описание.Вставить("Идентификатор", "");
	Описание.Вставить("GTIN", "");
	Описание.Вставить("Единицы", Новый Соответствие);
	Описание.Вставить("Коэффициент", 1);
	Описание.Вставить("Основное", Ложь);
	Описание.Вставить("Цена", 0);
	Описание.Вставить("Кол_во", 0);
	Описание.Вставить("Сумма", 0);
	Описание.Вставить("СуммаБезнал", 0);
	Описание.Вставить("СтавкаНДС", "");
	Описание.Вставить("СуммаНДС", 0);
	Описание.Вставить("БазоваяЕдиницаОКЕИ", "796");
	Описание.Вставить("Ссылка", Неопределено); // Для работы с ДБФ, по просьбе Андрея (с)Сычев
	
	Возврат Описание;

КонецФункции

&НаКлиенте
Процедура ОписаниеНоменклатуры1СКлиент_Заполнить(СтрокаНоменклатуры1С, СопоставлениеЗаполнить) Экспорт
	
	Перем СЛокальнаяПеременная;
	
	НоменклатураСсылка	= СопоставлениеЗаполнить.Номенклатура;
		
	// Признак основной номенклатуры для сопоставления
	Если НЕ СтрокаНоменклатуры1С.Основное Тогда
		Если Не СопоставлениеЗаполнить.Свойство("Основное", СЛокальнаяПеременная) Тогда
			СЛокальнаяПеременная = Ложь;
		КонецЕсли;
		СтрокаНоменклатуры1С.Основное = СЛокальнаяПеременная;
	КонецЕсли;
	
	
	// GTIN
	Если НЕ ЗначениеЗаполнено(СтрокаНоменклатуры1С.GTIN) Тогда
		Если Не СопоставлениеЗаполнить.Свойство("GTIN_1С", СЛокальнаяПеременная) Тогда
			СЛокальнаяПеременная = "";
		КонецЕсли;
		СтрокаНоменклатуры1С.GTIN = СокрЛП(СЛокальнаяПеременная);
	КонецЕсли;
	
	
	// Идентификатор номенклатуры 1С
	Если НЕ ЗначениеЗаполнено(СтрокаНоменклатуры1С.Идентификатор) Тогда
		Если Не СопоставлениеЗаполнить.Свойство("Идентификатор1С", СЛокальнаяПеременная) Тогда
			СЛокальнаяПеременная = Строка(НоменклатураСсылка.УникальныйИдентификатор());
		КонецЕсли;
		СтрокаНоменклатуры1С.Идентификатор = СокрЛП(СЛокальнаяПеременная);
	КонецЕсли;
	
	
	// Характеристика
	Если		СопоставлениеЗаполнить.Свойство("Характеристика", СЛокальнаяПеременная) Тогда
		// При записи в старых методах приходит Ссылка на характеристику, а при групповом чтении ссылка на характеристику СБИС.
	ИначеЕсли	СопоставлениеЗаполнить.Свойство("Характеристика1С", СЛокальнаяПеременная) Тогда
		// Характеристика 1С по-новому (ссылка)
	КонецЕсли;
	Если 		ЗначениеЗаполнено(СЛокальнаяПеременная)
		И	Не	ТипЗнч(СЛокальнаяПеременная) = Тип("Структура")
		И	Не	ТипЗнч(СЛокальнаяПеременная) = Тип("Строка")
		И	СтрокаНоменклатуры1С.Характеристики.Найти(СЛокальнаяПеременная) = Неопределено Тогда
		СтрокаНоменклатуры1С.Характеристики.Добавить(СЛокальнаяПеременная);
	КонецЕсли;
	
	// Ссылка на номенклатуру 1С для использования в ДБФ по просьбе Андрея (с)Сычев
	СтрокаНоменклатуры1С.Ссылка = НоменклатураСсылка;
	
	// Единицы
	Если	СопоставлениеЗаполнить.Свойство("ЕдИзм1С", СЛокальнаяПеременная)
		И	ТипЗнч(СЛокальнаяПеременная) = Тип("Массив")
		И	ЗначениеЗаполнено(СЛокальнаяПеременная) Тогда
		
		Для каждого СтрЕдиница Из СЛокальнаяПеременная Цикл
			СопоставлениеДляЕдиницыКлиент_Заполнить1С(СтрокаНоменклатуры1С, СтрЕдиница);	
		КонецЦикла;
	ИначеЕсли ТипЗнч(СЛокальнаяПеременная) = Тип("Соответствие") Тогда
			СтрокаНоменклатуры1С.Единицы.Очистить();
			СтрокаНоменклатуры1С.Единицы = СЛокальнаяПеременная;
	Иначе
		СопоставлениеДляЕдиницыКлиент_Заполнить1С(СтрокаНоменклатуры1С, СопоставлениеЗаполнить);
	КонецЕсли;      
	
	// БазоваяЕдиницаОКЕИ
	Если Не СопоставлениеЗаполнить.Свойство("БазоваяЕдиницаОКЕИ", СЛокальнаяПеременная) Тогда
		СЛокальнаяПеременная = "796";
	КонецЕсли;
	СтрокаНоменклатуры1С.БазоваяЕдиницаОКЕИ = СокрЛП(СЛокальнаяПеременная);
	
	// Данные для загрузки в документ 1С
	Если СопоставлениеЗаполнить.Свойство("Цена")
		И ЗначениеЗаполнено(СопоставлениеЗаполнить.Цена) Тогда
		
		СписокСвойств = "Цена, Кол_во, Сумма, СуммаБезНал, СуммаНДС, СтавкаНДС, БазоваяЕдиницаОКЕИ";
		ЗаполнитьЗначенияСвойств(СтрокаНоменклатуры1С, СопоставлениеЗаполнить, СписокСвойств);
				
	КонецЕсли;                                                         

КонецПроцедуры

// Функция - Описание номенклатуры1 с клиент получить
//
// Параметры:
//  ОписаниеНоменклатуры1С	 - Структура	     - Строка Номенклатуры1С (см.НовыйОписаниеНоменклатуры1СКлиент)
//  КлючПоиска				 - Строка/Массив	 - Если строка, то одно конкретное свойство класса, если массив, то набор свойств необходимых для заполнения (свойств может не быть в классе, тогда "Неопределено")
// 
// Возвращаемое значение:
// Произвольный  - Значение конкретного свойства или структура данных, заполненных по списку свойств
//
&НаКлиенте
Функция ОписаниеНоменклатуры1СКлиент_Получить(ОписаниеНоменклатуры1С, КлючПоиска) Экспорт
		
	Если ТипЗнч(КлючПоиска) = Тип("Массив") Тогда
		ВыгружаемыеДанные = Новый Структура;
		
		Для Каждого Ключ Из КлючПоиска Цикл   
			
			Если Ключ = "Номенклатура" Тогда
				ВыгружаемыеДанные.Вставить("Номенклатура", ОписаниеНоменклатуры1С.Ссылка);		
			ИначеЕсли Ключ = "ЕдИзм" Тогда
				
				Для Каждого Единица Из ОписаниеНоменклатуры1С.Единицы Цикл
					
					ВыгружаемыеДанные.Вставить("ЕдИзм", Единица.Ключ);
					ВыгружаемыеДанные.Вставить("ОКЕИ", Единица.Значение.ОКЕИ);
					ВыгружаемыеДанные.Вставить("Коэффициент", Единица.Значение.Коэффициент);  
					Прервать;
				КонецЦикла; 
				
			ИначеЕсли Ключ = "НДС" Тогда
				
				НДС = Новый Структура("Ставка, Сумма");
				НДС.Ставка = ОписаниеНоменклатуры1С.СтавкаНДС;
				НДС.Сумма = ОписаниеНоменклатуры1С.СуммаНДС;
				ВыгружаемыеДанные.Вставить("НДС", НДС);
				
			ИначеЕсли Ключ = "ЦенаБезНДС" Тогда
				
				ЦенаБезНДС = ОписаниеНоменклатуры1С.СуммаБезНал / ОписаниеНоменклатуры1С.Кол_во;
				ВыгружаемыеДанные.Вставить("ЦенаБезНДС", ЦенаБезНДС);
				
			Иначе
				
				Попытка
					ВыгружаемыеДанные.Вставить(Ключ, ОписаниеНоменклатуры1С[Ключ]);
				Исключение
					ВыгружаемыеДанные.Вставить(Ключ, Неопределено);
				КонецПопытки;
				
			КонецЕсли;
			
		КонецЦикла;	
		
		Возврат ВыгружаемыеДанные;
		
	Иначе
		Возврат ОписаниеНоменклатуры1С[КлючПоиска];
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ОписаниеНоменклатуры1СКлиент_Вставить(ОписаниеНоменклатуры1С, КлючВставить, ЗначениеВставить) Экспорт
	
	Перем Значение, КлючДобавления;
	
	Если		КлючВставить = "Характеристика" Тогда
		ОписаниеНоменклатуры1С.Характеристики.Добавить(ЗначениеВставить);
	ИначеЕсли	КлючВставить = "GTIN" Тогда 
		ОписаниеНоменклатуры1С.GTIN = ЗначениеВставить;
	ИначеЕсли	КлючВставить = "Единица" Тогда
		ИдЗаписиСбис = СопоставлениеДляЕдиницыКлиент_Ключ(ЗначениеВставить);
		ОписаниеНоменклатуры1С.Единицы.Вставить(ИдЗаписиСбис, ЗначениеВставить);
	Иначе
		ОписаниеНоменклатуры1С[КлючВставить] = ЗначениеВставить;
	КонецЕсли;
	
КонецПроцедуры

