
// Функция возвращает результат работы метода ExtSysAction.CheckAnnul
//
// Параметры:
//  ИдентификаторыПакетов  - Массив - обрабатываемые идентификаторы пакетов документов
//  ДопПараметры  - Структура - Набор полей, работа с которыми может расширить результат работы функции.
//
// Возвращаемое значение:
//   Структура   - Результат вызова. Список доступных к аннулированию идентификаторов пакетов.
//
&НаКлиенте
Функция ПроверитьВозможностьАннулирования(ИдентификаторыПакетов, ДопПараметры = Неопределено) 
	
	Кэш = ГлавноеОкно.Кэш;  
	
	ПараметрыВызова = Новый Структура;  
	ПараметрыВызова.Вставить("docflowIds",ИдентификаторыПакетов);
	 
	ФлагОшибки = Ложь;  
	
	РезультатВызова = ГлобальныйКэш.ТекущийСеанс.Модули.Интеграция.ExtSysAction_CheckAnnul(ПараметрыВызова, Новый Структура("Кэш", Кэш), ФлагОшибки); 
	
	Если ФлагОшибки Тогда
		ВызватьСбисИсключение(РезультатВызова, "МодульОбъектаКлиент.ПроверитьВозможностьАннулирования");
	КонецЕсли;   
	
	Возврат РезультатВызова;
	
КонецФункции  

// Функция возвращает результат работы метода ExtSysOrders.List, список заказов для построения реестра документов.
//
// Параметры:
//  ПараметрыФильтра  - Структура - Дополнительные параметры фильтра
//  ДопПараметры  - Структура - Набор полей, работа с которыми может расширить результат работы функции.
//
// Возвращаемое значение:
//   Структура   - Результат вызова.
//
&НаКлиенте
Функция ПолучитьСписокЗаказов(ПараметрыФильтра, ДопПараметры = Неопределено) Экспорт	    
	
	Кэш = ГлавноеОкно.Кэш;   
	ОбщиеФункцииДокументов = ПолучитьЗначениеПараметраТекущегоСеанса("ФункцииДокументов");  
	ФлагОшибки = Ложь;   
	МодульИнтеграции = ПолучитьЗначениеПараметраТекущегоСеанса("Интеграция");
	
	ГлавноеОкно.сбисПоказатьСостояние("Получение данных с " + ПолучитьЗначениеПараметраСбис("ПредставлениеСервера"), ГлавноеОкно);
	
	ГлавноеОкно.ОтметитьВсе = Ложь;
	 
	ПараметрыФильтрации = СформироватьФильтрДляРеестраОнлайна(Кэш.ГлавноеОкно, Новый Структура("ДопФильтры", ПараметрыФильтра));
	
	РезультатВызова = МодульИнтеграции.ExtSysOrders_List(ПараметрыФильтрации, Новый Структура("Кэш", Кэш), ФлагОшибки); 
	
	Если ФлагОшибки Тогда         
		
		ГлавноеОкно.сбисСпрятатьСостояние(ГлавноеОкно);	
		ВызватьИсключение СбисИсключение_Представление(РезультатВызова); 
		
	КонецЕсли;   
	
	Возврат ОбщиеФункцииДокументов.ОбработатьСписокДокументов(Кэш, РезультатВызова);
	
КонецФункции


