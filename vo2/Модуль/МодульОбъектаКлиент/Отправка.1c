
&НаКлиенте
Функция СформироватьСтруктуруКонтрагентаДляОтправки(ОписаниеКонтрагента, Кэш) Экспорт
	Перем ДопПараметрыСтороны, ПерДанныеЛица;
	kontr			= Новый Структура; 
	СоставПакета	= ОписаниеКонтрагента.СоставПакета;
	
	//ИД контрагента
	Если	СоставПакета.Контрагент.Свойство("Идентификатор")
		И	ЗначениеЗаполнено(СоставПакета.Контрагент.Идентификатор) Тогда
		kontr.Вставить( "Идентификатор", СоставПакета.Контрагент.Идентификатор);
	Иначе
		СбисИдентификаторКонтрагента = ПрочитатьДополнительныйПараметрСтороны(СоставПакета.Контрагент, "КодОператораАбонентскогоЯщика");
		Если Не СбисИдентификаторКонтрагента = Неопределено Тогда
			//ИД явно не указан, то проверить наличие ИД оператора А/Я
			kontr.Вставить("Идентификатор", СбисИдентификаторКонтрагента);
		КонецЕсли;
	КонецЕсли;
	
	//Контакты
	Если СоставПакета.Контрагент.Свойство("Контакт")  Тогда
		Если СоставПакета.Контрагент.Контакт.Свойство("Телефон")  Тогда
			kontr.Вставить( "Телефон", СоставПакета.Контрагент.Контакт.Телефон );
		КонецЕсли;
		Если СоставПакета.Контрагент.Контакт.Свойство("EMAIL")  Тогда
			kontr.Вставить( "Email", СоставПакета.Контрагент.Контакт.EMAIL );
		КонецЕсли;
	КонецЕсли;
	
	//СвФЛ/СвЮл
	СвЮЛФЛ = Новый Структура;
	Если СоставПакета.Контрагент.Свойство("СвФЛ", ПерДанныеЛица) Тогда
		Если СоставПакета.Контрагент.Свойство("Параметр") Тогда
			Для Каждого Параметр из СоставПакета.Контрагент.Параметр Цикл
				Если Параметр.Имя = "ЧастноеЛицо" Тогда
					СвЮЛФЛ.Вставить("ЧастноеЛицо", Параметр.Значение);
				КонецЕсли;
				Если Параметр.Имя = "СНИЛС" Тогда
					СвЮЛФЛ.Вставить("СНИЛС", Параметр.Значение);						
				КонецЕсли;     					
			КонецЦикла;
		КонецЕсли;  
		Если ПерДанныеЛица.Свойство("Фамилия") Тогда
			СвЮЛФЛ.Вставить( "Фамилия", ПерДанныеЛица.Фамилия );	
		КонецЕсли;
		Если ПерДанныеЛица.Свойство("Имя") Тогда
			СвЮЛФЛ.Вставить( "Имя", ПерДанныеЛица.Имя );	
		КонецЕсли;
		Если ПерДанныеЛица.Свойство("Отчество") Тогда
			СвЮЛФЛ.Вставить( "Отчество", ПерДанныеЛица.Отчество );	
		КонецЕсли;

		kontr.Вставить("СвФЛ", СвЮЛФЛ);	
	ИначеЕсли СоставПакета.Контрагент.Свойство("СвЮЛ", ПерДанныеЛица) Тогда
		Если ПерДанныеЛица.Свойство("КПП") Тогда
			СвЮЛФЛ.Вставить( "КПП", ПерДанныеЛица.КПП );
		КонецЕсли;
		Если ПерДанныеЛица.Свойство("Название") Тогда
			СвЮЛФЛ.Вставить( "Название", ПерДанныеЛица.Название );	
		КонецЕсли;
		kontr.Вставить("СвЮЛ", СвЮЛФЛ);
	ИначеЕсли СоставПакета.Контрагент.Свойство("СвИн", ПерДанныеЛица) Тогда
		//https://sbis.ru/help/integration/api/all_methods/doc
		kontr.Вставить("СвЮЛ", СвЮЛФЛ);
		Если ПерДанныеЛица.Свойство("Идентиф") Тогда  
			СвЮЛФЛ.Вставить("ИНН", ПерДанныеЛица.Идентиф);
		КонецЕсли;
	Иначе
		//Ошибка
		ВызватьСбисИсключение("Неизвестный формат данных контрагента", "МодульОбъектаКлиент.СформироватьСтруктуруКонтрагентаДляОтправки");
	КонецЕсли;
	Если ПерДанныеЛица.Свойство("ИНН") Тогда  
		СвЮЛФЛ.Вставить( "ИНН", ПерДанныеЛица.ИНН );
	КонецЕсли;
	Если ПерДанныеЛица.Свойство("КодСтраны") Тогда
		СвЮЛФЛ.Вставить( "КодСтраны", ПерДанныеЛица.КодСтраны );	
	КонецЕсли;
	Если	ПерДанныеЛица.Свойство("КодФилиала")
		И	ЗначениеЗаполнено(ПерДанныеЛица.КодФилиала) Тогда
		СвЮЛФЛ.Вставить( "КодФилиала", ПерДанныеЛица.КодФилиала );	
	КонецЕсли;
	
	
	//Подразделение
	Если СоставПакета.Контрагент.Свойство("Подразделение") и СоставПакета.Контрагент.Подразделение.Свойство("Идентификатор") Тогда
		Подразделение = Новый Структура;
		Подразделение.Вставить( "Идентификатор", СоставПакета.Контрагент.Подразделение.Идентификатор); 
		kontr.Вставить( "Подразделение", Подразделение );
	КонецЕсли;	
	Возврат kontr;
КонецФункции

&НаКлиенте
Процедура ЗаписатьВложенияСБИС(СоставПакета, Вложение, ПараметрыЗаписи, ДопПараметры) Экспорт
	
	Кэш = ГлавноеОкно.Кэш;
	
	Если Не (СоставПакета.Свойство("Идентификатор") и ЗначениеЗаполнено(СоставПакета.Идентификатор)) Тогда 
		ВызватьСбисИсключение("Не указан документ для записи", "МодульОбъектаКлиент.ЗаписатьВложенияСБИС");
	КонецЕсли;
	
	Если Вложение.Свойство("ПолноеИмяФайла") Тогда // внешний файл добавлен в пакет
		Имя= Вложение.ИмяФайла;
		ДвоичныеДанные=Кэш.Интеграция.СБИС_СериализоватьФайлВBase64(Кэш, Новый Структура("ПолноеИмяФайла", Вложение.ПолноеИмяФайла), Новый Структура, Ложь); 
	Иначе  // сформирован xml
		Если Вложение.Свойство("СтруктураФайла") Тогда
			Имя= Вложение.СтруктураФайла.Файл.Имя+".xml";
		Иначе
			Имя= Вложение.Файл.Имя+".xml";
		КонецЕсли;
		Если Вложение.Свойство("XMLДокумента") Тогда
			ДвоичныеДанные=Кэш.Интеграция.СБИС_СериализоватьСтрокуВBase64(Кэш, Новый Структура("Строка", Вложение.XMLДокумента), Новый Структура, Ложь);
		Иначе
			ДвоичныеДанные="";
		КонецЕсли
	КонецЕсли;

	Если ЗначениеЗаполнено(ДвоичныеДанные) Тогда
		document = Новый Структура;
		document.Вставить( "Идентификатор", СоставПакета.Идентификатор ); 
		attachmentList = Новый Массив;
		attachment = Новый Структура; 
		file = Новый Структура;
		file.Вставить( "Имя", Имя);
		file.Вставить( "ДвоичныеДанные", ДвоичныеДанные);
		attachment.Вставить( "Файл", file );
		Если Не( Вложение.Свойство("Идентификатор") и ЗначениеЗаполнено(Вложение.Идентификатор)) Тогда
			ИдВложения = Строка(Новый УникальныйИдентификатор());
			Вложение.Вставить("Идентификатор", ИдВложения);
		КонецЕсли;
		attachment.Вставить( "Идентификатор",  Вложение.Идентификатор);
		attachmentList.Добавить( attachment );
		document.Вставить( "Вложение", attachmentList );
		
		Отказ = Ложь;
		ДопПараметрыЗапроса	= Новый Структура("СообщатьПриОшибке, ВернутьОшибку, ЖдатьОтвета, ЕстьРезультат", Ложь, Истина, Истина, Истина);
		Результат = Кэш.Интеграция.СБИС_ЗаписатьВложение(Кэш, document, ДопПараметрыЗапроса, Отказ);
		Если Отказ Тогда   
			ВызватьСбисИсключение(Результат, Кэш.СБИС.ПараметрыИнтеграции.ИнтеграцияИмя + ".СБИС_ЗаписатьВложение");
		КонецЕсли;
	Иначе    
		ВызватьСбисИсключение("Нет файла для записи", "МодульОбъектаКлиент.ЗаписатьВложенияСБИС");
	КонецЕсли; 
	
КонецПроцедуры		// alo Меркурий >>

