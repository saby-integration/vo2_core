
// Процедура - копипаст типовой функции корректировки реализации для заполнения ТЧ по основанию. 
// Используется ТОЛЬКО для устаревших .4 редакций, т.к. типовые функции заполнения не являются экспортными
//
// Параметры:
//  МенеджерВременныхТаблиц	 - МенеджерВременныхТаблиц	 - куда заполнить ТЧ
//  ДокументОснование		 - ДокументСсылка			 - Основание, по которому делается выборка для дозаполнения
//
Процедура УТ11_4_СформироватьВременнуюТаблицуИсходныхДанных(МенеджерВременныхТаблиц, ДокументОснование) Экспорт
	
	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		ТекстЗапросаПоОснованию = УТ11_4_ТекстЗапросаПоИсходнымДаннымРеализацияТоваровУслуг();
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.АктВыполненныхРабот") Тогда
		ТекстЗапросаПоОснованию = УТ11_4_ТекстЗапросаПоИсходнымДаннымАктВыполненныхРабот();
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.РеализацияУслугПрочихАктивов") Тогда
		ТекстЗапросаПоОснованию = УТ11_4_ТекстЗапросаПоИсходнымДаннымРеализацияУслугПрочихАктивов();
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	КорректировкаРеализации.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ДанныеПоследнейКорректировки
	|ИЗ
	|	Документ.КорректировкаРеализации КАК КорректировкаРеализации
	|ГДЕ
	|	КорректировкаРеализации.Проведен
	|	И КорректировкаРеализации.ВидКорректировки <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияПерепоставленногоТовара)
	|	И КорректировкаРеализации.ВидКорректировки <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратНедопоставленногоТовара)
	|	И КорректировкаРеализации.ДокументОснование = &ДокументОснование
	|
	|УПОРЯДОЧИТЬ ПО
	|	КорректировкаРеализации.МоментВремени УБЫВ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки				   КАК НомерСтроки,
	|	ТаблицаТовары.НоменклатураНабора		   КАК НоменклатураНабора,
	|	ТаблицаТовары.ХарактеристикаНабора		   КАК ХарактеристикаНабора,
	|	ТаблицаТовары.Номенклатура		 		   КАК Номенклатура,
	|	ТаблицаТовары.Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	|	ТаблицаТовары.Характеристика			   КАК Характеристика,
	|	ТаблицаТовары.Назначение				   КАК Назначение,
	|	ТаблицаТовары.Серия						   КАК Серия,
	|	ТаблицаТовары.СтатусУказанияСерий		   КАК СтатусУказанияСерий,
	|	ТаблицаТовары.Содержание				   КАК Содержание,
	|	ТаблицаТовары.Упаковка					   КАК Упаковка,
	|	ТаблицаТовары.КоличествоУпаковок		   КАК КоличествоУпаковок,
	|	ТаблицаТовары.Количество				   КАК Количество,
	|	ТаблицаТовары.Сумма						   КАК Сумма,
	|	ТаблицаТовары.СтавкаНДС					   КАК СтавкаНДС,
	|	ТаблицаТовары.СтатьяДоходов				   КАК СтатьяДоходов,
	|	ТаблицаТовары.АналитикаДоходов			   КАК АналитикаДоходов,
	|	ТаблицаТовары.СуммаНДС					   КАК СуммаНДС,
	|	ТаблицаТовары.СуммаСНДС					   КАК СуммаСНДС,
	|	ТаблицаТовары.КодСтроки					   КАК КодСтроки,
	|	ТаблицаТовары.Склад						   КАК Склад,
	|	ТаблицаТовары.ЗаказКлиента				   КАК ЗаказКлиента,
	|	ТаблицаТовары.КодТНВЭД					   КАК КодТНВЭД,
	|	ТаблицаТовары.Цена						   КАК Цена,
	|	ТаблицаТовары.ВидЦены					   КАК ВидЦены
	|
	|ПОМЕСТИТЬ ИсходныеДанные
	|ИЗ
	|	Документ.КорректировкаРеализации.Товары КАК ТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеПоследнейКорректировки КАК ДанныеПоследнейКорректировки
	|		ПО ТаблицаТовары.Ссылка = ДанныеПоследнейКорректировки.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|" + ТекстЗапросаПоОснованию + "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДанныеПоследнейКорректировки
	|";
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	Запрос.Выполнить();
	
КонецПроцедуры

Функция УТ11_4_ТекстЗапросаПоИсходнымДаннымРеализацияТоваровУслуг()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки                                   КАК НомерСтроки,
	|	ТаблицаТовары.НоменклатураНабора                            КАК НоменклатураНабора,
	|	ТаблицаТовары.ХарактеристикаНабора                          КАК ХарактеристикаНабора,
	|	ТаблицаТовары.Номенклатура                                  КАК Номенклатура,
	|	ТаблицаТовары.Номенклатура.ТипНоменклатуры                  КАК ТипНоменклатуры,
	|	ТаблицаТовары.Характеристика                                КАК Характеристика,
	|	ТаблицаТовары.Назначение                                    КАК Назначение,
	|	ВЫБОР КОГДА ТаблицаТовары.СтатусУказанияСерий = 14
	|		ТОГДА ТаблицаТовары.Серия
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ                                                       КАК Серия,
	|	ВЫБОР КОГДА ТаблицаТовары.СтатусУказанияСерий = 14
	|		ТОГДА 14
	|		ИНАЧЕ 0
	|	КОНЕЦ                                                       КАК СтатусУказанияСерий,
	|	""""                                                        КАК Содержание,
	|	ТаблицаТовары.Упаковка                                      КАК Упаковка,
	|	ТаблицаТовары.КоличествоУпаковок                            КАК КоличествоУпаковок,
	|	ТаблицаТовары.Количество                                    КАК Количество,
	|	ТаблицаТовары.Сумма                                         КАК Сумма,
	|	ТаблицаТовары.СтавкаНДС                                     КАК СтавкаНДС,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.ПустаяСсылка) КАК СтатьяДоходов,
	|	Неопределено                                                КАК АналитикаДоходов,
	|	ТаблицаТовары.СуммаНДС                                      КАК СуммаНДС,
	|	ТаблицаТовары.СуммаСНДС                                     КАК СуммаСНДС,
	|	ТаблицаТовары.КодСтроки                                     КАК КодСтроки,
	|	ТаблицаТовары.Склад                                         КАК Склад,
	|	ТаблицаТовары.ЗаказКлиента                                  КАК ЗаказКлиента,
	|	ТаблицаТовары.КодТНВЭД                                      КАК КодТНВЭД,
	|	
	|	ВЫБОР
	|		КОГДА ВЫРАЗИТЬ(ТаблицаТовары.Цена * ТаблицаТовары.КоличествоУпаковок КАК ЧИСЛО(31,2)) = ТаблицаТовары.Сумма
	|			ТОГДА ТаблицаТовары.Цена
	|		ИНАЧЕ ВЫРАЗИТЬ(ТаблицаТовары.Сумма / ТаблицаТовары.КоличествоУпаковок КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК Цена,
	|	
	|	ВЫБОР КОГДА ВЫРАЗИТЬ(ТаблицаТовары.Цена * ТаблицаТовары.КоличествоУпаковок КАК ЧИСЛО(31,2)) = ТаблицаТовары.Сумма ТОГДА
	|		ТаблицаТовары.ВидЦены
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Справочник.ВидыЦен.ПустаяСсылка)
	|	КОНЕЦ КАК ВидЦены
	|
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК ТаблицаТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеПоследнейКорректировки КАК ДанныеПоследнейКорректировки
	|		ПО ИСТИНА
	|ГДЕ
	|	ДанныеПоследнейКорректировки.Ссылка ЕСТЬ NULL 
	|	И ТаблицаТовары.Ссылка = &ДокументОснование
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция УТ11_4_ТекстЗапросаПоИсходнымДаннымАктВыполненныхРабот()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТаблицаУслуги.НомерСтроки                                   КАК НомерСтроки,
	|	ТаблицаУслуги.НоменклатураНабора                            КАК НоменклатураНабора,
	|	ТаблицаУслуги.ХарактеристикаНабора                          КАК ХарактеристикаНабора,
	|	ТаблицаУслуги.Номенклатура                                  КАК Номенклатура,
	|	ТаблицаУслуги.Номенклатура.ТипНоменклатуры                  КАК ТипНоменклатуры,
	|	ТаблицаУслуги.Характеристика                                КАК Характеристика,
	|	ТаблицаУслуги.Назначение                                    КАК Назначение,
	|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)         КАК Серия,
	|	0                                                           КАК СтатусУказанияСерий,
	|	ТаблицаУслуги.Содержание                                    КАК Содержание,
	|	ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)  КАК Упаковка,
	|	ТаблицаУслуги.Количество                                    КАК КоличествоУпаковок,
	|	ТаблицаУслуги.Количество                                    КАК Количество,
	|	ТаблицаУслуги.Сумма                                         КАК Сумма,
	|	ТаблицаУслуги.СтавкаНДС                                     КАК СтавкаНДС,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.ПустаяСсылка) КАК СтатьяДоходов,
	|	Неопределено                                                КАК АналитикаДоходов,
	|	ТаблицаУслуги.СуммаНДС                                      КАК СуммаНДС,
	|	ТаблицаУслуги.СуммаСНДС                                     КАК СуммаСНДС,
	|	ТаблицаУслуги.КодСтроки                                     КАК КодСтроки,
	|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)                    КАК Склад,
	|	ТаблицаУслуги.ЗаказКлиента                                  КАК ЗаказКлиента,
	|	ЗНАЧЕНИЕ(Справочник.КлассификаторТНВЭД.ПустаяСсылка)        КАК КодТНВЭД,
	|	
	|	ВЫБОР
	|		КОГДА ВЫРАЗИТЬ(ТаблицаУслуги.Цена * ТаблицаУслуги.Количество КАК ЧИСЛО(31,2)) = ТаблицаУслуги.Сумма
	|			ТОГДА ТаблицаУслуги.Цена
	|		ИНАЧЕ ВЫРАЗИТЬ(ТаблицаУслуги.Сумма / ТаблицаУслуги.Количество КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК Цена,
	|	
	|	ВЫБОР КОГДА ВЫРАЗИТЬ(ТаблицаУслуги.Цена * ТаблицаУслуги.Количество КАК ЧИСЛО(31,2)) = ТаблицаУслуги.Сумма ТОГДА
	|		ТаблицаУслуги.ВидЦены
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Справочник.ВидыЦен.ПустаяСсылка)
	|	КОНЕЦ КАК ВидЦены
	|
	|ИЗ
	|	Документ.АктВыполненныхРабот.Услуги КАК ТаблицаУслуги
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеПоследнейКорректировки КАК ДанныеПоследнейКорректировки
	|		ПО ИСТИНА
	|
	|ГДЕ
	|	ДанныеПоследнейКорректировки.Ссылка ЕСТЬ NULL 
	|	И ТаблицаУслуги.Ссылка = &ДокументОснование
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция УТ11_4_ТекстЗапросаПоИсходнымДаннымРеализацияУслугПрочихАктивов()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТаблицаДоходы.НомерСтроки                                    КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)               КАК НоменклатураНабора,
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК ХарактеристикаНабора,
	|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)               КАК Номенклатура,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.ПустаяСсылка)         КАК ТипНоменклатуры,
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК Характеристика,
	|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)                 КАК Назначение,
	|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)          КАК Серия,
	|	0                                                            КАК СтатусУказанияСерий,
	|	ТаблицаДоходы.Содержание                                     КАК Содержание,
	|	ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)   КАК Упаковка,
	|	ТаблицаДоходы.Количество                                     КАК КоличествоУпаковок,
	|	ТаблицаДоходы.Количество                                     КАК Количество,
	|	ТаблицаДоходы.Сумма                                          КАК Сумма,
	|	ТаблицаДоходы.СтавкаНДС                                      КАК СтавкаНДС,
	|	ТаблицаДоходы.СтатьяДоходов                                  КАК СтатьяДоходов,
	|	ТаблицаДоходы.АналитикаДоходов                               КАК АналитикаДоходов,
	|	ТаблицаДоходы.СуммаНДС                                       КАК СуммаНДС,
	|	ТаблицаДоходы.СуммаСНДС                                      КАК СуммаСНДС,
	|	0                                                            КАК КодСтроки,
	|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)                     КАК Склад,
	|	ЗНАЧЕНИЕ(Документ.ЗаказКлиента.ПустаяСсылка)                 КАК ЗаказКлиента,
	|	ЗНАЧЕНИЕ(Справочник.КлассификаторТНВЭД.ПустаяСсылка)         КАК КодТНВЭД,
	|
	|	ТаблицаДоходы.Цена КАК Цена,
	|	ЗНАЧЕНИЕ(Справочник.ВидыЦен.ПустаяСсылка) КАК ВидЦены
	|
	|ИЗ
	|	Документ.РеализацияУслугПрочихАктивов.Доходы КАК ТаблицаДоходы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеПоследнейКорректировки КАК ДанныеПоследнейКорректировки
	|		ПО ИСТИНА
	|ГДЕ
	|	ДанныеПоследнейКорректировки.Ссылка ЕСТЬ NULL 
	|	И ТаблицаДоходы.Ссылка = &ДокументОснование
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

