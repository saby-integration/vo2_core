
// Функция - Определяет, есть справочники, документы и др. таблицы, а так же их реквизиты
// в метаданных конфигурации.
//
// Параметры:
//  ПутьОбъекта	 - Строка	 - Строка по типу: "Контрагенты", "ДополнительныеСведения.Объект".
//   Может содержать Название метаданных (справочник, документ и т.д.): определяет есть ли
//   объект с таким названием. Может серез точку быть указано Название реквизита.
//   Тогда определяется, если ли такой реквизит у объекта метаданных.
//  ДопПараметры - Структура	 - Содержит описание коллекций метаданных, в которых
//   производится поиск.
//   КлассМетаданных (строка) - Справочники, Документы, РегистрыСведений или ПланыВидовХарактеристик.
//   НаборОбъектов (строка) - Реквизиты, ТабличныеЧасти, Измерения или Ресурсы.
//   Объект (любая ссылка) - Ссылка на элемент Справочника, Документа или Плана видов.
//   
// Для перечислений  КлассМетаданных = "Перечисления", ПутьОбъекта = НазваниеПеречисления.Значение
// Возвращаемое значение:
//  Булево - Истина, если указанный объект есть
//
Функция СбисСуществуетОбъектКонфигурацииСервер(ПутьОбъекта, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = Новый Структура;
	КонецЕсли;
	
	ПутьМассивом = Новый Массив;
	ПутьВСтроках = СтрЗаменить(ПутьОбъекта, ".", Символы.ПС);
	
	Для НомерСтроки = 1 По СтрЧислоСтрок(ПутьВСтроках) Цикл
		ПутьМассивом.Добавить(СокрЛП(СтрПолучитьСтроку(ПутьВСтроках, НомерСтроки)));
	КонецЦикла;
	
	КонтекстПолученияПути = Новый Структура("ОбъектМетаданных, ПутьКМетаданным, ПутьОбъекта", Метаданные, Новый Структура, ПутьМассивом);
	ОпределитьПутьМетаданных(КонтекстПолученияПути, ДопПараметры);
	ОбъектМетаданных = КонтекстПолученияПути.ОбъектМетаданных;
	ПутьКМетаданным = КонтекстПолученияПути.ПутьКМетаданным;
	
	Для каждого ЧастьПутиОбъекта Из ПутьМассивом Цикл
		
		РезультатПоиска = Ложь; 
		
		Если ПутьКМетаданным = Неопределено Тогда
			Возврат РезультатПоиска;
		КонецЕсли;
		
		Для каждого ЧастьПутиМетаданных Из ПутьКМетаданным Цикл
			
			Попытка
				Если ЧастьПутиМетаданных.Ключ = "Перечисления" Тогда
					
					ЭлементКоллекцииМетаданных = ОбъектМетаданных.Перечисления[ПутьМассивом[0]].ЗначенияПеречисления.Найти(ПутьМассивом[1]); 
					Если ЭлементКоллекцииМетаданных = Неопределено Тогда
						Продолжить;	
					КонецЕсли;	
					РезультатПоиска = Истина;
					Возврат РезультатПоиска;
					
				Иначе
					ЭлементКоллекцииМетаданных = ОбъектМетаданных[ЧастьПутиМетаданных.Ключ].Найти(ЧастьПутиОбъекта);
					
				КонецЕсли;
			Исключение
				ВызватьИсключение("Неизвестная коллекция метаданных " + ЧастьПутиМетаданных.Ключ);
			КонецПопытки;
			
			Если ЭлементКоллекцииМетаданных = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			ОбъектМетаданных = ЭлементКоллекцииМетаданных;
			ПутьКМетаданным = ЧастьПутиМетаданных.Значение;
			РезультатПоиска = Истина;
			Прервать;
			
		КонецЦикла;
		
		Если НЕ РезультатПоиска Тогда
			Возврат РезультатПоиска;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат РезультатПоиска;
	
КонецФункции

Процедура ОпределитьПутьМетаданных(КонтекстПоискаПути, ДопПараметры)
	Перем КлассМетаданных, НаборОбъектов;
	
	Если ДопПараметры.Свойство("Объект")
		И Метаданные.НайтиПоТипу(ТипЗнч(ДопПараметры.Объект)) <> Неопределено Тогда 	
		
		КонтекстПоискаПути.ОбъектМетаданных = Метаданные.НайтиПоТипу(ТипЗнч(ДопПараметры.Объект));
		
		Если НЕ ДопПараметры.Свойство("НаборОбъектов", НаборОбъектов) Тогда
			КонтекстПоискаПути.ПутьКМетаданным = Неопределено;
		ИначеЕсли КонтекстПоискаПути.ПутьОбъекта.Количество() < 3 Тогда
			КонтекстПоискаПути.ПутьКМетаданным = Новый Структура(НаборОбъектов);
		Иначе

			ОбъектыТабличнойЧасти = Новый Структура("Реквизиты");
			КонтекстПоискаПути.ПутьКМетаданным = Новый Структура(НаборОбъектов, ОбъектыТабличнойЧасти);
		КонецЕсли;
		
	Иначе
		
		Если НЕ ДопПараметры.Свойство("КлассМетаданных", КлассМетаданных) Тогда
			КонтекстПоискаПути.ПутьКМетаданным = Неопределено;	
		ИначеЕсли НЕ ДопПараметры.Свойство("НаборОбъектов", НаборОбъектов) Тогда
			КонтекстПоискаПути.ПутьКМетаданным.Вставить(КлассМетаданных, Неопределено);
		ИначеЕсли КонтекстПоискаПути.ПутьОбъекта.Количество() < 3 Тогда
			КонтекстПоискаПути.ПутьКМетаданным.Вставить(КлассМетаданных, Новый Структура(НаборОбъектов));
		Иначе

			ОбъектыТабличнойЧасти = Новый Структура("Реквизиты");
			КонтекстПоискаПути.ПутьКМетаданным.Вставить(КлассМетаданных, Новый Структура(НаборОбъектов, ОбъектыТабличнойЧасти));
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры
