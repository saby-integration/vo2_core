
//Процедура генерирует и вызывает ExtSysException(СбисИсключение)
Процедура ВызватьСбисИсключениеСервер(БазоваяОшибка=Неопределено, СбисИмяКоманды,
								СбисКод=Неопределено, СбисСообщение=Неопределено, СбисДетализация=Неопределено, СбисДамп=Неопределено, СбисХелп=Неопределено) Экспорт
	ИсключениеВызов = НовыйСбисИсключениеСервер(БазоваяОшибка, СбисИмяКоманды, СбисКод, СбисСообщение, СбисДетализация, СбисДамп, СбисХелп);
	ВызватьИсключение(ЗначениеВСтрокуВнутр(ИсключениеВызов));
КонецПроцедуры

//Конструктор ExtSysException
//БазоваяОшибка		- (необяз) ExtSysException(СбисИсключение)/ИнформацияОбОшибке/Структура("code, message, detail, dump, help")/Строка (в json/произвольная)/Число(код ошибки)
//СбисИмяКоманды	- Имя команды в стек
//Работает если в БазовойОшибке что-то неопределено, иначе берутся значения по-умолчанию
//СбисКод			- (необяз)код для новой ошибки. 		По-умолч 100
//СбисСообщение		- (необяз)сообщение для новой ошибки.	По-умолч сообщение по коду 
//СбисДетализация	- (необяз)детализация для новой ошибки.	По-умолч сообщение по коду
//СбисДамп			- (необяз)дамп ошибки
//СбисХелп			- (необяз)ссылка на решение проблемы
Функция НовыйСбисИсключениеСервер(БазоваяОшибка=Неопределено,	СбисИмяКоманды,
							СбисКод=Неопределено,		СбисСообщение=Неопределено, СбисДетализация=Неопределено, 
							СбисДамп=Неопределено,		СбисХелп=Неопределено) Экспорт
	Перем СбисСтек, СбисОсновнаяОшибка, СтрокаРазбора;
	СбисИсключение = СбисИсключение_Пустое();
	СбисИсключение.code			= СбисКод;
	СбисИсключение.message		= СбисСообщение;
	СбисИсключение.details		= СбисДетализация;
	СбисИсключение.methodName	= СбисИмяКоманды;
	СбисИсключение.dump			= СбисДамп;
	СбисИсключение.help			= СбисХелп;

	//Проверить перегрузки для конструктора
	Если		ТипЗнч(БазоваяОшибка) = Тип("ИнформацияОбОшибке")	Тогда
		СбисОсновнаяОшибка = СбисИсключение_ИнформацияОбОшибке(БазоваяОшибка);
	ИначеЕсли	ТипЗнч(БазоваяОшибка) = Тип("Строка")				Тогда
		СбисОсновнаяОшибка = СбисИсключение_Строка(БазоваяОшибка);
	ИначеЕсли	ТипЗнч(БазоваяОшибка) = Тип("Число")				Тогда
		СбисОсновнаяОшибка = СбисИсключение_Число(БазоваяОшибка);
	ИначеЕсли	ТипЗнч(БазоваяОшибка) = Тип("Структура")			Тогда
		СбисОсновнаяОшибка = СбисИсключение_Структура(БазоваяОшибка);
	Иначе
		//Неизвестная ошибка
		СбисОсновнаяОшибка = СбисИсключение_Пустое();
		Если Не БазоваяОшибка = Неопределено Тогда
			СбисОсновнаяОшибка.dump = Новый Структура("exception_value", БазоваяОшибка);
		КонецЕсли;
	КонецЕсли;
	
	//Если не указаны основные поля для новой ошибки
	Если СбисИсключение.code = Неопределено Тогда
		Если СбисОсновнаяОшибка = Неопределено Тогда
			СбисИсключение.code = 100;
		Иначе
			СбисИсключение.code = СбисОсновнаяОшибка.code;
		КонецЕсли;
	КонецЕсли;
	Если СбисИсключение.message = Неопределено Тогда
		Если		СбисОсновнаяОшибка = Неопределено
			Или	Не	СбисОсновнаяОшибка.Свойство("message", СбисИсключение.message)
			Или		СбисИсключение.message = Неопределено Тогда
			СбисИсключение.message = СбисИсключение_СообщениеПоКоду(СбисИсключение.code);
		КонецЕсли;
	КонецЕсли;
	Если СбисИсключение.details = Неопределено Тогда
		Если СбисОсновнаяОшибка = Неопределено Тогда
			СбисИсключение.details = СбисИсключение.message;
		ИначеЕсли Не СбисОсновнаяОшибка.Свойство("details", СбисИсключение.details) Тогда
			СбисИсключение.details = СбисОсновнаяОшибка.detail;
		КонецЕсли;
	КонецЕсли;
	Если СбисИсключение.help = Неопределено Тогда
		Если	СбисОсновнаяОшибка <> Неопределено
			И	СбисОсновнаяОшибка.Свойство("help") Тогда
			СбисИсключение.help = СбисОсновнаяОшибка.help;
		КонецЕсли;
	КонецЕсли;
	
	Если СбисОсновнаяОшибка = Неопределено Тогда
		Возврат СбисИсключение;
	ИначеЕсли Не СбисОсновнаяОшибка.Свойство("base", СбисИсключение.base) Тогда
		СбисИсключение.base = СбисОсновнаяОшибка;
	КонецЕсли;
	//Добавим основную ошибку в стек
	Если СбисОсновнаяОшибка.Свойство("stack", СбисСтек) Тогда
		СбисОсновнаяОшибка.Удалить("stack");
	Иначе
		СбисСтек = Новый Массив;
		СбисЗаписьВСтек = Новый Структура("methodName, code, message, dump");
		ЗаполнитьЗначенияСвойств(СбисЗаписьВСтек, СбисОсновнаяОшибка);
		Если		СбисОсновнаяОшибка.Свойство("method_Name") Тогда
			СбисЗаписьВСтек.methodName = СбисОсновнаяОшибка.method_Name;
		ИначеЕсли	СбисОсновнаяОшибка.Свойство("Action") Тогда
			СбисЗаписьВСтек.methodName = СбисОсновнаяОшибка.Action;
		КонецЕсли;
		СбисСтек.Добавить(СбисЗаписьВСтек);
	КонецЕсли;
	//Добавим обрабатываемую ошибку в стек
	СбисЗаписьВСтек = Новый Структура("methodName, code, message, dump");
	ЗаполнитьЗначенияСвойств(СбисЗаписьВСтек, СбисИсключение);
	СбисСтек.Вставить(0, СбисЗаписьВСтек);
	СбисИсключение.Вставить("stack", СбисСтек);
	Если СбисОсновнаяОшибка.Свойство("data") Тогда
		СбисИсключение.Вставить("data", СбисОсновнаяОшибка.data);
	КонецЕсли;
	//пробросим дамп в основную ошибку, чтоб не потерять
	Если СбисДамп = Неопределено Тогда
		Если СбисОсновнаяОшибка.Свойство("dump", СбисИсключение.dump) Тогда
			СбисОсновнаяОшибка.Удалить("dump");
		КонецЕсли;
	КонецЕсли;
	Возврат СбисИсключение;
	
КонецФункции

// Функция - возвращает пустое исключение
// 
// Возвращаемое значение:
//   - 
//
Функция СбисИсключение_Пустое()

	Возврат Новый Структура("code, message, details, methodName, dump, help, base, data", 100);

КонецФункции

// Функция - возвращает пустое исключение
// 
// Возвращаемое значение:
//   - 
//
Функция СбисИсключение_ВСтрокуСервер(СбисИсключение, ДопПарамеры = Неопределено) Экспорт 

	Если	Не ТипЗнч(СбисИсключение) = Тип("Структура")
		Или Не СбисИсключение.Свойство("code") 
		Или Не СбисИсключение.Свойство("details") Тогда
		ИсключениеВСтроку = НовыйСбисИсключениеСервер(СбисИсключение, ДопПарамеры);
	Иначе
		ИсключениеВСтроку = СбисИсключение;
	КонецЕсли;
	Возврат ЗначениеВСтрокуВнутр(ИсключениеВСтроку);

КонецФункции

// Функция - генерирует ExtSysException из информации об ошибке
//
// Параметры:
//  ВыбранныйИнформацияОбОшибке	 - ИнформацияОбОшибке	 - вызванное исключение, которое не является запакованным ExtSysException
// 
// Возвращаемое значение:
//  Структура - ExtSysException
//
Функция СбисИсключение_ИнформацияОбОшибке(БазоваяОшибка)
	
	Попытка
		Возврат ЗначениеИзСтрокиВнутр(БазоваяОшибка.Описание);
	Исключение
		//Это не завёрнутый ExtSysException, дальше разбираем как 1С-ное исключение
	КонецПопытки;

	СложнаяСтруктура		= Ложь;
	КодНовогоИсключения		= 500;
	
	СтруктураОшибкиСообщения = БазоваяОшибка;
	Пока ТипЗнч(СтруктураОшибкиСообщения.Причина) = Тип("ИнформацияОбОшибке") Цикл
		СтруктураОшибкиСообщения = СтруктураОшибкиСообщения.Причина;
		СложнаяСтруктура = Истина;
	КонецЦикла;
	
	Если СложнаяСтруктура Тогда
		СообщениеОбОшибке = БазоваяОшибка.Описание;
		ДетализацияОшибки = СтруктураОшибкиСообщения.Описание;
	ИначеЕсли БазоваяОшибка.Причина = Неопределено Тогда
		СообщениеОбОшибке = СбисИсключение_СообщениеПоКоду(КодНовогоИсключения);
		ДетализацияОшибки = БазоваяОшибка.Описание;
	Иначе
		СообщениеОбОшибке = БазоваяОшибка.Описание;
		ДетализацияОшибки = БазоваяОшибка.Причина;
	КонецЕсли;
	Если Найти(ДетализацияОшибки, "ODBC") Тогда
		Если Не Найти(ДетализацияОшибки, "dbf' уже существует") Тогда
			КодНовогоИсключения	= 776; 
			СообщениеОбОшибке = ДетализацияОшибки;
		КонецЕсли;
	ИначеЕсли ДетализацияОшибки = "Не подключен файл статусов DBF." Тогда
		КодНовогоИсключения	= 717;
		ДетализацияОшибки	= СообщениеОбОшибке + " " + ДетализацияОшибки;
		СообщениеОбОшибке	= СбисИсключение_СообщениеПоКоду(КодНовогоИсключения);
	КонецЕсли;
	
	СбисДамп = Новый Структура("ИсходнаяСтрока, НомерСтроки, ДетализацияОшибки", БазоваяОшибка.ИсходнаяСтрока, БазоваяОшибка.НомерСтроки, ДетализацияОшибки);
	СбисОсновнаяОшибка = Новый Структура("code, message, details", КодНовогоИсключения, СообщениеОбОшибке, ДетализацияОшибки);
	СбисОсновнаяОшибка.Вставить("methodName",	БазоваяОшибка.ИмяМодуля);
	СбисОсновнаяОшибка.Вставить("dump",			СбисДамп);
	СбисОсновнаяОшибка.Вставить("help",			СбисИсключение_РешениеПроблемыПоИсключению(СбисОсновнаяОшибка));
	
	//СбисОсновнаяОшибка = Новый Структура("code,details,methodName,dump,message,help", КодНовогоИсключения, ДетализацияОшибки, БазоваяОшибка.ИмяМодуля, СбисДамп, СообщениеОбОшибке, СбисХелп);
	Возврат СбисОсновнаяОшибка;
	
КонецФункции

// Функция - генерирует ExtSysException из строки
//
// Параметры:
//  БазоваяОшибка	 - Строка	 - исключение, которое может быть сериализованнйо в JSON строкой, либо просто сообщением.
// 
// Возвращаемое значение:
//  Структура - ExtSysException
//
Функция СбисИсключение_Строка(БазоваяОшибка)
    Перем СтрокаРазбора;
	
	СбисОсновнаяОшибка = СбисИсключение_Пустое();
	
	Если Лев(БазоваяОшибка, 1) = "{" Тогда
		//Это JSON - объект с ошибкой
		СтрокаРазбора = БазоваяОшибка;
	ИначеЕсли	Лев(БазоваяОшибка,35) = "callBLObject(): Метод бизнес логики"
		И		Найти(БазоваяОшибка,"вернул ошибку") Тогда
		//Это ошибка от БЛ в формате JSON
		СтрокаРазбора = Сред(БазоваяОшибка, Найти(БазоваяОшибка,"{"));
		СтрокаРазбора = Лев(СтрокаРазбора, СтрДлина(СтрокаРазбора)-1);
	ИначеЕсли Найти(БазоваяОшибка,"{""jsonrpc"":") Тогда
		//Это ошибка от БЛ в формате JSON
		СтрокаРазбора = Сред(БазоваяОшибка, Найти(БазоваяОшибка,"{""jsonrpc"":"));
	КонецЕсли;	
	Если СтрокаРазбора = Неопределено Тогда
		//Это сообщение неизвестной ошибки.
		СбисОсновнаяОшибка.message	= БазоваяОшибка;
		СбисОсновнаяОшибка.details	= БазоваяОшибка;
	Иначе
		Попытка
			СбисОсновнаяОшибка = СбисПрочитатьJSON_НаСервере(СтрокаРазбора);
			Если СбисОсновнаяОшибка.Свойство("Error") Тогда
				СбисОсновнаяОшибка = СбисОсновнаяОшибка.Error;
			КонецЕсли;
		Исключение
			СбисОсновнаяОшибка.message	= БазоваяОшибка;
			СбисОсновнаяОшибка.details	= БазоваяОшибка;
		КонецПопытки;
	КонецЕсли;
	Возврат СбисОсновнаяОшибка;
	
КонецФункции	

Функция СбисИсключение_Число(БазоваяОшибка)
	
	СбисОсновнаяОшибка = СбисИсключение_Пустое();
	СбисОсновнаяОшибка.code		= БазоваяОшибка;
	СбисОсновнаяОшибка.message	= СбисИсключение_СообщениеПоКоду(БазоваяОшибка);
	СбисОсновнаяОшибка.details	= СбисОсновнаяОшибка.message;
    Возврат СбисОсновнаяОшибка;
	
КонецФункции

// Функция - генерирует ExtSysException из структуры
//
// Параметры:
//  БазоваяОшибка	 - Строка	 - исключение, которое может быть сериализованнйо в JSON строкой, либо просто сообщением.
// 
// Возвращаемое значение:
//  Структура - ExtSysException
//
Функция СбисИсключение_Структура(БазоваяОшибка)
	
	СбисОсновнаяОшибка = СбисИсключение_Пустое();
	Если БазоваяОшибка.Свойство("code") Тогда
		ЗаполнитьЗначенияСвойств(СбисОсновнаяОшибка, БазоваяОшибка);
		СбисТест =	(СбисОсновнаяОшибка.details = Неопределено
				И	БазоваяОшибка.Свойство("detail",		СбисОсновнаяОшибка.details));
		СбисТест =	(СбисОсновнаяОшибка.methodName = Неопределено
				И	БазоваяОшибка.Свойство("method_name",	СбисОсновнаяОшибка.methodName));
	Иначе
		СбисОсновнаяОшибка.dump = Новый Структура("exception_value", БазоваяОшибка);
	КонецЕсли;
	
	Возврат СбисОсновнаяОшибка;
	
КонецФункции
	
// Функция - генерирует ExtSysException из информации об ошибке
//
// Параметры:
//  ВыбранныйИнформацияОбОшибке	 - ИнформацияОбОшибке	 - вызванное исключение, которое не является запакованным ExtSysException
// 
// Возвращаемое значение:
//  Структура - ExtSysException
//
Функция СбисИсключение_РешениеПроблемыПоИсключению(СбисИсключение)
	
	Если СбисИсключение.code = 776 Тогда
		Если	Найти(СбисИсключение.methodName, "ДБФ")
			И	СбисИсключение.message = СбисИсключение.details Тогда 
			Возврат "https://sbis.ru/help/integration/catalog/driver_not_found";
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

Функция СбисИсключение_СообщениеПоКоду(СбисКодОшибки) Экспорт
	Если		СбисКодОшибки = 100 Тогда
		Возврат "Неизвестная ошибка системы";
	ИначеЕсли	СбисКодОшибки = 300 Тогда
		Возврат "Неизвестная ошибка БЛ";
	ИначеЕсли	СбисКодОшибки = 301 Тогда
		Возврат "Сервис СБИС.Диск временно недоступен";
	ИначеЕсли	СбисКодОшибки = 400 Тогда
		Возврат "Неизвестная ошибка СБИС Плагина";
	ИначеЕсли	СбисКодОшибки = 412 Тогда
		Возврат	"Работа СБИС Плагин остановлена";
	ИначеЕсли	СбисКодОшибки = 409 Тогда
		Возврат "Сеанс с учетной системой завершен";
	ИначеЕсли	СбисКодОшибки = 500 Тогда
		Возврат "Неизвестная ошибка клиента";
	ИначеЕсли	СбисКодОшибки = 600 Тогда
		Возврат "Неизвестная ошибка настроек";
	ИначеЕсли	СбисКодОшибки = 610 Тогда
		Возврат "Отсутствует файл настроек для данного типа данных";		
	ИначеЕсли	СбисКодОшибки = 700 Тогда
		Возврат "Неизвестная ошибка подключения";
	ИначеЕсли	СбисКодОшибки = 717 Тогда
		Возврат "Несовместимая версия файла базы данных";
	ИначеЕсли	СбисКодОшибки = 721 Тогда
		Возврат "Неверно указан ИНН";
	ИначеЕсли	СбисКодОшибки = 724 Тогда
		Возврат "Организация неопределена";
	ИначеЕсли	СбисКодОшибки = 726 Тогда
		Возврат "Вложения неопределены";
	ИначеЕсли	СбисКодОшибки = 735 Тогда
		Возврат "Данная операция недоступна";
	ИначеЕсли	СбисКодОшибки = 755 Тогда
		Возврат "Не найден сертификат ЭП";
	ИначеЕсли	СбисКодОшибки = 756 Тогда
		Возврат "Подключение недоступно";
	ИначеЕсли	СбисКодОшибки = 760 Тогда
		Возврат "Неверные параметры фильтра";
	ИначеЕсли	СбисКодОшибки = 762 Тогда
		Возврат "Версия системы не поддерживается";
	ИначеЕсли	СбисКодОшибки = 770 Тогда
		Возврат "Ошибка XSLT";
	ИначеЕсли	СбисКодОшибки = 772 Тогда
		Возврат "Ошибка работы с файловой системой";
	ИначеЕсли	СбисКодОшибки = 773 Тогда
		Возврат "Неизвестная ошибка при выполнении метода";	
	ИначеЕсли	СбисКодОшибки = 775 Тогда
		Возврат "Не получен ответ от плагина";	
	ИначеЕсли	СбисКодОшибки = 776 Тогда
		Возврат "Запуск программы/функции/метода не удался";	
	ИначеЕсли	СбисКодОшибки = 779 Тогда
		Возврат "Не найден объект";	
	ИначеЕсли 	СбисКодОшибки = 784 Тогда
		Возврат "Ошибка в настройках соединения";
	ИначеЕсли	СбисКодОшибки = 790 Тогда
		Возврат "Обработка прервана пользователем"
	КонецЕсли;
	Возврат "Неизвестная ошибка системы";
КонецФункции

