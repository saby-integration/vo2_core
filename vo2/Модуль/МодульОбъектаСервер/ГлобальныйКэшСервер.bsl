
// Процедура - обновляет параметр глобального модуля на сервере
//
// Параметры:
//  ИмяПараметраМодуля	 - Строка	 - имя обновляемого параметра
//  АдресПараметраМодуля - ФиксированнаяСтруктрура	 - Тип и Значение
//
Процедура ОбновитьПараметрГлобальногоМодуляНаСервере(ИмяПараметраМодуля, АдресПараметраМодуля) Экспорт
	
	Если ИмяПараметраМодуля = "ОчиститьГлобальныйКэш" Тогда
		
		ОчиститьГлобальныйКэшНаСервере();
		Возврат;
		
	КонецЕсли;
	
	Если АдресПараметраМодуля.Тип = "Адрес" Тогда
	
		ЗначениеПараметраМодуля = ПолучитьИзВременногоХранилища(АдресПараметраМодуля.Значение);
		
	Иначе
		
		ЗначениеПараметраМодуля = АдресПараметраМодуля.Значение;
		
	КонецЕсли;
	
	Если ИмяПараметраМодуля = "ГлобальныйКэш" Тогда
		
		ГлобальныйКэш = ЗначениеПараметраМодуля;
		СохранитьГлобальныйКэшНаСервере();
		Возврат;
		
	КонецЕсли;
	
	ГлобальныйКэш = ПолучитьГлобальныйКэшНаСервере();
	
	Если	ИмяПараметраМодуля = "Парам"			Тогда
		
		Для Каждого КлючИЗначениеПараметра Из ЗначениеПараметраМодуля Цикл
			
			ГлобальныйКэш.Парам.Вставить(КлючИЗначениеПараметра.Ключ, КлючИЗначениеПараметра.Значение);
			
		КонецЦикла;
		
	ИначеЕсли	ИмяПараметраМодуля = "ПараметрыСистемы"	Тогда
		
		ГлобальныйКэш.Вставить("ПараметрыСистемы", ЗначениеПараметраМодуля);
		
	КонецЕсли;
	
	СохранитьГлобальныйКэшНаСервере()
	
КонецПроцедуры

Функция ПолучитьГлобальныйКэшНаСервере()
	
	Если ГлобальныйКэш = Неопределено Тогда
		
		ВосстановитьГлобальныйКэшНаСервере();
		
	КонецЕсли;
	
	Возврат ГлобальныйКэш;
	
КонецФункции

Процедура ОчиститьГлобальныйКэшНаСервере()
	
	#Если Не НаКлиенте Тогда
		
		УстановитьПривилегированныйРежим(Истина);

		Попытка
			
			ХранилищеОбщихНастроек.Удалить("Integration", "ГлобальныйКэшСервер");
			
		Исключение
			
			//Обёрнуто скорее на всякий случай, чтобы дальнейшее закрытие не падало.
			
		КонецПопытки;
		
		УстановитьПривилегированныйРежим(Ложь);
		
	#КонецЕсли
		
КонецПроцедуры

Процедура СохранитьГлобальныйКэшНаСервере()
	
	#Если Не НаКлиенте Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		
		ХранилищеОбщихНастроек.Сохранить("Integration", "ГлобальныйКэшСервер", ГлобальныйКэш);
		
		УстановитьПривилегированныйРежим(Ложь);
		
	#КонецЕсли
		
КонецПроцедуры

Процедура ВосстановитьГлобальныйКэшНаСервере()
	
	#Если ТолстыйКлиентОбычноеПриложение Тогда                                                
		
		СбисИсключение = НовыйСбисИсключение(500, "МодульОбъектаСервер.ВосстановитьГлобальныйКэшНаСервере",,,"Ошибка работы с кэш. Дальнейшая работа не возможна!");
		ВызватьИсключение СбисИсключение_ВСтроку(СбисИсключение);
		
	#Иначе
		
		УстановитьПривилегированныйРежим(Истина);
		
		ГлобальныйКэш = ХранилищеОбщихНастроек.Загрузить("Integration", "ГлобальныйКэшСервер");
		
		УстановитьПривилегированныйРежим(Ложь);
		
	#КонецЕсли
		
КонецПроцедуры

