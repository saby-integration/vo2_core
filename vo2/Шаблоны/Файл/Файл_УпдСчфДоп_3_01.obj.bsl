
&НаКлиенте
Перем Файл_Шаблон_Модуль; 

#Область include_local_ПолучитьМодульОбъекта
#КонецОбласти

&НаКлиенте
Функция СформироватьДокументДляГенератора(ПараметрыФормированияВходящие, Кэш) Экспорт
	
   	Файл_Шаблон_Модуль = Кэш.ГлавноеОкно.сбисПолучитьФорму("Файл_Шаблон");
	Файл_Шаблон_Модуль.СформироватьДокументДляГенератора(ПараметрыФормированияВходящие, Кэш);
	
	Если МодульОбъектаКлиент().ПолучитьЗначениеПараметраСбис("УПД24") Тогда
		
		ТекДокумент = ПараметрыФормированияВходящие.Документ.Файл.Документ;
		ТекДокумент.Вставить("НаимДокОпр",	"Документ об отгрузке товаров (выполнении работ), передаче имущественных прав (документ об оказании услуг)");
		ТекДокумент.Вставить("ПоФактХЖ",	"Документ об отгрузке товаров (выполнении работ), передаче имущественных прав (документ об оказании услуг)");
		
	КонецЕсли;
	
КонецФункции

// Процедура заполняет данные документа по его параметрам
//
// Параметры:
//  Док	 - Структура - Контекст рассчитываемого документа
//  Кэш	 - Структура - Кэш обработки
//
&НаКлиенте
Процедура ЗаполнитьПоДокументПараметр(Док, Кэш) Экспорт  
	
	Файл_Шаблон_Модуль = Кэш.ГлавноеОкно.сбисПолучитьФорму("Файл_Шаблон");
	Файл_Шаблон_Модуль.ЗаполнитьПоДокументПараметр(Док, Кэш);
	
	ТекДокумент = Док.Файл.Документ;

	Если НЕ ТекДокумент.Свойство("Параметр") Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ТекСтрока ИЗ ТекДокумент.Параметр Цикл

		Если ТекСтрока.Имя = "СвПерВещ" Тогда
			ТекДокумент.Вставить(ТекСтрока.Имя, ТекСтрока.Значение);
		КонецЕсли;  
		
		Если ТекСтрока.Имя = "ОбстФормСЧФ" Тогда
			ТекДокумент.Вставить(ТекСтрока.Имя, ТекСтрока.Значение);
		КонецЕсли;  
		
    КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьОснование(Док, Кэш) Экспорт

	ТекДокумент = Док.Файл.Документ;
	Если НЕ ТекДокумент.Свойство("Основание") Тогда
		Возврат;	
	КонецЕсли;
	
	Исключения = ПолучитьИсключенияОснование();
	ОснованиеМассив = Новый Массив;
	
	Для Каждого ТекСтрока ИЗ ТекДокумент.Основание Цикл
		
		Если ТекСтрока.Свойство("Тип") Тогда
			Ключ = "Тип";	
		Иначе
			Ключ = "Название";	
		КонецЕсли;
		
		Если Исключения.Получить(ТекСтрока[Ключ]) = Истина Тогда
			Продолжить;	
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ТекСтрока.Название) Тогда
			Продолжить;	
		КонецЕсли;
		
		Если НЕ ТекСтрока.Свойство("Дата") Тогда
			Продолжить;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ТекСтрока.Дата) И ТекСтрока.Название <> "-" Тогда
			Продолжить;	
		КонецЕсли; 
		
		Основание = Новый Структура("Номер, Название, Дата");
		Основание.Название 	= ТекСтрока.Название;
		Основание.Номер 	= ТекСтрока.Номер;
		Основание.Дата 		= ТекСтрока.Дата;
		
		ОснованиеМассив.Добавить(Основание);
		
	КонецЦикла;
	
	ЕстьОснование = ОснованиеМассив.Количество();
	УПД24 = МодульОбъектаКлиент().ПолучитьЗначениеПараметраСбис("УПД24");
	
	Если НЕ ЕстьОснование Тогда
		
		Если УПД24 Тогда
			ТекДокумент.Вставить("БезДокОснПер", "1");	
		Иначе  
			ОснованиеМассив.Добавить(Новый Структура("Название", "Без документа-основания"));
		КонецЕсли; 
		
	КонецЕсли;
	
	ТекДокумент.Вставить("Основание", ОснованиеМассив);
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьИсключенияОснование()
	
	ИсключенияИнфПол = Новый Соответствие;
	ИсключенияИнфПол.Вставить("ПРД", Истина);
	ИсключенияИнфПол.Вставить("СчФ", Истина);
	ИсключенияИнфПол.Вставить("КСчФ", Истина);
	ИсключенияИнфПол.Вставить("ИспрСчФ", Истина);
	ИсключенияИнфПол.Вставить("ИсхСчФ", Истина);
	ИсключенияИнфПол.Вставить("ИспрИсхСчФ", Истина);
	ИсключенияИнфПол.Вставить("ДокПодтвОтгр", Истина);
	
	Возврат ИсключенияИнфПол;
	
КонецФункции

//Штатный обработчик строки подстановки
&НаКлиенте
Процедура ОбработчикДанных_ЗаполнитьСтрокуТабличнойЧастиДокумента(Аргумент, ДопПараметры) Экспорт
	
	Файл_Шаблон_Модуль = ДопПараметры.Кэш.ГлавноеОкно.сбисПолучитьФорму("Файл_Шаблон");
	
	Файл_Шаблон_Модуль.ОбработчикДанных_ЗаполнитьСтрокуТабличнойЧастиДокумента	(Аргумент, ДопПараметры);
	Файл_Шаблон_Модуль.УстановитьПризнакДобавленияСтроки						(Аргумент, ДопПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьНоменклатуруВложения(Кэш, Контекст) Экспорт
	
	ФормаФайл = МодульОбъектаКлиент().НайтиФункциюСеансаОбработки("ОбновитьНоменклатуруВложения", "Файл_СчФктр_3_01");
	ФормаФайл.ОбновитьНоменклатуруВложения(Кэш, Контекст);
	
КонецПроцедуры