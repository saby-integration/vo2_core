&НаКлиенте
Перем Файл_Шаблон;
	
&НаКлиенте
Функция СформироватьДокументДляГенератора(ПараметрыФормированияВходящие, Кэш) Экспорт
    Перем ТекДокументАдресДоставки;

	Док			= ПараметрыФормированияВходящие.Документ;
	Вложение	= ПараметрыФормированияВходящие.Вложение;
	Файл_Шаблон = Кэш.ГлавноеОкно.сбисПолучитьФорму("Файл_Шаблон");
		
	ТекДокумент = Док.Файл.Документ;
	ТекДокумент.Вставить("ДефНомИспрСФ",  "-");
	ТекДокумент.Вставить("ДефДатаИспрСФ", "-");
	ТекДокумент.Вставить("ПоФактХЖ", 	Вложение.Название);
	ТекДокумент.Вставить("НаимДокОпр", 	?(ТекДокумент.Название = "Накладная", Null, ТекДокумент.Название)); 
	
	Если НЕ ТекДокумент.Свойство("Грузоотправитель") Тогда
		ТекДокумент.Вставить("Грузоотправитель", Новый Структура());
		Кэш.ОбщиеФункции.сбисСкопироватьСтруктуруНаКлиенте(ТекДокумент.Грузоотправитель, Вложение.Отправитель);
	КонецЕсли;
	Если НЕ ТекДокумент.Свойство("Грузополучатель") Тогда
		ТекДокумент.Вставить("Грузополучатель", Новый Структура());
		Кэш.ОбщиеФункции.сбисСкопироватьСтруктуруНаКлиенте(ТекДокумент.Грузополучатель, Вложение.Получатель);
	КонецЕсли;
	
	Если ТекДокумент.Свойство("АдресДоставки", ТекДокументАдресДоставки) Тогда
		Если ЗначениеЗаполнено(ТекДокументАдресДоставки) Тогда
			//Заменяется адрес ГП на адрес доставки. Логика из XSLT.
			АдресСписок = Новый Массив;
			АдресСписок.Добавить(ТекДокумент.АдресДоставки);
			ТекДокумент.Грузополучатель.Вставить("Адрес", АдресСписок);
		КонецЕсли;
		ТекДокумент.Удалить("АдресДоставки");
	КонецЕсли;
	
	Если Файл_Шаблон.ОтправительГрузоотправительОнЖе(Кэш, ТекДокумент.Отправитель, ТекДокумент.Грузоотправитель) Тогда
		ТекДокумент.Грузоотправитель.Вставить("ОнЖе", "он же");	
	КонецЕсли;
	
	Файл_Шаблон.ЗаполнитьПоДокументПараметр(Док, Кэш);
	Файл_Шаблон.ЗаполнитьПоДокументОснование(Док, Кэш);
	
	СчетчикБезНДС = 0;
	
	Для Каждого ТекСтрока ИЗ ТекДокумент.ТаблДок.СтрТабл Цикл
		ТекСтрока.Вставить("ДефСуммаЦен", 				"");
		ТекСтрока.Вставить("ДефНДС", 					"");
		ТекСтрока.Вставить("ДефЕдКод", 					"");
		ТекСтрока.Вставить("ДефКодСтраныПроизводства", 	"");
		Файл_Шаблон.ЗаполнитьПоСтрТаблПараметр(Кэш, ТекСтрока); 
		Файл_Шаблон.СформироватьИнфПол(Кэш, ТекСтрока);
		
		Если ТекСтрока.НДС.Свойство("Ставка") Тогда
			Если ТекСтрока.НДС.Ставка = "без НДС" Тогда
				ТекСтрока.Вставить("БезНДС", ТекСтрока.НДС.Ставка);
				СчетчикБезНДС = СчетчикБезНДС + 1;
			Иначе
				ТекСтрока.Вставить("БезНДС", "");
			КонецЕсли;
		Иначе
			ТекСтрока.Вставить("БезНДС", "");
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого ТекСтрока ИЗ ТекДокумент.ТаблДок.ИтогТабл Цикл
		
		Если СчетчикБезНДС = ТекДокумент.ТаблДок.СтрТабл.Количество() Тогда
			ТекСтрока.Вставить("БезНДС", "без НДС");	
		Иначе
			ТекСтрока.Вставить("БезНДС", "");
		КонецЕсли;
		
		ТекСтрока.Вставить("ДефСумма", "");
		ТекСтрока.Вставить("ДефНДС", "");
		
	КонецЦикла;
	
	СформироватьОснование(Док, Кэш);
	Файл_Шаблон.СформироватьПараметры(Док, Кэш);
	
	ТекДокумент.Вставить("Подписант", Новый Структура("ОблПолн", 5));
	
	Возврат Док;
	
КонецФункции
	
&НаКлиенте
Процедура СформироватьОснование(Док, Кэш) Экспорт

	ТекДокумент = Док.Файл.Документ;
	
	Если НЕ ТекДокумент.Свойство("Основание") Тогда
		Возврат;	
	КонецЕсли;
	
	Исключения = Файл_Шаблон.ПолучитьИсключенияОснование();
	ОснованиеМассив = Новый Массив;
	
	Для Каждого ТекСтрока ИЗ ТекДокумент.Основание Цикл
		
		Если ТекСтрока.Свойство("Тип") Тогда
			Ключ = "Тип";	
		Иначе
			Ключ = "Название";	
		КонецЕсли;
		
		Если Исключения.Получить(ТекСтрока[Ключ]) = Истина Тогда
			Продолжить;	
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ТекСтрока.Название) Тогда
			Продолжить;	
		КонецЕсли;
		
		Если НЕ ТекСтрока.Свойство("Дата") Тогда
			Продолжить;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ТекСтрока.Дата) И ТекСтрока.Название <> "-" Тогда
			Продолжить;	
		КонецЕсли; 
		
		Основание = Новый Структура("Номер, Название, Дата");
		Основание.Название 	= ТекСтрока.Название;
		Основание.Номер 	= ТекСтрока.Номер;
		Основание.Дата 		= ТекСтрока.Дата;
		
		ОснованиеМассив.Добавить(Основание);
		
	КонецЦикла;
	
	Если ОснованиеМассив.Количество() = 0 Тогда
		ОснованиеМассив.Добавить(Новый Структура("Название", "-"));
	КонецЕсли;
	
	ТекДокумент.Вставить("Основание", ОснованиеМассив);
	
КонецПроцедуры
