
&НаКлиенте
Перем Файл_Шаблон;

#Область include_local_ПолучитьМодульОбъекта
#КонецОбласти
	
// Процедура заполняет данные документа по его параметрам
//
// Параметры:
//  Док	 - Структура - Контекст рассчитываемого документа
//  Кэш	 - Структура - Кэш обработки
//
&НаКлиенте
Процедура ЗаполнитьПоДокументПараметр(Док, Кэш) Экспорт  
	
	Файл_Шаблон = Кэш.ГлавноеОкно.сбисПолучитьФорму("Файл_Шаблон");
	Файл_Шаблон.ЗаполнитьПоДокументПараметр(Док, Кэш);
	
	ТекДокумент = Док.Файл.Документ;

	Если НЕ ТекДокумент.Свойство("Параметр") Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ТекСтрока ИЗ ТекДокумент.Параметр Цикл

		Если ТекСтрока.Имя = "СвПерВещ" Тогда
			ТекДокумент.Вставить(ТекСтрока.Имя, ТекСтрока.Значение);
		КонецЕсли;  
		
		Если ТекСтрока.Имя = "ОбстФормСЧФ" Тогда
			ТекДокумент.Вставить(ТекСтрока.Имя, ТекСтрока.Значение);
		КонецЕсли; 
		
    КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоДокументОснование(Док, Кэш) Экспорт
	
	Файл_Шаблон = Кэш.ГлавноеОкно.сбисПолучитьФорму("Файл_Шаблон");
	Файл_Шаблон.ЗаполнитьПоДокументОснование(Док, Кэш);
	
	ТекДокумент = Док.Файл.Документ;

	Если Не ТекДокумент.Свойство("ДокПодтвОтгр") Тогда
		ТекДокумент.Вставить("ДокПодтвОтгр", Новый Структура());
		ТекДокумент.ДокПодтвОтгр.Вставить("ДатаДокОтгр", ТекДокумент.Дата);
		ТекДокумент.ДокПодтвОтгр.Вставить("НомДокОтгр",  ТекДокумент.Номер);
		ТекДокумент.ДокПодтвОтгр.Вставить("НаимДокОтгр", "Документ об отгрузке товаров (выполнении работ), передаче имущественных прав (документ об оказании услуг)");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьОснование(Док, Кэш) Экспорт

	ТекДокумент = Док.Файл.Документ;
	Если НЕ ТекДокумент.Свойство("Основание") Тогда
		Возврат;	
	КонецЕсли;
	
	Исключения = ПолучитьИсключенияОснование();
	ОснованиеМассив = Новый Массив;
	
	Для Каждого ТекСтрока ИЗ ТекДокумент.Основание Цикл
		
		Если ТекСтрока.Свойство("Тип") Тогда
			Ключ = "Тип";	
		Иначе
			Ключ = "Название";	
		КонецЕсли;
		
		Если Исключения.Получить(ТекСтрока[Ключ]) = Истина Тогда
			Продолжить;	
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ТекСтрока.Название) Тогда
			Продолжить;	
		КонецЕсли;
		
		Если НЕ ТекСтрока.Свойство("Дата") Тогда
			Продолжить;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ТекСтрока.Дата) И ТекСтрока.Название <> "-" Тогда
			Продолжить;	
		КонецЕсли; 
		
		Основание = Новый Структура("Номер, Название, Дата");
		Основание.Название 	= ТекСтрока.Название;
		Основание.Номер 	= ТекСтрока.Номер;
		Основание.Дата 		= ТекСтрока.Дата;
		
		ОснованиеМассив.Добавить(Основание);
		
	КонецЦикла;
	
	ЕстьОснование = ОснованиеМассив.Количество();
	УПД24 = МодульОбъектаКлиент().ПолучитьЗначениеПараметраСбис("УПД24");
	
	Если НЕ ЕстьОснование Тогда
		
		Если УПД24 Тогда
			ТекДокумент.Вставить("БезДокОснПер", "1");	
		Иначе  
			ОснованиеМассив.Добавить(Новый Структура("Название", "Без документа-основания"));
		КонецЕсли; 
		
	КонецЕсли;
	
	ТекДокумент.Вставить("Основание", ОснованиеМассив);
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьИсключенияОснование()
	
	ИсключенияИнфПол = Новый Соответствие;
	ИсключенияИнфПол.Вставить("ПРД", Истина);
	ИсключенияИнфПол.Вставить("СчФ", Истина);
	ИсключенияИнфПол.Вставить("КСчФ", Истина);
	ИсключенияИнфПол.Вставить("ИспрСчФ", Истина);
	ИсключенияИнфПол.Вставить("ИсхСчФ", Истина);
	ИсключенияИнфПол.Вставить("ИспрИсхСчФ", Истина);
	ИсключенияИнфПол.Вставить("ДокПодтвОтгр", Истина);
	
	Возврат ИсключенияИнфПол;
	
КонецФункции

//Штатный обработчик строки подстановки
&НаКлиенте
Процедура ОбработчикДанных_ЗаполнитьСтрокуТабличнойЧастиДокумента(Аргумент, ДопПараметры) Экспорт
	
	Файл_Шаблон = ДопПараметры.Кэш.ГлавноеОкно.сбисПолучитьФорму("Файл_Шаблон");
	
	Файл_Шаблон.ОбработчикДанных_ЗаполнитьСтрокуТабличнойЧастиДокумента	(Аргумент, ДопПараметры);
	Файл_Шаблон.УстановитьПризнакДобавленияСтроки						(Аргумент, ДопПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьНоменклатуруВложения(Кэш, Контекст) Экспорт
	
	ФормаФайл = МодульОбъектаКлиент().НайтиФункциюСеансаОбработки("ОбновитьНоменклатуруВложения", "Файл_СчФктр_3_01");
	ФормаФайл.ОбновитьНоменклатуруВложения(Кэш, Контекст);
	
КонецПроцедуры