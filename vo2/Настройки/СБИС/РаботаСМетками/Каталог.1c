&НаКлиенте
Процедура	СбисСохранитьМеткиСтатусов(Кэш, МеткиСтатусов, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры.СпособХраненияМетокСтатусов = 2 Тогда
		МодульОбъектаКлиент().СохранитьОбщуюНастройку("status_marks", МеткиСтатусов);
		Возврат;
	КонецЕсли;
	
	ОбщиеНастройки	= ПрочитатьОбщиеНастройкиПодключения(Кэш, Кэш.Парам.ИдентификаторНастроек);
	СохраненныеМетки= ?(ОбщиеНастройки.Свойство("status_marks"), ОбщиеНастройки.status_marks, Новый Структура);
	
	Если		ДопПараметры.СпособХраненияМетокСтатусов = 0 Тогда
		КлючПользователя = ПолучитьКлючПользователяСтенда(Кэш);
		КлючАккаунта = ПолучитьКлючАккаунта(Кэш, ДопПараметры);
		Если Не СохраненныеМетки.Свойство(КлючПользователя) Тогда
			СохраненныеМетки.Вставить(КлючПользователя, Новый Структура);
		КонецЕсли;
		
		СохраненныеМетки[КлючПользователя].Вставить(КлючАккаунта, МеткиСтатусов);
		
		Результат = Новый Структура("status_marks", СохраненныеМетки);	
		СохранитьПараметрыСБИС(Кэш, Результат, Кэш.Парам.ИдентификаторНастроек);
	ИначеЕсли	ДопПараметры.СпособХраненияМетокСтатусов = 1 Тогда
		КлючАккаунта = ПолучитьКлючАккаунта(Кэш, ДопПараметры);
		СохраненныеМетки.Вставить(КлючАккаунта, МеткиСтатусов);
		
		Результат = Новый Структура("status_marks", СохраненныеМетки);	
		СохранитьПараметрыСБИС(Кэш, Результат, Кэш.Парам.ИдентификаторНастроек);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция СбисПрочитатьМеткиСтатусов(Кэш, ДопПараметры = Неопределено, Отказ = Ложь) Экспорт
	СпособХраненияМетокСтатусов = Кэш.Парам.СпособХраненияМетокСтатусов;
	ДопПараметры = Новый Структура("СпособХраненияМетокСтатусов", СпособХраненияМетокСтатусов);
	Если СпособХраненияМетокСтатусов = 0 Тогда
		КлючПользователя = ПолучитьКлючПользователяСтенда(Кэш);
		КлючАккаунта = ПолучитьКлючАккаунта(Кэш, ДопПараметры);
		Если	КлючПользователя	= Неопределено
			Или	КлючАккаунта		= Неопределено Тогда
			//Не удалось определить ключ пользователя стенда. Выставим метки по-дефолту.
			Возврат Кэш.ФормаНастроекОбщее.МеткиСтатусовПоУмолчанию();
		КонецЕсли;
		ЗначениеНастроекРезультат = ПрочитатьОбщиеНастройкиПодключения(Кэш, Кэш.Парам.ИдентификаторНастроек); //по пользователю
		Если	ЗначениеНастроекРезультат.Свойство("status_marks",	ЗначениеНастроекРезультат)
			И	ЗначениеНастроекРезультат.Свойство(КлючПользователя,ЗначениеНастроекРезультат)
			И	ЗначениеНастроекРезультат.Свойство(КлючАккаунта,	ЗначениеНастроекРезультат) Тогда
			Возврат ЗначениеНастроекРезультат;
		КонецЕсли;
	ИначеЕсли СпособХраненияМетокСтатусов = 1 Тогда
		КлючАккаунта = ПолучитьКлючАккаунта(Кэш, ДопПараметры);
		Если КлючАккаунта = Неопределено Тогда
			//Не удалось определить ключ пользователя стенда. Выставим метки по-дефолту.
			Возврат Кэш.ФормаНастроекОбщее.МеткиСтатусовПоУмолчанию();
		КонецЕсли;
		ЗначениеНастроекРезультат = ПрочитатьОбщиеНастройкиПодключения(Кэш, Кэш.Парам.ИдентификаторНастроек); //по аккаунту
		Если	ЗначениеНастроекРезультат.Свойство("status_marks",	ЗначениеНастроекРезультат)
			И	ЗначениеНастроекРезультат.Свойство(КлючАккаунта,	ЗначениеНастроекРезультат) Тогда
			Возврат ЗначениеНастроекРезультат;
		КонецЕсли;	
	ИначеЕсли СпособХраненияМетокСтатусов = 2 Тогда	
		ЗначениеНастроекРезультат = Кэш.ГлавноеОкно.МодульОбъектаКлиент().МеткиСтатусовИзОбщихНастроек();  //для 1С
		Если ЗначениеНастроекРезультат.Свойство("status_marks") Тогда 
			Возврат ЗначениеНастроекРезультат.status_marks;
		КонецЕсли;	
	КонецЕсли;
	Возврат Кэш.ФормаНастроекОбщее.МеткиСтатусовПоУмолчанию();
КонецФункции

&НаКлиенте
Функция ПолучитьКлючПользователяСтенда(Кэш, ДопПараметры = Неопределено)
	Если	Не Кэш.СБИС.ПараметрыИнтеграции.Свойство("КодСервиса")
		Или	Не Кэш.СБИС.ПараметрыИнтеграции.Свойство("ИдПользователя") Тогда
		Возврат Неопределено;
	КонецЕсли;
	Возврат Кэш.ОбщиеФункции.СбисФорматСтроки(Кэш.СБИС.ПараметрыИнтеграции.КодСервиса + "_" + Кэш.СБИС.ПараметрыИнтеграции.ИдПользователя);
КонецФункции

&НаКлиенте
Функция ПолучитьКлючАккаунта(Кэш, ДопПараметры)                                                                                     
	Перем СбисКлючАккаунта;
	Если ДопПараметры.СпособХраненияМетокСтатусов Тогда
		СбисКлючАккаунта = Кэш.СБИС.ПараметрыИнтеграции.КодСервиса + "_acc";
	ИначеЕсли Кэш.СБИС.ПараметрыИнтеграции.Свойство("ИдАккаунта") Тогда
		СбисКлючАккаунта = "_" + Кэш.СБИС.ПараметрыИнтеграции.ИдАккаунта;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	Возврат Кэш.ОбщиеФункции.СбисФорматСтроки(СбисКлючАккаунта);
КонецФункции

