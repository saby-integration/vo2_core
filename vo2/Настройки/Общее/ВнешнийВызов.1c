
//Вызов получения и установки настроек
&НаКлиенте
Процедура СбисПолучитьИУстановитьНастройкиВКэш(ПараметрыУстановки, Кэш) Экспорт
	Попытка
		Отказ = Ложь;
		
		//Настройки устанавливаются если ещё не были применены, либо если изменились параметры работы обработки. В остальных случаях, настройки не переустанавливаются
		УстановленныеИниФайлы = Кэш.ФормаНастроек.СбисПолучитьНастройки(Кэш, Отказ, ПараметрыУстановки.ПродолжитьУстановку);
		Если Отказ Тогда
			МодульОбъектаКлиент().ВызватьСбисИсключение(Кэш, УстановленныеИниФайлы, "ФормаНастроек.СбисПолучитьНастройки");
		КонецЕсли;
		
		Если	ПараметрыУстановки.ПродолжитьУстановку
			Или	ПараметрыУстановки.Принудительно Тогда
			Кэш.ГлавноеОкно.СбисПоказатьСостояние("Установка настроек", Кэш.ГлавноеОкно);
			РезультатУстановкиНастроек = Кэш.ГлавноеОкно.СбисУстановитьНастройки(Кэш, УстановленныеИниФайлы, ПараметрыУстановки.Принудительно, Отказ);
			Кэш.ГлавноеОкно.СбисСпрятатьСостояние(Кэш.ГлавноеОкно);		
			Если Отказ Тогда
				МодульОбъектаКлиент().ВызватьСбисИсключение(Кэш, РезультатУстановкиНастроек, "ГлавноеОкно.СбисУстановитьНастройки");
			КонецЕсли;
		КонецЕсли;
			
		Кэш.ГлавноеОкно.СбисОбновитьИнформациюНастроекНаГлавномОкне(Кэш, УстановленныеИниФайлы, Отказ);
		Кэш.ГлавноеОкно.ФильтрУстановитьТипыПолей(Кэш);

		СтруктураМетокСтатусов = Кэш.ФормаНастроек.СбисПрочитатьМеткиСтатусов(Кэш);
		ЗаполнитьЗначенияСвойств(Кэш.ГлавноеОкно, СтруктураМетокСтатусов);
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		МодульОбъектаКлиент().ВызватьСбисИсключение(Кэш, ИнформацияОбОшибке, "ФормаНастроекОбщее.СбисПолучитьУстановитьНастройки");
	КонецПопытки;
КонецПроцедуры

&НаКлиенте
Функция СбисУстановитьНастройки(ЛокальныйКэш, УстановленныеИниФайлы, СказатьЧтоВсеХорошо=Ложь, Отказ=Ложь, СбисДополнительныеПараметры=Неопределено) Экспорт
	Перем СтруктураПараметровСбис, СтруктураПараметровСбисИсходная, ИдентификаторНастроек;
	Если УстановленныеИниФайлы = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	Если Не СбисДополнительныеПараметры = Неопределено Тогда
		СбисДополнительныеПараметры.Свойство("ИдентификаторНастроек", ИдентификаторНастроек);
		СбисДополнительныеПараметры.Свойство("СтруктураНастроек", СтруктураПараметровСбис);
	КонецЕсли;
	Если ИдентификаторНастроек = Неопределено Тогда
		ИдентификаторНастроек = ЛокальныйКэш.Парам.ИдентификаторНастроек;
	КонецЕсли;
	Если СтруктураПараметровСбис = Неопределено Тогда
		СтруктураПараметровСбис = ЛокальныйКэш.ФормаНастроек.ПолучитьПараметрыСБИС(ЛокальныйКэш, ИдентификаторНастроек, Отказ);
		Если Отказ Тогда
			Возврат ЛокальныйКэш.ОбщиеФункции.СбисИсключение(СтруктураПараметровСбис, "ГлавноеОкно.СбисУстановитьНастройки");
		КонецЕсли;
	КонецЕсли;
	НужнаПроверкаПоИзменениям =		ЛокальныйКэш.ФормаНастроек.СбисОпределитьНеобходимостьПроверки(ЛокальныйКэш, УстановленныеИниФайлы, СтруктураПараметровСбис)
								Или	СказатьЧтоВсеХорошо = Истина;
	
	ПараметрыУстановки = Новый Структура("НеобходимоОбновление", НужнаПроверкаПоИзменениям);
	Если НужнаПроверкаПоИзменениям Тогда
		УстановленныеИниФайлы = ЛокальныйКэш.ФормаНастроек.сбисПроверитьУстановленныеНастройки(ЛокальныйКэш, УстановленныеИниФайлы, ЛокальныйКэш.Ини, СтруктураПараметровСбис, ИдентификаторНастроек);
	Иначе// если не проверяем, то считаем, что с ини все хорошо
		ЛокальныйКэш.ИниВПорядке = Истина;
	КонецЕсли;
	
	ЛокальныйКэш.ФормаНастроек.УстановитьПараметрыИнтеграции_УстановкаНастроек(ЛокальныйКэш, СтруктураПараметровСбис, СбисДополнительныеПараметры, Отказ);
	Если Не ЛокальныйКэш.ИниВПорядке Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Результат = ЛокальныйКэш.ФормаНастроекОбщее.СбисУстановитьВыбранныеНастройки(ЛокальныйКэш, ПараметрыУстановки, СбисДополнительныеПараметры, Отказ);
	Если Отказ Тогда
		ЛокальныйКэш.ИниВПорядке = Ложь;
		Возврат ЛокальныйКэш.ОбщиеФункции.СбисИсключение(Результат, "ГлавноеОкно.СбисУстановитьНастройки");
	Иначе
		ЛокальныйКэш.ФормаНастроек.СохранитьПараметрыСБИС(ЛокальныйКэш, СтруктураПараметровСбис, ИдентификаторНастроек);
		ЛокальныйКэш.Интеграция.УстановитьВидимостьОбновитьСтатусы(ЛокальныйКэш);
	КонецЕсли;
	Возврат Истина;
КонецФункции

