&НаКлиенте
Процедура ИзменитьСоответствиеНоменклатуры() 
	
	СопоставлениеНоменклатуры	= МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(ЭтаФорма, "Таблица_НоменклатураКонтрагентов").ТекущиеДанные.СопоставлениеНоменклатуры;
	фрм							= МодульОбъектаКлиент().ПолучитьФормуОбработки("ФормаУстановкаСооответствияНоменклатуры");
	фрм.Показать(Кэш, Новый Структура("Контрагент, СопоставлениеНоменклатуры",КонтрагентНоменклатуры, СопоставлениеНоменклатуры));
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьСоответствиеНоменклатуры(Команда)
	
	фрм = сбисПолучитьФорму("ФормаУстановкаСооответствияНоменклатуры");
	фрм.Показать(Кэш, Новый Структура("Контрагент",КонтрагентНоменклатуры));
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьСоответствиеНоменклатуры(Команда)
	
	стрСопоставление	= Новый Массив;
	
	СтрокиУдалить = Таблица_НоменклатураКонтрагентов.НайтиСтроки(Новый Структура("Отмечен", Истина));
	Если Не СтрокиУдалить.Количество() Тогда
		НоменклатураКонтрагентов	= МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(ЭтаФорма, "Таблица_НоменклатураКонтрагентов"); 
		ТекущаяСтрока				= НоменклатураКонтрагентов.ТекущиеДанные;
		
		Если ТекущаяСтрока = Неопределено Тогда
			Возврат;
		Иначе
			стрСопоставление.Добавить(ТекущаяСтрока.СопоставлениеНоменклатуры);
			СтрокиУдалить.Добавить(ТекущаяСтрока);
		КонецЕсли;
	Иначе
		Для Каждого СтрокаУдалить Из СтрокиУдалить Цикл
			стрСопоставление.Добавить(СтрокаУдалить.СопоставлениеНоменклатуры);
		КонецЦикла;
	КонецЕсли;
	
	стрКонтрагент = Кэш.ОбщиеФункции.сбисЗаполнитьСтруктуруКонтрагента(КонтрагентНоменклатуры);
	
	ПараметрыУдаленияСопоставлений = Новый Структура("Номенклатура, Контрагент", стрСопоставление, стрКонтрагент);
	ДопПараметры = Новый Структура("Кэш", Кэш);
	
	фрмСопоставлениеНоменклатуры = Кэш.ТекущийСеанс.Модули.МодульСопоставлениеНоменклатуры;
	фрмСопоставлениеНоменклатуры.НоменклатураПоставщика_МассовоеУдаление(ПараметрыУдаленияСопоставлений, ДопПараметры);
	
	Для Каждого СтрокаУдалить Из СтрокиУдалить Цикл
		Таблица_НоменклатураКонтрагентов.Удалить(СтрокаУдалить);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура сбисЗаполнитьСписокСправочникаНоменклатурыКонтрагентов(СписокВходящий) Экспорт

	Таблица_НоменклатураКонтрагентов.Очистить();
	
	Если Не ЗначениеЗаполнено(СписокВходящий) Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(СписокВходящий) = Тип("Структура") Тогда
		
		Список = СписокВходящий.Список;
		
	Иначе
		
		Список = СписокВходящий;
		
	КонецЕсли;
	
	Для Каждого Соответствие Из Список Цикл
		
		СтрокаТаблицы							= Таблица_НоменклатураКонтрагентов.Добавить();
		СтрокаТаблицы.Наименование				= Соответствие.НоменклатураСБИС.Наименование;
		СтрокаТаблицы.Код						= Соответствие.НоменклатураСБИС.Код;
		СтрокаТаблицы.СопоставлениеНоменклатуры = Соответствие; 
		СтрокаТаблицы.Ссылка.ЗагрузитьЗначения(СоответствиеВМассив(Соответствие.Номенклатура1С,"Ключ")); 

	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура КонтрагентНоменклатурыПриИзменении(Элемент)
	ОбновитьКонтент();
КонецПроцедуры


//Перенести в модульобъектаклиент
&НаКлиенте
Функция СоответствиеВМассив(Соответствие, ИмяЭлемента = "")
	
	Результат = Новый Массив;
	
	Для Каждого КлючЗначение Из Соответствие Цикл
		
		Если ИмяЭлемента = "Ключ" Тогда
			
			Результат.Добавить(КлючЗначение.Ключ);
			
		ИначеЕсли ЗначениеЗаполнено(ИмяЭлемента) Тогда
			
			Результат.Добавить(КлючЗначение.Значение[ИмяЭлемента]);
			
		Иначе
			
			Результат.Добавить(КлючЗначение.Значение);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

