
//Функция формирует ленту о прохождении документа	
&НаКлиенте
Функция ЗаполнитьПрохождение(Кэш, СоставПакета, ДопПараметры) Экспорт
	Перем ПакетСобытие;
	СбисСобытияЛента = Новый СписокЗначений;
	
	Если НЕ СоставПакета.Свойство("Событие", ПакетСобытие)
		Или НЕ ЗначениеЗаполнено(ПакетСобытие) Тогда
		Возврат СбисСобытияЛента;
	КонецЕсли;
	СбисСобытия = Новый Соответствие();
	СистемныеСбисСобытия = Новый Массив;
	СистемныеСбисСобытия.Добавить("извещение о получении ");
	СистемныеСбисСобытия.Добавить("загрузка");
	Для Каждого Событие Из ПакетСобытие Цикл
		КлючСбисСобытия		= "";
		НазваниеСбисСобытия	= Событие.Название;
		ЭтоСистемноеСобытие	= Ложь;
		ЗаполнитьПодпись	= Истина;
		Для	Каждого	СистемноеСобытие Из СистемныеСбисСобытия Цикл
			Если НРег(Лев(НазваниеСбисСобытия, СтрДлина(СистемноеСобытие))) = СистемноеСобытие Тогда
				ЭтоСистемноеСобытие	= Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если ЭтоСистемноеСобытие Тогда
			Продолжить;
		КонецЕсли;
		 
		Если		Лев(НазваниеСбисСобытия, 9)  = "Получение" Тогда
			Если Найти(НазваниеСбисСобытия, "аннулирова") Тогда
				НазваниеСбисСобытия = "Запрос на аннулирование";
			Иначе
				НазваниеСбисСобытия = "Отправлено";
				КлючСбисСобытия		= "Отправка1";
			КонецЕсли;
		ИначеЕсли	Лев(НазваниеСбисСобытия, 11) = "Уведомление" Тогда
			КлючСбисСобытия		= "Утверждение";
			Если		Найти(НазваниеСбисСобытия, "о приеме")	Тогда
				НазваниеСбисСобытия	= "Утверждено";
			ИначеЕсли   Найти(НазваниеСбисСобытия, "об уточнении")	Тогда
				НазваниеСбисСобытия	= "Отклонено";
			КонецЕсли;
		ИначеЕсли	Лев(НазваниеСбисСобытия,9)	= "Извещение" Тогда
			Если Найти(НазваниеСбисСобытия, "отправ") Тогда
				НазваниеСбисСобытия = "Отправлено";
				КлючСбисСобытия		= "Отправка1";
			ИначеЕсли Найти(НазваниеСбисСобытия, "получении") Тогда
				КлючСбисСобытия		= "Получение";
				НазваниеСбисСобытия	= "Получено";
			КонецЕсли;
		ИначеЕсли	Лев(НазваниеСбисСобытия,13)	= "Подтверждение" Тогда
			ЗаполнитьПодпись = Ложь;
			Если		Найти(НазваниеСбисСобытия, "даты получения") Тогда
				НазваниеСбисСобытия	= "Получено";
				КлючСбисСобытия		= "Получение";
			ИначеЕсли	Найти(НазваниеСбисСобытия, "даты отправки") Тогда
				НазваниеСбисСобытия = "Отправлено";
				КлючСбисСобытия		= "Отправка1";
			ИначеЕсли	Найти(НазваниеСбисСобытия, "получателю на извещение") Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		Если	ПустаяСтрока(КлючСбисСобытия) Тогда
			КлючСбисСобытия = СтрЗаменить(НазваниеСбисСобытия, " ", "");
		КонецЕсли;
		СобытиеВставка = СбисСобытия.Получить(КлючСбисСобытия); 
		Если СобытиеВставка = Неопределено Тогда
			СобытиеВставка = Новый Структура;
			СбисСобытия.Вставить(КлючСбисСобытия, СобытиеВставка);
		КонецЕсли;	
		СобытиеВставка.Вставить("Название",		НазваниеСбисСобытия	+ ?(ПустаяСтрока(Событие.Комментарий),"",": "	+ Событие.Комментарий));
		СобытиеВставка.Вставить("ДатаВремя",	?(ЗначениеЗаполнено(Событие.ДатаВремяДокумента),Событие.ДатаВремяДокумента,Событие.ДатаВремя));
		Если ЗаполнитьПодпись Тогда
			СобытиеВставка.Вставить("Подпись",	СбисИмяСертификата(Событие));
		КонецЕсли;
	КонецЦикла;
	Для Каждого СобытиеПрочее Из СбисСобытия Цикл
		СбисСобытияЛента.Добавить(СобытиеПрочее.Значение, СбисПредставлениеДатыНаСортировку(СобытиеПрочее.Значение.ДатаВремя));
	КонецЦикла;
	СбисСобытияЛента.СортироватьПоПредставлению();
	Возврат СбисСобытияЛента;
КонецФункции

&НаКлиенте
Функция СбисИмяСертификата(Событие)
	Перем Результат;
	Если	Событие.Свойство("Вложение", Результат)
		И	Результат[0].Свойство("Подпись", Результат)
		И	Результат[0].Свойство("Сертификат", Результат) Тогда
		Результат = Результат.ФИО;
	ИначеЕсли	Событие.Свойство("Исполнитель", Результат)	Тогда//нет сертификата, проверить наличие исполнителя, взять ФИО оттуда
		ФИОПодписи	= Результат.Фамилия;
		ФИОПодписи	= ФИОПодписи + ?(ПустаяСтрока(ФИОПодписи), "", " ") + Результат.Имя;
		ФИОПодписи	= ФИОПодписи + ?(ПустаяСтрока(ФИОПодписи), "", " ") + Результат.Отчество;
		Результат = ФИОПодписи;
	Иначе
		Результат = "";
	КонецЕсли;
	Возврат Результат;
КонецФункции

&НаКлиенте
Функция СбисПредставлениеДатыНаСортировку(ДатаВремяЗначение)
	ДатаВремяЗначение = МодульОбъектаКлиент().ДатаИзСтроки(ДатаВремяЗначение);
	Возврат Формат(ДатаВремяЗначение, "ДФ=yyyyMMdd") + Формат(Час(ДатаВремяЗначение)*60*60 + Минута(ДатаВремяЗначение)*60 + Секунда(ДатаВремяЗначение), "ЧЦ=5; ЧДЦ=0; ЧН=; ЧВН=; ЧГ=0");
КонецФункции

