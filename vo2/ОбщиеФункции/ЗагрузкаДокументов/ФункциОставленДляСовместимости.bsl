
////////Функции оставлены для совместимости/////////
///////////////Делать запись через ини//////////////

// Функция заполняет договор в загружаемых заказах для конфигурации УТ11	
&НаКлиенте
Функция ЗаполнитьДоговорСКлиентомУТ11(Контекст) Экспорт
	сбисКонтекстСервер = Новый Структура("Документ", Контекст.Документ);
	Возврат сбисПолучитьДоговорСКлиентомУТ11_Сервер(сбисКонтекстСервер);
КонецФункции

//Функция возвращает единицу измерения для номенклатуры в загружаемом документе (для конфигураций БУХ) 
&НаКлиенте
Функция ЗаполнитьЕдиницуИзмерения(Контекст) Экспорт
	Если Контекст.СтрокаФайла.Свойство("ЕдИзмОрг") и ЗначениеЗаполнено(Контекст.СтрокаФайла.ЕдИзмОрг) Тогда
		Возврат Контекст.СтрокаФайла.ЕдИзмОрг;	
	КонецЕсли;	
	сбисОписаниеОбъекта = Новый Структура("Тип, Ключ, Значение", РассчитатьЗначение("СправочникЕдиницИзмерения", Контекст.Ини)); 
	// d.ch 29.07.19
	// получаем название реквизита табличной части документа куда будем записывать единицу измерения
	// берем рассчитанное значение из СтрТабл по имени реквизита
	ИмяРеквизита = Сред(Контекст.ТекущееПолеИни.Значение, Найти(Контекст.ТекущееПолеИни.Значение,".") + 1);
	Если Не Контекст.СтрТабл.Свойство(ИмяРеквизита, сбисОписаниеОбъекта.Ключ) Тогда
		сбисОписаниеОбъекта.Ключ = Контекст.СтрТабл.ЕдиницаИзмерения;
	КонецЕсли;
	
	сбисОписаниеОбъекта.Значение = сбисПолучитьЗначениеРассчитанногоОбъекта(МестныйКэш, сбисОписаниеОбъекта);
	Если сбисОписаниеОбъекта.Значение = Неопределено Тогда
		сбисКонтекстСервера = Новый Структура("Ини, ТекущееПолеИни, СтрТабл");
		ЗаполнитьЗначенияСвойств(сбисКонтекстСервера, Контекст);
		сбисКонтекстСервера.Вставить("ОписаниеОбъекта", сбисОписаниеОбъекта);
		сбисОписаниеОбъекта.Значение = сбисПолучитьЕдиницуИзмерения_Справочник_НаСервере(сбисКонтекстСервера);
		Если Не ЗначениеЗаполнено(сбисОписаниеОбъекта.Значение) Тогда    // если не нашли по коду ОКЕИ, попробуем по наименованию (например, для блоков сигарет код ОКЕИ 0000 в файле)
			Попытка
				сбисКонтекстСервера.ОписаниеОбъекта.Ключ = Контекст.СтрокаФайла.ЕдИзм;
				сбисОписаниеОбъекта.Значение = сбисПолучитьЕдиницуИзмеренияПоНаименованию_Справочник_НаСервере(сбисКонтекстСервера);
			Исключение
			КонецПопытки;
		КонецЕсли;
		сбисЗакэшироватьЗначениеРассчитанногоОбъекта(МестныйКэш, сбисОписаниеОбъекта);
	КонецЕсли;
	Возврат сбисОписаниеОбъекта.Значение;
КонецФункции      

//Функция возвращает единицу измерения для номенклатуры в загружаемом документе (для конфигураций БГУ2) 
&НаКлиенте
Функция ЗаполнитьЕдиницуИзмеренияБГУ(Контекст) Экспорт
	Если Контекст.СтрокаФайла.Свойство("ЕдИзмОрг") и ЗначениеЗаполнено(Контекст.СтрокаФайла.ЕдИзмОрг) Тогда
		Возврат Контекст.СтрокаФайла.ЕдИзмОрг;	
	КонецЕсли;	
	сбисОписаниеОбъекта = Новый Структура("Тип, Ключ, Значение", "Справочник.КлассификаторЕдиницИзмерения"); 
	ИмяРеквизита = Сред(Контекст.ТекущееПолеИни.Значение, Найти(Контекст.ТекущееПолеИни.Значение,".") + 1);
	Если Не Контекст.СтрТабл.Свойство(ИмяРеквизита, сбисОписаниеОбъекта.Ключ) Тогда
		сбисОписаниеОбъекта.Ключ = Контекст.СтрТабл.ЕдиницаИзмерения;
	КонецЕсли;
	
	сбисОписаниеОбъекта.Значение = сбисПолучитьЗначениеРассчитанногоОбъекта(МестныйКэш, сбисОписаниеОбъекта);
	Если сбисОписаниеОбъекта.Значение = Неопределено Тогда
		сбисОписаниеОбъекта.Значение = Объект1СПоОписанию(сбисОписаниеОбъекта, "ПоКоду");
		сбисЗакэшироватьЗначениеРассчитанногоОбъекта(МестныйКэш, сбисОписаниеОбъекта);
	КонецЕсли;
	Возврат сбисОписаниеОбъекта.Значение;
КонецФункции

//Функция заполняет менеджера в загружаемых документах для конфигурации УТ11	
&НаКлиенте
Функция ЗаполнитьМенеджераУТ11(Контекст) Экспорт
	сбисОписаниеОбъекта = Новый Структура("Тип, Ключ, Значение", "Справочники.Пользователи", "ТекущийПользовательИБ"); 
	сбисОписаниеОбъекта.Значение = сбисПолучитьЗначениеРассчитанногоОбъекта(МестныйКэш, сбисОписаниеОбъекта);
	Если сбисОписаниеОбъекта.Значение = Неопределено Тогда	
		сбисОписаниеОбъекта.Значение = сбисПолучитьМенеджераУТ11_Сервер();
		сбисЗакэшироватьЗначениеРассчитанногоОбъекта(МестныйКэш, сбисОписаниеОбъекта);
	КонецЕсли;
	Возврат сбисОписаниеОбъекта.Значение;
КонецФункции

&НаКлиенте
Функция ЗаполнитьСоглашениеСКлиентомУТ11(Контекст) Экспорт
	// Функция заполняет соглашение в загружаемых заказах для конфигурации УТ11	
	сбисКонтекстСервер = Новый Структура("Контрагент", Контекст.Документ.Контрагент);
	Возврат сбисПолучитьСоглашениеСКлиентомУТ11_Сервер(сбисКонтекстСервер);
КонецФункции

// Функция заполняет соглашение в загружаемых документах для конфигурации УТ11	
&НаКлиенте
Функция ЗаполнитьСоглашениеУТ11(Контекст) Экспорт
	сбисКонтекстСервера = Новый Структура("Контрагент", Контекст.Документ.Контрагент);
	Возврат сбисПолучитьСоглашениеУТ11_Сервер(сбисКонтекстСервера);
КонецФункции

// Функция заполняет НалогообложениеНДС в загружаемых документах, если не заполнен договор для конфигураций УТ11, ERP, KA2
&НаКлиенте
Функция ЗаполнитьНалогообложениеНДСБезДоговораУТ11(Контекст) Экспорт
	сбисКонтекстСервер = Новый Структура("Организация, Дата", Контекст.Документ.Организация, Контекст.Документ.Дата);
	Возврат сбисПолучитьНалогообложениеНДСБезДоговораУТ11_Сервер(сбисКонтекстСервер);
КонецФункции

