
&НаКлиенте
//Пример записи параметров отбора 'Название,ОКЕИ&lt;Строка&gt;,Кол_во|Количество&lt;Число&gt;' 
//	"," - разделитель элементов.
//	"|" - при необходимости записать ключ в источнике, отличный от ключа поиска. Запись: КлючДляОтбора|КлючПоискаВИсточнике
//	<>	- описание типа, к которому требуется привести поля. Строка делает СокрЛП.
Функция СбисПараметрыОтбора(Кэш, ИсточникДанных, ПоляФильтра) Экспорт
	Перем СтрокаТип;
	СписокПолей = СтрЗаменить(ПоляФильтра, ",", Символы.ПС);
	Фильтр = Новый Структура();
	Для СтрСчётчик = 1 По СтрЧислоСтрок(СписокПолей) Цикл
		СтрокаВФильтр = СокрЛП(СтрПолучитьСтроку(СписокПолей, СтрСчётчик));
		Тип = Неопределено;
		Если Найти(СтрокаВФильтр, "<") Тогда
			СтрокаТип = СтрЗаменить(СтрЗаменить(СтрокаВФильтр, "<", Символы.ПС), ">", Символы.ПС);
			СтрокаВФильтр = СтрПолучитьСтроку(СтрокаТип, 1) + СтрПолучитьСтроку(СтрокаТип, 3);
			СтрокаТип = НРег(СтрПолучитьСтроку(СтрокаТип, 2));
		КонецЕсли;
		Если Найти(СтрокаВФильтр, "|") Тогда
			СтрокаВФильтр = СтрЗаменить(СтрокаВФильтр, "|", Символы.ПС);
		Иначе
			СтрокаВФильтр = СтрокаВФильтр + Символы.ПС + СтрокаВФильтр;
		КонецЕсли;
		Попытка
			ФильтрЗначение = Новый Структура("Значение", ИсточникДанных[СокрЛП(СтрПолучитьСтроку(СтрокаВФильтр, 2))]);
			ФильтрЗначение.Значение = СбисПривестиТип(СтрокаТип, ФильтрЗначение.Значение);
			ФильтрЗначение.Вставить("Тип", СтрокаТип);
			Фильтр.Вставить(СокрЛП(СтрПолучитьСтроку(СтрокаВФильтр, 1)), ФильтрЗначение);
		Исключение
		КонецПопытки;
	КонецЦикла;
	Возврат Фильтр;
КонецФункции

&НаКлиенте
Функция СбисПривестиТип(СбисТип, СбисЗначение)
	Если		СбисТип = "число" Тогда 
		СбисЗначение = Число(СбисЗначение);
	ИначеЕсли	СбисТип = "булево" Тогда
		СбисЗначение = Булево(СбисЗначение);
	ИначеЕсли	СбисТип = "дата" Тогда
		СбисЗначение = Дата(СбисЗначение);
	ИначеЕсли	СбисТип = "строка" Тогда
		СбисЗначение = СокрЛП(СбисЗначение);
	КонецЕсли;
	Возврат СбисЗначение;
КонецФункции

&НаКлиенте
Функция СбисНайтиСтроки(Кэш, МассивСтрок, ПараметрыОтбора) Экспорт
	Перем ЗначениеФильтр;
	МассивРезультат = Новый Массив;
	Для Каждого СтрокаМассива Из МассивСтрок Цикл
		СбисПодходит = Истина;
		Для Каждого ЭлементФильтра Из ПараметрыОтбора Цикл
			Если	СтрокаМассива.Свойство(ЭлементФильтра.Ключ, ЗначениеФильтр)
				И	СбисПривестиТип(ЭлементФильтра.Значение.Тип, ЗначениеФильтр) = ЭлементФильтра.Значение.Значение Тогда
				Продолжить;
			КонецЕсли;
			СбисПодходит = Ложь;
			Прервать;
		КонецЦикла;
		Если СбисПодходит Тогда
			МассивРезультат.Добавить(СтрокаМассива);
		КонецЕсли;
	КонецЦикла;
	Возврат МассивРезультат;
КонецФункции

