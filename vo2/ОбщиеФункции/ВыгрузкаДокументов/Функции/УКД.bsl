
//Функция сводит увеличение и уменьшение в общий СведПрослеж для УКД
&НаКлиенте
Функция сбисОбработатьСведПрослеж_УКД(Контекст) Экспорт
    Перем мСведПрослеж, СведПрослежРасчет;
	Результат = Новый Структура("Карта, Значение", Новый Массив, Новый Массив);
	Если Не Контекст.Свойство("мСведПрослеж", мСведПрослеж) Тогда
		Возврат Результат.Значение;
	КонецЕсли;
	КлючиГруппировки	= СтрЗаменить(РассчитатьЗначение("СведПрослежРасчет_КлючиГруппировки", мСведПрослеж.СведПрослежРасчет), ",", Символы.ПС);
	КлючиСумммирования	= СтрЗаменить(РассчитатьЗначение("СведПрослежРасчет_КлючиСуммирования", мСведПрослеж.СведПрослежРасчет), ",", Символы.ПС);
	КлючиРасчета = Новый Структура("Группировка, Суммирование", КлючиГруппировки, КлючиСумммирования);
	//Соберём все строки
	Для Каждого КлючИЗначениеПрослеж Из мСведПрослеж Цикл
		Если Не Лев(КлючИЗначениеПрослеж.Ключ, 12) = "СведПрослеж_" Тогда
			Продолжить;
		КонецЕсли;
		Если ТипЗнч(КлючИЗначениеПрослеж.Значение) = Тип("Массив") Тогда
			Для Каждого СтрокаСведПрослеж Из КлючИЗначениеПрослеж.Значение Цикл
				ОбработатьСведПрослеж_УКД_ДобавитьСтроку(Результат, КлючиРасчета, СтрокаСведПрослеж);
			КонецЦикла;
		Иначе
			ОбработатьСведПрослеж_УКД_ДобавитьСтроку(Результат, КлючиРасчета, КлючИЗначениеПрослеж.Значение);
		КонецЕсли;
	КонецЦикла;
	//Сделаем Формат, если есть описание
	Для НомерСтр = 1 По СтрЧислоСтрок(КлючиСумммирования) Цикл
		КлючДанные = СтрПолучитьСтроку(КлючиСумммирования, НомерСтр);
		СтрокаФормат = Неопределено;
		Если Не Контекст.Свойство(КлючДанные + "_Формат", СтрокаФормат) Тогда
			Продолжить;
		КонецЕсли;
		Для Каждого СтрокаРезультат Из Результат.Значение Цикл
			СтрокаРезультат[КлючДанные] = Формат(СтрокаРезультат[КлючДанные], СтрокаФормат);
		КонецЦикла;
	КонецЦикла;	
	Возврат Результат.Значение;
КонецФункции

&НаКлиенте
Процедура ОбработатьСведПрослеж_УКД_ДобавитьСтроку(Результат, Ключи, Данные) Экспорт
	КлючДанные = "";
	Для НомерСтр = 1 По СтрЧислоСтрок(Ключи.Группировка) Цикл
		КлючДанные = КлючДанные + Строка(Данные[СтрПолучитьСтроку(Ключи.Группировка, НомерСтр)]);
	КонецЦикла;
	СтрокаИндекс = Результат.Карта.Найти(КлючДанные);
	Если СтрокаИндекс = Неопределено Тогда
		Результат.Карта.Добавить(КлючДанные);
		Результат.Значение.Добавить(Данные);
	Иначе
		СтрокаДанные = Результат.Значение[СтрокаИндекс];
		Для НомерСтр = 1 По СтрЧислоСтрок(Ключи.Суммирование) Цикл
			КлючДанные = СтрПолучитьСтроку(Ключи.Суммирование, НомерСтр);
			СтрокаДанные[КлючДанные] = СтрокаДанные[КлючДанные] + Данные[КлючДанные];
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

