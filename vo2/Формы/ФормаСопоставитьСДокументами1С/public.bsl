
////////////////////////////////////////////////////////////////////////////////
// Открытие формы, формирование списка документов для сопоставления

// Формирование коллекции документов на форме
&НаКлиенте
Функция Показать(ПараметрыВходящие) Экспорт
	
	ИнициироватьФорму(ПараметрыВходящие);
	ТаблицаСопоставленияДокументов.Очистить();
	
	//Функция открывает окно сопоставления документов и заполняет все реквизиты на форме
	ГлавноеОкно							= МестныйКэш.ГлавноеОкно;
	СписокОтмеченныхДокументов			= ПараметрыВходящие.СписокДокументов;
	СписокДокументовДляСопоставления	= ПодготовитьСписокДокументовКСопоставлению(Кэш, СписокОтмеченныхДокументов);
	
	Всего	= СписокДокументовДляСопоставления.Количество();
	сч		= 0;
	Для Каждого Строка Из СписокДокументовДляСопоставления Цикл
		
		сч = сч + 1;
		ГлавноеОкно.сбисПоказатьСостояние("Ищем подходящие документы", ГлавноеОкно, Мин(100,Окр(сч*100/Всего)));

		//если уже есть документ 1С то его добавляем в строку, иначе ищем подходящие
		Документ1С					= Неопределено;
		СписокПодходящихДокументов	= Неопределено;
		СопоставлениеСохранено		= Ложь;
		КолВоДокументов				= 0;
		ЕстьСопоставления			= ЗначениеЗаполнено(Строка.Документы1С);
		
		Если ЕстьСопоставления Тогда
			
			КолВоДокументов			= 1;
			СопоставлениеСохранено	= Истина;
			Если		ТипЗнч(Строка.Документы1С) = Тип("СписокЗначений") Тогда
				Документ1С = Строка.Документы1С[0].Значение;
			ИначеЕсли	ТипЗнч(Строка.Документы1С) = Тип("Массив") Тогда
				Документ1С = Строка.Документы1С[0];
			Иначе
				Документ1С = Строка.Документы1С;
			КонецЕсли;
			
		Иначе
			
			ЭтоДубль = СбисЭтоДубльВложения(ТаблицаСопоставленияДокументов, Строка);
			Если Не ЭтоДубль Тогда
				СписокПодходящихДокументов	= Кэш.ОбщиеФункции.НайтиПодходящиеДокументы(Кэш, Строка);
				КолВоДокументов				= СписокПодходящихДокументов.Количество();
				
				Если КолВоДокументов = 1 Тогда
					Документ1С = СписокПодходящихДокументов[0].Значение;
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
				
		НоваяСтрока = ТаблицаСопоставленияДокументов.Добавить();
		НоваяСтрока.СписокПодходящихДокументов		= СписокПодходящихДокументов;
		НоваяСтрока.РанееСопоставленныеДокументы1С	= Строка.РанееСопоставленныеДокументы1С;
		Если Документ1С <> Неопределено Тогда 
			НоваяСтрока.Документ1С = Документ1С;
		КонецЕсли;
		
		НоваяСтрока.СоставПакета     = Новый СписокЗначений;
		НоваяСтрока.Организация      = Строка.СоставПакета.НашаОрганизация.Название;
		НоваяСтрока.Контрагент       = Строка.СоставПакета.Контрагент.Название;
		НоваяСтрока.Сумма            = Строка.Сумма;
		НоваяСтрока.Номер            = Строка.Номер;
		НоваяСтрока.ДатаДокумента    = Строка.Дата;
		НоваяСтрока.Статус           = Строка.Статус; 		 
		НоваяСтрока.Найдено          = КолВоДокументов;
		
		НоваяСтрока.СопоставлениеСохранено = СопоставлениеСохранено;
		
		Если КолВоДокументов = 1 Тогда
			
			НоваяСтрока.СопоставлениеОпределено = Истина;
			
			Если СопоставлениеСохранено = Истина Тогда
				//синяя иконка документа
				НоваяСтрока.НайденоКартинка = 1;
			Иначе
				//серая иконка документа
				НоваяСтрока.НайденоКартинка = 3;
			КонецЕсли;
		ИначеЕсли КолВоДокументов > 1 Тогда
			//восклицательный знак
			НоваяСтрока.НайденоКартинка = 2;
			НоваяСтрока.СопоставлениеОпределено = Ложь;
		Иначе
			//пусто
			НоваяСтрока.НайденоКартинка = 0;
			НоваяСтрока.СопоставлениеОпределено = Ложь;
		КонецЕсли;
		
		НоваяСтрока.СоставПакета.Добавить(Строка.СоставПакета);
		НоваяСтрока.ДокументСБИСНазвание	= Строка.НазваниеСБИС;
		НоваяСтрока.Типы1С					= Строка.Типы1С;
		НоваяСтрока.ТипСБИС					= Строка.ТипСБИС;
		НоваяСтрока.ИдСБИС					= Строка.СоставПакета.Идентификатор;
		НоваяСтрока.ИдВложения				= Строка.ИдВложения;
		НоваяСтрока.Состояние				= Строка.СоставПакета.Состояние.Название;
		НоваяСтрока.Отмечен					= Ложь;
		
		НоваяСтрока.ТипВерсияВложения	= Строка.ТипВерсияВложения;
		НоваяСтрока.ОрганизацияИНН		= ?(Строка.НашаОрганизация.Свойство("СвЮЛ"),Строка.НашаОрганизация.СвЮЛ.ИНН,?(Строка.НашаОрганизация.Свойство("СвФЛ"),Строка.НашаОрганизация.СвФЛ.ИНН,""));
		НоваяСтрока.ОрганизацияКПП		= ?(Строка.НашаОрганизация.Свойство("СвЮЛ"),Строка.НашаОрганизация.СвЮЛ.КПП,"");
		НоваяСтрока.КонтрагентИНН		= ?(Строка.Контрагент.Свойство("СвЮЛ"),Строка.Контрагент.СвЮЛ.ИНН,?(Строка.Контрагент.Свойство("СвФЛ"),Строка.Контрагент.СвФЛ.ИНН,"")) ;
		НоваяСтрока.КонтрагентКПП		= ?(Строка.Контрагент.Свойство("СвЮЛ"),Строка.Контрагент.СвЮЛ.КПП,"");
		НоваяСтрока.Дубль				= ЭтоДубль;
		НоваяСтрока.ИдентификаторСтроки = сч;//чтобы обновлять первоначальную копию при наложении фильтров
		
		// Состояние сопоставления:
		// 	0 - сопоставлено, всё хорошо
		//	1 - готов к сопоставлению, подобрать документ
		//	2 - проблемы при сопоставлении
		//	3 - проверке сопоставления не подлежит
		
	КонецЦикла;
	Если РежимРаботыФормы = "Сопоставление" Тогда
		ПеречитатьСравнениеДокументов(ТаблицаСопоставленияДокументов);
	Иначе		
		ВыполнитьСравнениеДокументов(ТаблицаСопоставленияДокументов, ПараметрыВходящие);
	КонецЕсли;
		
	Для Каждого Строка Из ТаблицаСопоставленияДокументов Цикл

		РассчитатьДельтуИСуммуДокумента1С(Строка);
		
	КонецЦикла;


	ВсегоДокументов = сч;

	//ФормаИндикатора.СпрятатьСтатусДлительнойОперации(Истина);
	ГлавноеОкно.сбисСпрятатьСостояние(ГлавноеОкно);
	
	Если ТаблицаСопоставленияДокументов.Количество() Тогда

		ЗаполнитьКопиюТаблицыСопоставления(МестныйКэш.ПараметрыСистемы);
 		ФильтрСопоставленияПоУмолчанию();
		ЭтаФорма.Открыть();
		
	Иначе
		
		Сообщить("Среди отмеченных документов нет подходящих для сопоставления.");
		
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ИнициироватьФорму(ПараметрыВходящие)
	
	ЗакрытьФормуФильтра();
	МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(ЭтаФорма, "СравнитьДокументы").Видимость = Истина;
	Если МестныйКэш = Неопределено Тогда
		Если Не ПараметрыВходящие.Свойство("Кэш", МестныйКэш) Тогда
			МестныйКэш = МодульОбъектаКлиент().ПолучитьТекущийЛокальныйКэш();
		КонецЕсли;
	КонецЕсли;
	
	Кэш = МестныйКэш;

	ПараметрыФормы			= Новый Структура("Результат");
	ОтметитьВсе				= Ложь;
 	СопоставлениеПоСумме	= МодульОбъектаКлиент().ПолучитьЗначениеПараметраСбис("СопоставлениеПоСумме");
	СуммаДельта				= 0;
	СуммаДокументовСБИС		= 0;
	Если Не ПараметрыВходящие.Свойство("Режим", РежимРаботыФормы) Тогда
		РежимРаботыФормы = "Сопоставление";
	КонецЕсли;

КонецПроцедуры

