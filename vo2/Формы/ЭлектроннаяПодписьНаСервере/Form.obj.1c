
&НаКлиенте
Функция сбисОписанияПрограмм(Кэш) Экспорт
	
	Если Кэш.Ини.Свойство("Конфигурация") и Кэш.Ини.Конфигурация.Свойство("мПрограммыЭП") и Не ЗначениеЗаполнено(Кэш.Парам.НастройкиКриптографии.ИмяПрограммы) Тогда
		ОписанияПрограмм = сбисПолучитьНастройкиКриптографии(Кэш);
	Иначе
		ОписанияПрограмм = Новый Массив;
		СвойстваПрограммы = Новый Структура("ИмяПрограммы,ПутьКПрограмме,ТипПрограммы", Кэш.Парам.НастройкиКриптографии.ИмяПрограммы,Кэш.Парам.НастройкиКриптографии.ПутьКПрограмме,Кэш.Парам.НастройкиКриптографии.ТипПрограммы);
		ОписанияПрограмм.Добавить(СвойстваПрограммы);
	КонецЕсли;
	Возврат ОписанияПрограмм;
КонецФункции
&НаСервере
Функция сбисСоздатьМенеджерКриптографииНаСервере(ОписанияПрограмм, Алгоритм = Неопределено) Экспорт
	
	Менеджер = Неопределено;
	Для Каждого СвойстваПрограммы Из ОписанияПрограмм Цикл
		
		Попытка
			ИнформацияМодуля = СредстваКриптографии.ПолучитьИнформациюМодуляКриптографии(
				СвойстваПрограммы.ИмяПрограммы,
				СвойстваПрограммы.ПутьКПрограмме,
				СвойстваПрограммы.ТипПрограммы);
		Исключение
			Продолжить;
		КонецПопытки;
		
		Если ИнформацияМодуля = Неопределено Тогда
			Менеджер = Неопределено;
			Продолжить;
		КонецЕсли;
		
		Попытка
			Менеджер = Новый МенеджерКриптографии(
				СвойстваПрограммы.ИмяПрограммы,
				СвойстваПрограммы.ПутьКПрограмме,
				СвойстваПрограммы.ТипПрограммы);
		Исключение
			Продолжить;
		КонецПопытки;

		Если Алгоритм<>Неопределено Тогда
			Если Алгоритм.Найти(Менеджер.АлгоритмПодписи) = Неопределено Тогда
				Менеджер = Неопределено;
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		Прервать; // Требуемый менеджер криптографии получен.
	КонецЦикла;  
	Если Менеджер = Неопределено Тогда
		ВызватьИсключение("Не удалось создать менеджер криптографии. Возможно в настройках неверно указана программа электронной подписи.");		
	КонецЕсли;
	Возврат Менеджер;
	
КонецФункции

&НаКлиенте
Функция сбисПолучитьНастройкиКриптографии(Кэш) Экспорт
	Контекст = Новый Структура("Документ, СтрТабл, Ини",,);
	Контекст.Ини = Новый Структура;
	Кэш.ОбщиеФункции.сбисСкопироватьСтруктуруНаКлиенте(Контекст.Ини, Кэш.Ини.Конфигурация.мПрограммыЭП);
	
	НастройкиЭП = Кэш.ОбщиеФункции.ПолучитьДанныеДокумента1С(Кэш.Ини.Конфигурация.мПрограммыЭП, Неопределено, Новый Структура, Кэш.Парам);
    Возврат НастройкиЭП.ПрограммыЭП;
КонецФункции
     
&НаКлиенте
Функция сбисСписокСертификатовДляАвторизации(Кэш, ТекстОшибки) Экспорт
	Возврат сбисСписокСертификатовДляАвторизацииНаСервере(сбисОписанияПрограмм(Кэш), ТекстОшибки);	
КонецФункции

&НаСервере
Функция сбисСписокСертификатовДляАвторизацииНаСервере(ОписанияПрограмм, ТекстОшибки) Экспорт
	СертификатыДляАвторизации = Новый Массив();
	Попытка
		МенеджерКриптографии = сбисСоздатьМенеджерКриптографииНаСервере(ОписанияПрограмм);
	Исключение    
		ТекстОшибки = ИнформацияОбОшибке().Описание;
		Возврат СертификатыДляАвторизации;
	КонецПопытки;
	
	ХранилищеСертификатовКриптографии = МенеджерКриптографии.ПолучитьХранилищеСертификатов();
	Сертификаты = ХранилищеСертификатовКриптографии.ПолучитьВсе(); 
	Для Каждого Серт из Сертификаты Цикл
		Попытка
			СертДвоичныеДанные = СтрЗаменить(СтрЗаменить(Base64Строка(Серт.Выгрузить()),Символы.ПС,""),Символы.ВК,"");
			СертОтпечаток = СтрЗаменить(Строка(Серт.Отпечаток), " ", "");
			СертификатыДляАвторизации.Добавить(Новый Структура("ДвоичныеДанные,Отпечаток,Название,ФИО,Должность,ДействителенПо",СертДвоичныеДанные, СертОтпечаток,Серт.Субъект.CN, Серт.Субъект.SN+" "+Серт.Субъект.GN,?(Серт.Субъект.Свойство("T"),Серт.Субъект.T,""),Серт.ДатаОкончания));
		Исключение
		КонецПопытки;
	КонецЦикла;
	Возврат СертификатыДляАвторизации;

КонецФункции
&НаКлиенте
Функция сбисДвоичныеДанныеСертификатаПоОтпечатку(Кэш, Отпечаток) Экспорт
	Возврат сбисДвоичныеДанныеСертификатаПоОтпечаткуНаСервере(сбисОписанияПрограмм(Кэш), Отпечаток);	
КонецФункции  
&НаСервере
Функция сбисДвоичныеДанныеСертификатаПоОтпечаткуНаСервере(ОписанияПрограмм, Отпечаток) Экспорт
	Попытка
		МенеджерКриптографии = сбисСоздатьМенеджерКриптографииНаСервере(ОписанияПрограмм);
	Исключение    
		Возврат Ложь;
	КонецПопытки;
	ХранилищеСертификатовКриптографии = МенеджерКриптографии.ПолучитьХранилищеСертификатов();
	Сертификаты = ХранилищеСертификатовКриптографии.ПолучитьВсе(); 
	Для Каждого Серт из Сертификаты Цикл
		Если СтрЗаменить(Строка(Серт.Отпечаток), " ", "") = Отпечаток Тогда
			Возврат СтрЗаменить(СтрЗаменить(Base64Строка(Серт.Выгрузить()),Символы.ПС,""),Символы.ВК,"");
		КонецЕсли;
	КонецЦикла;
КонецФункции

&НаКлиенте
Функция сбисВыбратьПодходящийСертификат(Кэш, СертификатыДок, ТекстОшибки) Экспорт
	Возврат сбисВыбратьПодходящийСертификатНаСервере(сбисОписанияПрограмм(Кэш), СертификатыДок, Кэш.Парам, ТекстОшибки);
КонецФункции

&НаСервере
Функция сбисВыбратьПодходящийСертификатНаСервере(ОписанияПрограмм, СертификатыДок, КэшПарам, ТекстОшибки) Экспорт
	Результат = Новый Структура();
	Попытка
		МенеджерКриптографии = сбисСоздатьМенеджерКриптографииНаСервере(ОписанияПрограмм);
	Исключение    
		ТекстОшибки = ИнформацияОбОшибке().Описание;
	КонецПопытки;
	Если ТекстОшибки = "" Тогда
		Попытка
			ХранилищеСертификатовКриптографии = МенеджерКриптографии.ПолучитьХранилищеСертификатов();
			Сертификаты = ХранилищеСертификатовКриптографии.ПолучитьВсе(); 
		Исключение
			ТекстОшибки = ИнформацияОбОшибке().Описание;
		КонецПопытки;
	КонецЕсли;
	Алгоритм = Неопределено;
	Для Каждого СертДок Из СертификатыДок Цикл
		Если СертДок.Ключ.Тип = "Отложенный" Или СертДок.Ключ.Тип = "ОтложенныйСПодтверждением" Или СертДок.Ключ.Тип = "Серверный" Тогда
			Результат.Вставить("СертификатДляПодписания", Неопределено);
			Результат.Вставить("СертификатДок", СертДок); 
			Результат.Вставить("Алгоритм", Алгоритм);
			Возврат Результат;
		ИначеЕсли СертДок.Ключ.Тип = "Клиентский" и ТекстОшибки = "" Тогда
			Для Каждого Серт из Сертификаты Цикл
				СертОтпечаток = СтрЗаменить(Строка(Серт.Отпечаток), " ", "");
				Если  СертОтпечаток = СертДок.Отпечаток Тогда
                    СертДД = Серт.Выгрузить();
					Если Не ЗначениеЗаполнено(КэшПарам.НастройкиКриптографии.ИмяПрограммы) Тогда  // используются штатные настройки 1С, надо выбрать подходящую по алгоритму подписи
						Попытка
							Алгоритм = сбисВыбратьНаборАлгоритмовДляСозданияПодписи(СертДок.Алгоритм);
							Если Алгоритм.Найти(МенеджерКриптографии.АлгоритмПодписи) = Неопределено Тогда
								Попытка
									сбисСоздатьМенеджерКриптографииНаСервере(ОписанияПрограмм, Алгоритм);
								Исключение
									// если не удалось создать менеджер криптографии с нужным алгоритмом, то продолжим поиск подходящего серта   
									ТекстОшибки = ИнформацияОбОшибке().Описание;
									Продолжить;
								КонецПопытки;
							КонецЕсли;
						Исключение
						КонецПопытки;
					КонецЕсли;
                    Результат.Вставить("СертификатДляПодписания", Base64Строка(СертДД));    
					Результат.Вставить("СертификатДок", СертДок); 
					Результат.Вставить("Алгоритм", Алгоритм);
					Возврат Результат;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	Возврат Ложь;

КонецФункции

&НаКлиенте
Функция сбисПодписатьФайл(Кэш, ПолноеИмяФайла, Сертификат, Алгоритм, ТекстОшибки) Экспорт
	ДвоичныеДанные = Новый ДвоичныеДанные(ПолноеИмяФайла);
	Подпись = сбисПодписатьФайлНаСервере(сбисОписанияПрограмм(Кэш), Base64Строка(ДвоичныеДанные), Сертификат, Алгоритм, ТекстОшибки);
	Возврат Подпись;
КонецФункции

&НаКлиенте
Функция сбисПодписатьДвоичныеДанные(Кэш, ДвоичныеДанные, Сертификат, Алгоритм, ТекстОшибки) Экспорт
	Подпись = сбисПодписатьФайлНаСервере(сбисОписанияПрограмм(Кэш), ДвоичныеДанные, Сертификат, Алгоритм, ТекстОшибки);
	Возврат Подпись;
КонецФункции

&НаСервере
Функция сбисПодписатьФайлНаСервере(ОписанияПрограмм, ДвоичныеДанные, Сертификат, Алгоритм, ТекстОшибки) Экспорт
	Попытка
		МенеджерКриптографии = сбисСоздатьМенеджерКриптографииНаСервере(ОписанияПрограмм, Алгоритм);
	Исключение    
		ТекстОшибки = ИнформацияОбОшибке().Описание;
		Возврат Ложь;
	КонецПопытки;
	Серт = Новый СертификатКриптографии(Base64Значение(Сертификат));
	Подпись = МенеджерКриптографии.Подписать(Base64Значение(ДвоичныеДанные), Серт);
	Возврат СтрЗаменить(СтрЗаменить(Base64Строка(Подпись),Символы.ПС,""),Символы.ВК,"");
КонецФункции

&НаКлиенте
Функция сбисРасшифровать(Кэш, ДвоичныеДанные, ПолноеИмяФайла) Экспорт
	Рез = сбисРасшифроватьНаСервере(сбисОписанияПрограмм(Кэш), Base64Строка(ДвоичныеДанные));
	РезДвоичныеДанные = Base64Значение(Рез);
	РезДвоичныеДанные.Записать(ПолноеИмяФайла);
КонецФункции 

&НаСервере
Функция сбисРасшифроватьНаСервере(ОписанияПрограмм, ДвоичныеДанные) Экспорт
	МенеджерКриптографии = сбисСоздатьМенеджерКриптографииНаСервере(ОписанияПрограмм);
	ДвоичныеДанныеРашифрованные = МенеджерКриптографии.Расшифровать(Base64Значение(ДвоичныеДанные));
	НедопустимыеСимволы = Новый Массив;
	НедопустимыеСимволы.Добавить(Символы.ПС);
	НедопустимыеСимволы.Добавить(Символы.ВК);
	ДанныеВозврат = Base64Строка(ДвоичныеДанныеРашифрованные);
	Для Каждого НедопустимыйСимвол Из НедопустимыеСимволы Цикл
		ДанныеВозврат = СтрЗаменить(ДанныеВозврат, НедопустимыйСимвол, "");
	КонецЦикла;
	Возврат ДанныеВозврат;
КонецФункции  
&НаСервере
Функция сбисВыбратьНаборАлгоритмовДляСозданияПодписи(Алгоритм) Экспорт
	
	Алгоритмы = сбисИменаАлгоритмовПодписиГОСТ_34_10_2012_256();
	Если Алгоритмы.Найти(Алгоритм)<>Неопределено Тогда
		Возврат Алгоритмы;
	КонецЕсли; 
	Алгоритмы = сбисИменаАлгоритмовПодписиГОСТ_34_10_2012_512();
	Если Алгоритмы.Найти(Алгоритм)<>Неопределено Тогда
		Возврат Алгоритмы;
	КонецЕсли;
	Алгоритмы = сбисИменаАлгоритмовПодписиГОСТ_34_10_2001();
	Если Алгоритмы.Найти(Алгоритм)<>Неопределено Тогда
		Возврат Алгоритмы;
	КонецЕсли;
	Алгоритмы = сбисИменаАлгоритмовПодписиГОСТ_34_10_94();
	Если Алгоритмы.Найти(Алгоритм)<>Неопределено Тогда
		Возврат Алгоритмы;
	КонецЕсли;
	Алгоритмы = Новый Массив;
	Алгоритмы.Добавить("RSA_SIGN");
	Алгоритмы.Добавить("SHA1RSA");     // приходит от СБИС
	Алгоритмы.Добавить("SHA1WithRSASignature");    // приходит от СБИС
	Алгоритмы.Добавить("SHA256RS");   // приходит от СБИС
	Если Алгоритмы.Найти(Алгоритм)<>Неопределено Тогда
		Возврат Алгоритмы;
	КонецЕсли;
	
	Возврат сбисИменаАлгоритмовПодписиГОСТ_34_10_2012_256();
	
КонецФункции

&НаСервере
Функция сбисИменаАлгоритмовПодписиГОСТ_34_10_94()
	
	Имена = Новый Массив;
	Имена.Добавить("ГОСТ 34.10-94"); // Представление.
	Имена.Добавить("GOST R 34.10-94");
	
	Возврат Имена;
	
КонецФункции
&НаСервере
Функция сбисИменаАлгоритмовПодписиГОСТ_34_10_2001()
	
	Имена = Новый Массив;
	Имена.Добавить("ГОСТ 34.10-2001"); // Представление.
	Имена.Добавить("ГОСТ Р 34.10-2001");  // приходит от СБИС
	Имена.Добавить("GOST R 34.10-2001");
	Имена.Добавить("ECR3410-CP");
	
	Возврат Имена;
	
КонецФункции
&НаСервере
Функция сбисИменаАлгоритмовПодписиГОСТ_34_10_2012_256()
	
	Имена = Новый Массив;
	Имена.Добавить("ГОСТ 34.10-2012 256"); // Представление.
	Имена.Добавить("ГОСТ Р 34.10-2012 256");   // приходит от СБИС
	Имена.Добавить("GR 34.10-2012 256");
	Имена.Добавить("GOST 34.10-2012 256");
	Имена.Добавить("GOST R 34.10-12 256");
	Имена.Добавить("GOST3410-12-256");
	
	Возврат Имена;
	
КонецФункции
&НаСервере
Функция сбисИменаАлгоритмовПодписиГОСТ_34_10_2012_512()
	
	Имена = Новый Массив;
	Имена.Добавить("ГОСТ 34.10-2012 512"); // Представление. 
	Имена.Добавить("ГОСТ Р 34.10-2012 512");   // приходит от СБИС
	Имена.Добавить("GR 34.10-2012 512");
	Имена.Добавить("GOST 34.10-2012 512");
	
	Возврат Имена;
	
КонецФункции