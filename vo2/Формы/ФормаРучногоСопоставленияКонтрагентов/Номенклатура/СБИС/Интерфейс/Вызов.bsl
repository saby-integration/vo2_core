&НаКлиенте 
Процедура Показать(ПараметрыОткрытия) Экспорт

	СбисУстановитьФорму(ПараметрыОткрытия);
	#Если ТолстыйКлиентОбычноеПриложение Тогда
		ОткрытьМодально();
	#Иначе
		Открыть();
    #КонецЕсли
КонецПроцедуры 


&НаКлиенте
Процедура СбисУстановитьФорму(ПараметрыОткрытия)
	
	ЭтаФорма.Заголовок = ПараметрыОткрытия.Заголовок;
	ТабличнаяЧасть.Очистить();
	МестныйКэш = ПараметрыОткрытия.Кэш; 
	ПараметрыФормы = Новый Структура("Результат");
	
	#Область ЗаполнениеДанныхНоменклатурыСБИС
	// Данные шапки берём неизменно из номенклатуры СБИС, изменения вносить запрещено
	НоменклатураКонтрагента  = ПараметрыОткрытия.ОсновныеДанные.НоменклатураСБИС.Наименование;
	КоличествоСБИС 			 = ПараметрыОткрытия.ОсновныеДанные.НоменклатураСБИС.Количество;
	Цена 					 = ПараметрыОткрытия.ОсновныеДанные.НоменклатураСБИС.Цена;
	СтавкаНДС 				 = ПараметрыОткрытия.ОсновныеДанные.НоменклатураСБИС.СтавкаНДС;
	СуммаНДСПоДокументу 	 = ПараметрыОткрытия.ОсновныеДанные.НоменклатураСБИС.СуммаНДС;  
	СуммаПоДокументу 		 = ПараметрыОткрытия.ОсновныеДанные.НоменклатураСБИС.Сумма;
	СуммаВсего               = ПараметрыОткрытия.ОсновныеДанные.НоменклатураСБИС.Сумма;
	СуммаНДСВсего			 = ПараметрыОткрытия.ОсновныеДанные.НоменклатураСБИС.СуммаНДС;
	
	Если ПараметрыОткрытия.ОсновныеДанные.НоменклатураСБИС.Свойство("GTIN") Тогда
		GTIN = ПараметрыОткрытия.ОсновныеДанные.НоменклатураСБИС.GTIN;
	КонецЕсли;
	
	Если ПараметрыОткрытия.ОсновныеДанные.НоменклатураСБИС.Свойство("КодПокупателя")  Тогда
		КодПокупателя = ПараметрыОткрытия.ОсновныеДанные.НоменклатураСБИС.КодПокупателя;		
	ИначеЕсли ПараметрыОткрытия.ОсновныеДанные.НоменклатураСБИС.Свойство("ТипКода") И ПараметрыОткрытия.ОсновныеДанные.НоменклатураСБИС.ТипКода = "КодПокупателя" Тогда
		КодПокупателя = ПараметрыОткрытия.ОсновныеДанные.НоменклатураСБИС.Код;
	Иначе 
		КодПокупателя = "";
	КонецЕсли;
	
	Если ПараметрыОткрытия.ОсновныеДанные.НоменклатураСБИС.Свойство("Характеристика") Тогда  
		Характеристика = ПараметрыОткрытия.ОсновныеДанные.НоменклатураСБИС.Характеристика;
	КонецЕсли;
	
	// Теоретически не может быть несколько единиц поставщика, но если такая фигня случится, то возьмём первую
	Если ПараметрыОткрытия.ОсновныеДанные.НоменклатураСБИС.Единицы.Количество() Тогда 
		Для Каждого ЕдиницаПоставщика Из ПараметрыОткрытия.ОсновныеДанные.НоменклатураСБИС.Единицы Цикл 
			ЕдиницыИзмерения 		= ЕдиницаПоставщика.Значение.Название;
			КоэффициентСБИС 		= ЕдиницаПоставщика.Значение.Коэффициент; 
			Прервать;
		КонецЦикла;
	КонецЕсли;
	#КонецОбласти //ЗаполнениеДанныхНоменклатурыСБИС
	
	#Область СправочникиНоменклатурыИЕдиниц
	Если МестныйКэш.Ини.Конфигурация.Свойство("Номенклатура") Тогда
		ИмяСправочникаНоменклатуры = СокрЛП(Сред(МестныйКэш.Ини.Конфигурация.Номенклатура.Значение, Найти(МестныйКэш.Ини.Конфигурация.Номенклатура.Значение, ".") + 1));
		СправочникНоменклатураПолноеИмя = "Справочник." + ИмяСправочникаНоменклатуры;
		ТипСправочникаНоменклатуры = "СправочникСсылка." + ИмяСправочникаНоменклатуры;
	Иначе
		СправочникНоменклатураПолноеИмя = "Справочник.Номенклатура";
		ТипСправочникаНоменклатуры = "СправочникСсылка.Номенклатура";
	КонецЕсли;
	
	ТипыИзИниКонфигурация = МодульОбъектаКлиент().ПолучитьЗначениеПараметраТекущегоСеанса("ТипыПолей1С");
	ТипСправочникаЕдиницаИзмерения = ТипыИзИниКонфигурация.КлассификаторЕдиницИзмерения.ОписаниеТипа;
	
	ЭлементТабличнаяЧастьНоменклатура = МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(ЭтаФорма, "ТабличнаяЧастьНоменклатура");
	ЭлементТабличнаяЧастьНоменклатура.ОграничениеТипа = Новый ОписаниеТипов(ТипСправочникаНоменклатуры);

	ЭлементТабличнаяЧастьЕдиницы = МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(ЭтаФорма, "ТабличнаяЧастьЕдиницы");
	ЭлементТабличнаяЧастьЕдиницы.ОграничениеТипа = Новый ОписаниеТипов(ТипСправочникаЕдиницаИзмерения);
	
	ДанныеРучногоСопоставления = ПараметрыОткрытия.ОсновныеДанные;
	#КонецОбласти // Справочники номенклатуры и единиц
	
	#Область ЗаполнениеНоменклатуры1С
	ОсновноеСопоставлениеНайдено = Ложь;
	Для Каждого Номенклатура1С Из ПараметрыОткрытия.ОсновныеДанные.Номенклатура1С Цикл
		
		Если НЕ ОсновноеСопоставлениеНайдено 
			И Номенклатура1С.Значение.Основное Тогда
			ОсновноеСопоставлениеНайдено = Истина;
		ИначеЕсли ОсновноеСопоставлениеНайдено 
			И Номенклатура1С.Значение.Основное Тогда
			МодульОбъектаКлиент().ОписаниеНоменклатуры1СКлиент_Вставить(Номенклатура1С.Значение, "Основное", Ложь);
		Иначе
			// Подумать какие ещё варианты развития событий могут быть
		КонецЕсли;
		
		Если ПараметрыОткрытия.ОсновныеДанные.Номенклатура1С.Количество() = 1 
			И НЕ ЗначениеЗаполнено(Номенклатура1С.Ключ) Тогда
			
			ТабличнаяЧастьНстр = ТабличнаяЧасть.Добавить(); 
			ЗаполнитьЗначенияСвойств(ТабличнаяЧастьНСтр, ПараметрыОткрытия.ОсновныеДанные.НоменклатураСБИС, "Цена, Кол_во, СуммаНДС, СтавкаНДС, СуммаБезНал, Сумма");
			
			Если НЕ КоэффициентСБИС = 0 Тогда
				ТабличнаяЧастьНСтр.Коэффициент 	  = КоэффициентСБИС;
				ТабличнаяЧастьНСтр.КоэффициентБыл = КоэффициентСБИС; 
			Иначе
				ТабличнаяЧастьНСтр.Коэффициент 	  = 1;
				ТабличнаяЧастьНСтр.КоэффициентБыл = 1;
			КонецЕсли;
			
			Прервать;
			
		ИначеЕсли ПараметрыОткрытия.ОсновныеДанные.Номенклатура1С.Количество() > 1
			И НЕ ЗначениеЗаполнено(Номенклатура1С.Ключ) Тогда
			Продолжить; 
		Иначе
			ТабличнаяЧастьНстр = ТабличнаяЧасть.Добавить();	
		КонецЕсли;     
		
		ТабличнаяЧастьНстр.Номенклатура = Номенклатура1С.Ключ;    
		
		ЗаполнитьДанныеДляЗагрузкиВДокумент1С(ДанныеРучногоСопоставления, ТабличнаяЧастьНстр.Номенклатура);
		
		// Первую заполненную номенклатуру 1С в списке найденных сопоставлений считаем основной и заполняем для неё все табличные данные из документа
		Если Номенклатура1С.Значение.Основное 
			И ТабличнаяЧастьНстр.Кол_во = 0 
			И ТабличнаяЧастьНСтр.Цена = 0 Тогда
			
			ЗаполнитьЗначенияСвойств(ТабличнаяЧастьНСтр, ПараметрыОткрытия.ОсновныеДанные.НоменклатураСБИС, "Цена, Кол_во, СуммаНДС, СтавкаНДС, СуммаБезНал, Сумма");   
			
		КонецЕсли;
		
		// Для всех строк по умолчанию заполняем:
		// --Единицы
		// --Ссылку на номенклатуру 1С
		// --GTIN
		ЗаполнитьЗначенияСвойств(ТабличнаяЧастьНСтр, Номенклатура1С.Значение, "GTIN");
		ТабличнаяЧастьНстр.Номенклатура = Номенклатура1С.Ключ;
		
		Если ЗначениеЗаполнено(Номенклатура1С.Значение.Единицы) Тогда 
			
			Для Каждого Единица Из Номенклатура1С.Значение.Единицы Цикл
				ТабличнаяЧастьНСтр.Единицы 	   = Единица.Ключ;
				
				Если ЗначениеЗаполнено(Единица.Значение.Коэффициент) Тогда
					ТабличнаяЧастьНСтр.Коэффициент = Единица.Значение.Коэффициент;
				ИначеЕсли ЗначениеЗаполнено(КоэффициентСБИС) Тогда
					ТабличнаяЧастьНСтр.Коэффициент = КоэффициентСБИС;
				Иначе
					ТабличнаяЧастьНСтр.Коэффициент = 1;
				КонецЕсли;
				
				ТабличнаяЧастьНстр.БазоваяЕдиницаОКЕИ = ОКЕИБазовойЕдиницыНоменклатуры1С(Единица.Ключ);
				Прервать;
			КонецЦикла;
					
			ТабличнаяЧастьНСтр.КоэффициентБыл = ТабличнаяЧастьНСтр.Коэффициент;
			ПроизвестиПересчетПоКоэффициентуЕдиницИзмерения(ТабличнаяЧастьНСтр);
				
		КонецЕсли;
		
		
	КонецЦикла;
	
	Если НЕ ТабличнаяЧасть.Количество() Тогда
		ТабличнаяЧастьНстр = ТабличнаяЧасть.Добавить();
		ЗаполнитьЗначенияСвойств(ТабличнаяЧастьНСтр, ПараметрыОткрытия.ОсновныеДанные.НоменклатураСБИС, "Цена, Кол_во, СуммаНДС, СтавкаНДС, СуммаБезНал, Сумма");
	КонецЕсли;
	#КонецОбласти //ЗаполнениеНоменклатуры1С
		
	// Не допилено, на будущее: обработка ошибок сопоставления номенклатуры будет блокировать интерфейс для закрытия пока пользователь не исправит всё, или закроет без сохранения изменений
	СообщенияОбОшибках = Новый Структура;
	СообщенияОбОшибках.Вставить("Модуль");
	СообщенияОбОшибках.Вставить("ТекстОшибки");
	СообщенияОбОшибках.Вставить("Критическая");
	// --------------------------------------------------------- 
	
	ЗакрытьБезСохранения = Ложь;
	СохранениеПроведено  = Ложь; 
	ИмеютсяИзмененияСопоставленнойНоменклатуры = Ложь;
	                  
	#Область Подвал
	ПеречистатьСуммыПоСтрокам();
	ПересчитатьИспользованноеИДоступноеКоличествоПоДокументу();
	#КонецОбласти // Подвал
	
КонецПроцедуры
