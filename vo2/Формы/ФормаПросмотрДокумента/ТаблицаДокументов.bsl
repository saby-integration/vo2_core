
// Процедура устанавливает строку таблицы документов по данным вложения СБИС
&НаКлиенте
Процедура ТаблицаДокументовУстановитьСтроку(СтрокаТаблицыДокументов, ВложениеСБИС)
	Перем ПараметрыДляТЧНоменклатуры;

	ДанныеДляЗагрузки = МестныйКэш.ОбщиеФункции.ЗаполнитьДанныеДляЗагрузкиПоУмолчанию(МестныйКэш, СоставПакета, ВложениеСБИС);

	ПараметрыНаПросмотр	= МодульОбъектаКлиент().ВложениеСБИС_Получить(ВложениеСБИС, "Просмотр");

	Если ВложениеСБИС.Свойство("Название") Тогда
		
		СтрокаТаблицыДокументов.Название = ВложениеСБИС.Название;
		
	Иначе
		
		СтрокаТаблицыДокументов.Название = "";
		
	КонецЕсли;
	
	Если ВложениеСБИС.Свойство("Идентификатор") Тогда
		
		СтрокаТаблицыДокументов.Идентификатор = ВложениеСБИС.Идентификатор;
		
	Иначе
		
		СтрокаТаблицыДокументов.Идентификатор = "";
		
	КонецЕсли;
	
	Если ВложениеСБИС.Свойство("Шифрование") Тогда
		
		СтрокаТаблицыДокументов.Шифрование = ВложениеСБИС.Шифрование;
		
	Иначе
		
		СтрокаТаблицыДокументов.Шифрование = Ложь;
		
	КонецЕсли;
	
	Если ВложениеСБИС.Свойство("Документы1С") Тогда
		
		СтрокаТаблицыДокументов.Документы1С			= ВложениеСБИС.Документы1С;
		СтрокаТаблицыДокументов.Документы1СНазвания = "";
		
		Для Каждого Документ1СВложения Из ВложениеСБИС.Документы1С Цикл
			
			СтрокаТаблицыДокументов.Документы1СНазвания = СтрокаТаблицыДокументов.Документы1СНазвания + Строка(Документ1СВложения.Значение) + Символы.ПС;
			
		КонецЦикла;
	Иначе
		
		СтрокаТаблицыДокументов.Документы1С			= Новый СписокЗначений;
		СтрокаТаблицыДокументов.Документы1СНазвания = "";
		
	КонецЕсли;
	
	Если ВложениеСБИС.Свойство("Подпись") Тогда
		
		СтрокаТаблицыДокументов.Подпись = "";
		Для Каждого Подпись Из ВложениеСБИС.Подпись Цикл
			
			СтрокаТаблицыДокументов.Подпись = СтрокаТаблицыДокументов.Подпись + МестныйКэш.ОбщиеФункции.ПолучитьПредставлениеСертификата(Подпись.Сертификат, "[ФИО]., [Должность]., [Название]") + Символы.ПС;
			
		КонецЦикла;
	Иначе
		
		СтрокаТаблицыДокументов.Подпись = "";
		
	КонецЕсли;
	
	СтрокаТаблицыДокументов.Вложение.Добавить(ВложениеСБИС);
	
	СтрокаТаблицыДокументов.МожемЗагрузитьВ1С	= Число(ДанныеДляЗагрузки.МожемЗагрузитьВ1С);
	СтрокаТаблицыДокументов.Статус				= ДанныеДляЗагрузки.Статус;
	СтрокаТаблицыДокументов.СтатусКартинка		= ДанныеДляЗагрузки.СтатусКартинка;
	
	Если ВложениеСБИС.Свойство("Отмечен")  Тогда
		
		СтрокаТаблицыДокументов.Отмечен = ВложениеСБИС.Отмечен;
		
	Иначе
		
		СтрокаТаблицыДокументов.Отмечен = Истина;
		
	КонецЕсли;
	
	ДанныеДляЗагрузки.Свойство("ПутьТаблДок",		СтрокаТаблицыДокументов.ПутьТаблДок);
	ДанныеДляЗагрузки.Свойство("ПутьКонтрагента",	СтрокаТаблицыДокументов.ПутьКонтрагента);
	
	Если Не ПараметрыНаПросмотр = Неопределено Тогда
		
		ПараметрыНаПросмотр.ПараметрыКонтрагент.ПоказатьСозданиеКарточки = Булево(Найти(ДанныеДляЗагрузки.Статус, "Не найден контрагент"));
		Если ДанныеДляЗагрузки.Свойство("ПараметрыНоменклатуры", ПараметрыДляТЧНоменклатуры) Тогда
			
			ПараметрыНаПросмотр.Вставить("ПараметрыНоменклатуры", ПараметрыДляТЧНоменклатуры);
			
		КонецЕсли;
	
	КонецЕсли;
	
	Если ДанныеДляЗагрузки.Свойство("ТекстОшибки") Тогда
		
		Сообщить(ДанныеДляЗагрузки.ТекстОшибки);
		
	КонецЕсли;
	
КонецПроцедуры

#Область include_core2_vo2_Формы_ФормаПросмотрДокумента_ТаблицаДокументов_События
#КонецОбласти

