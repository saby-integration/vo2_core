// Копия объекта ЛокалальныйКэш в модуле
&НаКлиенте
Перем МестныйКэш;

#Область include_local_ПолучитьМодульОбъекта
#КонецОбласти

&НаКлиенте
Функция СбисПолучитьФорму(ИмяФормы)
	Возврат МодульОбъектаКлиент().ПолучитьФормуОбработки(ИмяФормы);
КонецФункции

&НаКлиенте
Функция СбисЭлементФормы(Форма, ИмяЭлемента)
	Возврат МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(Форма, ИмяЭлемента);
КонецФункции

&НаКлиенте
Функция СбисПолучитьСтраницу(Элемент, ИмяСтраницы)
	Если ТипЗнч(ЭтаФорма) = Тип("УправляемаяФорма") Тогда
		Возврат Элемент.ПодчиненныеЭлементы[ИмяСтраницы];
	КонецЕсли;
	Возврат Элемент.Страницы[ИмяСтраницы];
КонецФункции
// ------------------------------------------------------
&НаКлиенте
Функция ОбновитьКонтент(Кэш) Экспорт 
	Перем СбисТекущийЭтап, Стороны;
	МестныйКэш = Кэш;
	ГлавноеОкно = Кэш.ГлавноеОкно;
	ПараметрыФильтра = Новый Структура("Тип, Направление, ДопПоля",	"ConsignmentNote", "Входящий", "Стороны,ТекущиеЭтапы");
	filter = Кэш.ОбщиеФункции.ПолучитьФильтрСобытий(Кэш, ПараметрыФильтра);

	СтруктураДанныхПолученная = Кэш.Интеграция.СбисПолучитьСписокДокументовПоФильтру(Кэш, filter, ГлавноеОкно); 
	
	СтруктураДляОбновленияФормы = Новый Структура("Таблица_РеестрСобытий");
	Если Не СтруктураДанныхПолученная.Свойство("Таблица_РеестрДокументов", СтруктураДляОбновленияФормы.Таблица_РеестрСобытий) Тогда
		СтруктураДляОбновленияФормы.Таблица_РеестрСобытий = Новый Массив;
	КонецЕсли;
	
	// Заказчик и Перевозчик
	Для Индекс = 0 По СтруктураДляОбновленияФормы.Таблица_РеестрСобытий.ВГраница() Цикл
		ОбрабатываемоеСобытие = СтруктураДляОбновленияФормы.Таблица_РеестрСобытий[Индекс];
		ОбрабатываемоеСобытие.СоставПакета[0].Значение.Свойство("Стороны", Стороны); 
		Если ЗначениеЗаполнено(Стороны) Тогда
			Если Стороны.Свойство("Заказчик") Тогда
				ОбрабатываемоеСобытие.Вставить("Заказчик",			Кэш.ОбщиеФункции.СбисНазваниеСтороны(Стороны.Заказчик));
			КонецЕсли;
			Если Стороны.Свойство("Перевозчик") Тогда
				ОбрабатываемоеСобытие.Вставить("Перевозчик",		Кэш.ОбщиеФункции.СбисНазваниеСтороны(Стороны.Перевозчик));
			КонецЕсли;
			Если Стороны.Свойство("Получатель") Тогда
				ОбрабатываемоеСобытие.Вставить("НашаОрганизация",	Кэш.ОбщиеФункции.СбисНазваниеСтороны(Стороны.Получатель));
			КонецЕсли;
			Если Стороны.Свойство("Отправитель") Тогда
				ОбрабатываемоеСобытие.Вставить("Контрагент",		Кэш.ОбщиеФункции.СбисНазваниеСтороны(Стороны.Отправитель));
			КонецЕсли;
		КонецЕсли;
		Если		ОбрабатываемоеСобытие.СоставПакета[0].Значение.Свойство("ТекущиеЭтапы", СбисТекущийЭтап) Тогда
			Если ТипЗнч(СбисТекущийЭтап) = Тип("Массив")
				И	СбисТекущийЭтап.Количество() Тогда
				
				ОбрабатываемоеСобытие.Вставить("ТекущийЭтап", СбисТекущийЭтап[0].Наименование);
			ИначеЕсли ТипЗнч(СбисТекущийЭтап) = Тип("Структура")
				И СбисТекущийЭтап.Свойство("Наименование", СбисТекущийЭтап) Тогда 
				
				ОбрабатываемоеСобытие.Вставить("ТекущийЭтап", СбисТекущийЭтап);
			Иначе
				// Пришло что-то новое, этап не ставим, но и не падаем
				Продолжить;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Кэш.ОбщиеФункции.ОбновитьПанельНавигации(Кэш);
	Контент = сбисЭлементФормы(ГлавноеОкно, "Контент");
	Контент.ТекущаяСтраница = сбисПолучитьСтраницу(Контент, "РеестрСобытий");	
	Кэш.ТаблДок = сбисЭлементФормы(ГлавноеОкно,"Таблица_РеестрСобытий");
	ГлавноеОкно.СписокДопОперацийРеестра.Очистить();
	Возврат СтруктураДляОбновленияФормы;
КонецФункции

// Функция делает подготовку к переходу	
&НаКлиенте
Функция ОбновитьКонтент_ПередВызовом(СтруктураРаздела, Кэш) Экспорт
	
	Если ЗначениеЗаполнено(Кэш.Парам.ФильтрыПоРазделам["Полученные_ЭТрН"]) Тогда
		Кэш.ГлавноеОкно.сбисВосстановитьФильтр(Кэш, Кэш.Парам.ФильтрыПоРазделам["Полученные_ЭТрН"]);
	Иначе
		ФильтрОчистить(Кэш);
	КонецЕсли;              
	
КонецФункции

&НаКлиенте	
Процедура НастроитьКолонки(Кэш) Экспорт  
	
	МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(Кэш.ГлавноеОкно, "Таблица_РеестрСобытий.Вложения").Видимость = Ложь;                    
	#Если ТолстыйКлиентОбычноеПриложение Тогда
		МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(Кэш.ГлавноеОкно, "Таблица_РеестрСобытий.НашаОрганизация").ТекстШапки = "Получатель";
		МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(Кэш.ГлавноеОкно, "Таблица_РеестрСобытий.Контрагент").ТекстШапки = "Отправитель";
	#Иначе
		МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(Кэш.ГлавноеОкно, "Таблица_РеестрСобытий.НашаОрганизация").Заголовок = "Получатель";
		МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(Кэш.ГлавноеОкно, "Таблица_РеестрСобытий.Контрагент").Заголовок = "Отправитель";
	#КонецЕсли

	МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(Кэш.ГлавноеОкно, "Таблица_РеестрСобытий.Комментарий").Видимость = Ложь;
	МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(Кэш.ГлавноеОкно, "Таблица_РеестрСобытий.Ответственный").Видимость = Ложь;

	
	МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(Кэш.ГлавноеОкно, "Таблица_РеестрСобытий.Расхождение").Видимость = Ложь;	
	МодульОбъектаКлиент().ПолучитьЭлементФормыОбработки(Кэш.ГлавноеОкно, "Таблица_РеестрСобытий.СтатусВГосСистеме").Видимость = Ложь;	
	
	МассивРеквизитов = Новый Массив; 
	МассивРеквизитов.Добавить("Перевозчик");
	МассивРеквизитов.Добавить("Заказчик");
	
	Кэш.ГлавноеОкно.НастроитьКолонкиФормы(Новый Структура("ИмяТаблицы, СтруктураПолей", "Таблица_РеестрСобытий", Новый Структура("КолонкиДобавить", МассивРеквизитов)));
	
КонецПроцедуры

&НаКлиенте
Процедура НавигацияУстановитьПанель(Кэш) Экспорт
	ГлавноеОкно = Кэш.ГлавноеОкно;
	сбисЭлементФормы(ГлавноеОкно,"ПанельНавигации").Видимость=Истина;
	сбисЭлементФормы(ГлавноеОкно,"ЗаписейНаСтранице1С").Видимость=Ложь;
	сбисЭлементФормы(ГлавноеОкно,"ЗаписейНаСтранице").Видимость=Истина;
КонецПроцедуры	

// Процедура устанавливает значения фильтра по-умолчанию для текущего раздела	
&НаКлиенте
Процедура ФильтрОчистить(Кэш) Экспорт
	ГлавноеОкно = Кэш.ГлавноеОкно;
	
	ОписаниеТиповФильтра = МодульОбъектаКлиент().ПолучитьЗначениеПараметраТекущегоСеанса("ТипыПолейФильтра");
	Если Не ТипЗнч(ОписаниеТиповФильтра) = Тип("Структура") Тогда
		ОписаниеТиповФильтра = Новый Структура;
	КонецЕсли;
	
	Если ТипЗнч(ЭтаФорма) = Тип("УправляемаяФорма") Тогда
		ГлавноеОкно.ФильтрПериод = "За весь период";
	Иначе
		ГлавноеОкно.ФильтрПериод = "0";
	КонецЕсли;
	Если ОписаниеТиповФильтра.Свойство("ФильтрОрганизация") Тогда
		ГлавноеОкно.ФильтрОрганизация = ОписаниеТиповФильтра.ФильтрОрганизация.ПривестиЗначение();
	Иначе	
		ГлавноеОкно.ФильтрОрганизация = "";
	КонецЕсли;
	Если ОписаниеТиповФильтра.Свойство("ФильтрКонтрагент") Тогда
		ГлавноеОкно.ФильтрКонтрагент = ОписаниеТиповФильтра.ФильтрКонтрагент.ПривестиЗначение();
	Иначе	
		ГлавноеОкно.ФильтрКонтрагент = "";
	КонецЕсли;
	Если ОписаниеТиповФильтра.Свойство("ФильтрОтветственный") Тогда
		ГлавноеОкно.ФильтрОтветственный = ОписаниеТиповФильтра.ФильтрОтветственный.ПривестиЗначение();
	Иначе	
		ГлавноеОкно.ФильтрОтветственный = "";
	КонецЕсли;
	ГлавноеОкно.ФильтрДатаНач = "";
	ГлавноеОкно.ФильтрДатаКнц = "";
	ГлавноеОкно.ФильтрСостояние = ГлавноеОкно.СписокСостояний.НайтиПоИдентификатору(0).Значение;
	ГлавноеОкно.ФильтрТипыДокументов = Новый СписокЗначений;
	ГлавноеОкно.ФильтрКонтрагентПодключен = "";
	ГлавноеОкно.ФильтрКонтрагентСФилиалами = Ложь;
	ГлавноеОкно.ФильтрСтраница = 1;
	ГлавноеОкно.ФильтрМаска = "";
	//++ Бухов А. Фильтр по умолчанию 	
	Если Кэш.Ини.Конфигурация.Свойство("ФильтрПоУмолчанию") И  Кэш.Ини.Конфигурация.ФильтрПоУмолчанию.Свойство(Кэш.Текущий.ТипДок) Тогда 
		Попытка
			Ини = Кэш.ОбщиеФункции.ПолучитьДанныеДокумента1С(Кэш.Ини.Конфигурация.ФильтрПоУмолчанию[Кэш.Текущий.ТипДок],Неопределено,Кэш.КэшЗначенийИни, Кэш.Парам);  // alo Меркурий
			Для Каждого Элем Из Ини Цикл 
				Если нрег(Лев(Элем.Ключ, 6)) = "фильтр" Тогда
					ГлавноеОкно[Элем.Ключ] = Элем.Значение;
				КонецЕсли;
			КонецЦикла;
		Исключение
		КонецПопытки;
	КонецЕсли;
	//-- Бухов А. Фильтр по умолчанию
КонецПроцедуры


// Функция заполняет данные о прохождении документа	
&НаКлиенте
Функция ЗаполнитьПрохождение(СоставПакета) Экспорт
	Возврат МестныйКэш.ОбщиеФункции.ЗаполнитьПрохождение(МестныйКэш, СоставПакета, Новый Структура);
КонецФункции

&НаКлиенте
Процедура НаСменуРаздела(Кэш) Экспорт 
	
	ПодготовитьФильтрРаздела(Кэш);
	
	ПанельМассовыхОпераций = сбисЭлементФормы(Кэш.ГлавноеОкно,"ПанельМассовыхОпераций");
	ПанельМассовыхОпераций.ТекущаяСтраница = Кэш.ГлавноеОкно.сбисПолучитьСтраницу(ПанельМассовыхОпераций,"Полученные_ЭТрН");
	
КонецПроцедуры

// Процедура для заполнения списков выбора в фильтре. Восстанавливает последние установки фильтра раздела
// 
// Параметры:
//  Кэш  - Структура - Кэш обработки
//
&НаКлиенте
Процедура ПодготовитьФильтрРаздела(Кэш) Экспорт

	СписокСостояний = Новый СписокЗначений();
	СписокСостояний.Добавить("Все документы");
	СписокСостояний.Добавить("Утвержденные");
	СписокСостояний.Добавить("Отклоненные");
	СписокСостояний.Добавить("Удаленные");
	СписокСостояний.Добавить("Черновики");
			
	Кэш.ГлавноеОкно.СписокСостояний = СписокСостояний;
	
	Кэш.ГлавноеОкно.ФильтрОбновитьПанель()
	
КонецПроцедуры // ПодготовитьФильтрРаздела()

&НаКлиенте
Функция сбисСписокДополнительныхОпераций(Кэш, ФормаПросмотра) Экспорт
	СписокДопОпераций = Новый СписокЗначений;
	СписокДопОпераций.Добавить("СбисПечать", "Печать");
	СписокДопОпераций.Добавить("СохранитьНаДиск", "Сохранить на диск");
	СписокДопОпераций.Добавить("ОткрытьДокументОнлайн", "Открыть документ на sbis.ru");
	СписокДопОпераций.Добавить("ОткрытьКонтрагентаОнлайнПоПакету", "Открыть контрагента на sbis.ru");
	ФормаПросмотра.СервисДопОперацияПросмотра(СписокДопОпераций);	// alo доп операции из инишки Сервис
	Возврат СписокДопОпераций;
КонецФункции
