
// Процедура - Показывает форму в отдельном окне, не зависимо от общего интерфейса
//
// Параметры:
//  ПараметрыОткрытия	 - Структура	 - Параметры для настройки и открытия формы
//
&НаКлиенте 
Процедура Показать(ПараметрыОткрытия = Неопределено) Экспорт

	ПараметрыФормы = Новый Структура("Инициирована", Ложь);
	ФормаЗакрытьУстановитьСтатусОтмена();
	
	//Необходимо реализовать оббработчики СбисУстановитьФорму и СбисЗаблокироватьИнтерфейс на текущем модуле 
	Попытка
		
		СбисУстановитьФорму(ПараметрыОткрытия);
		ПараметрыФормы.Инициирована = Истина;
		БлокировкаИнтерфейсаВыполнена = СбисЗаблокироватьИнтерфейс(РежимОткрытияОкнаФормы.Независимый, ПараметрыОткрытия);
		
	Исключение
		
		ИсключениеПоказать = МодульОбъектаКлиент().НовыйСбисИсключение(ИнформацияОбОшибке(), НазваниеКратко() + ".Показать", 776);
		МодульОбъектаКлиент().СообщитьСбисИсключение(ИсключениеПоказать, Новый Структура("Отложенно", Истина));
		Возврат;
		
	КонецПопытки;
	
	Открыть();

КонецПроцедуры

// Процедура - Показать поверх
//
// Параметры:
//  ПараметрыОткрытия	 - Структура	 - Параметры для настройки и открытия формы
//
&НаКлиенте 
Процедура ПоказатьПоверх(ПараметрыОткрытия = Неопределено) Экспорт
	
	Если ВладелецФормы = Неопределено Тогда
		
		ИсключениеОткрытия = МодульОбъектаКлиент().НовыйСбисИсключение(	776, 
																		НазваниеКратко() + ".ПоказатьПоверх",
																		,
																		,
																		,
																		"Для открытия формы поверх необходимо, чтобы был установлен владелец.");
		МодульОбъектаКлиент().СообщитьСбисИсключение(ИсключениеОткрытия, Новый Структура("ИмяКоманды, Отложенно", НазваниеКратко() + ".ПоказатьПоверх", Истина));
		Возврат;
		
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("Инициирована", Ложь);
	ФормаЗакрытьУстановитьСтатусОтмена();
		
	//Необходимо реализовать оббработчики СбисУстановитьФорму и СбисЗаблокироватьИнтерфейс на текущем модуле 
	
	Попытка
		
		СбисУстановитьФорму(ПараметрыОткрытия); 
		ПараметрыФормы.Инициирована = Истина;
		БлокировкаИнтерфейсаВыполнена = СбисЗаблокироватьИнтерфейс(РежимОткрытияОкнаФормы.БлокироватьОкноВладельца, ПараметрыОткрытия);
		
	Исключение
		
		МодульОбъектаКлиент().СообщитьСбисИсключение(ИнформацияОбОшибке(), Новый Структура("ИмяКоманды, Отложенно", НазваниеКратко() + ".ПоказатьПоверх", Истина));
		Возврат;
		
	КонецПопытки;
	
	Если Не БлокировкаИнтерфейсаВыполнена Тогда
		
		#Если ТолстыйКлиентОбычноеПриложение Тогда
			
			ОткрытьМодально();//Для ОФ требуется самостоятельная реализация блокировки окна владельца. Пока можно открыть форму модально.
			Возврат;
			
		#КонецЕсли
		
		ИсключениеОткрытия = МодульОбъектаКлиент().НовыйСбисИсключение(776, НазваниеКратко() + ".ПоказатьПоверх",,,,"Не удалось заблокировать окно владельца!");
		МодульОбъектаКлиент().СообщитьСбисИсключение(ИсключениеОткрытия, Новый Структура("ИмяКоманды, Отложенно", НазваниеКратко() + ".ПоказатьПоверх", Истина));
		Возврат;
		
	КонецЕсли;
	
	Открыть();

КонецПроцедуры

